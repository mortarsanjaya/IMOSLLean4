/-
Copyright (c) 2026 Gian Cordana Sanjaya. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Gian Cordana Sanjaya
-/

import IMOSLLean4.Main.IMO2024.N6
import Mathlib.RingTheory.Artinian.Ring
import Mathlib.Algebra.CharP.CharAndCard

/-!
# IMO 2024 N6 (Generalization)

Given a ring $R$, we say that a function $f : R ‚Üí R$ is *good* if
  there exists $a, b, c ‚àà R$ such that for any $r ‚àà R$,
$$ (ar^2 + br + c)(f(r) - (ar^2 + br + c)) ‚â† 0. $$
We say that $R$ is *nice* if every polynomial over $R$ is good.
Given a finite commutative ring $R$, determine whether $R$ is nice or not.

### Answer

We say that a ring $R$ is *boolean* if $r^2 = r$ for all $r ‚àà R$.
Then a finite commutative ring is nice if and only if it is not boolean.
-/

namespace IMOSL
namespace IMO2024N6

open Finset Polynomial

/-!
### Every finite field of characteristic 2 other than `ùîΩ‚ÇÇ` is good

Throughout this section, `F` is a finite field of characteristic `2`.
-/

/-- We have `a^2 + a = b^2 + b` iff `a = b` or `a = b + 1`. -/
theorem sq_add_self_eq_sq_add_self_iff [CommRing R] [IsDomain R] [CharP R 2] {a b : R} :
    a ^ 2 + a = b ^ 2 + b ‚Üî a = b ‚à® a = b + 1 := by
  rw [‚Üê CharTwo.add_eq_zero, add_add_add_comm, ‚Üê CharTwo.add_sq, sq, ‚Üê mul_add_one,
    mul_eq_zero, CharTwo.add_eq_zero, add_assoc, CharTwo.add_eq_zero]


namespace FiniteField

variable {F} [Field F] [Fintype F] [DecidableEq F]
local notation "q" => Fintype.card F


namespace CharTwo

variable [CharP F 2]

/-- The set of `x : F` such that `x^2 + x = t^2 + t` is `{t, t + 1}`. -/
theorem filter_sq_add_self_eq (t : F) :
    ({x | x ^ 2 + x = t ^ 2 + t} : Finset F) = {t, t + 1} := by
  refine ext Œª x ‚Ü¶ ?_
  rw [mem_filter_univ, sq_add_self_eq_sq_add_self_iff, mem_insert, mem_singleton]

/-- If `y = x^2 + x` for some `x : F`, then there are two choices of `x` that work. -/
theorem card_sq_add_self_eq {y : F} (hy : ‚àÉ t, t ^ 2 + t = y) :
    #{x | x ^ 2 + x = y} = 2 := by
  rcases hy with ‚ü®t, rfl‚ü©
  rw [filter_sq_add_self_eq, card_pair (succ_ne_self t).symm]

/-- The set of elements of `F` of the form `x^2 + x` has cardinality `q/2`. -/
theorem two_mul_card_image_sq_add_self : 2 * #(univ.image Œª x : F ‚Ü¶ x ^ 2 + x) = q :=
  calc 2 * #(univ.image Œª x : F ‚Ü¶ x ^ 2 + x)
  _ = ‚àë y ‚àà univ.image Œª x ‚Ü¶ x ^ 2 + x, 2 := by
    rw [Nat.mul_comm, sum_const]; rfl
  _ = ‚àë y ‚àà univ.image Œª x ‚Ü¶ x ^ 2 + x, #{x | x ^ 2 + x = y} := by
    refine sum_congr rfl Œª y hy ‚Ü¶ (card_sq_add_self_eq ?_).symm
    rwa [mem_image_univ_iff_mem_range, Set.mem_range] at hy
  _ = q := (card_eq_sum_card_image _ _).symm

/-- The set of elements of `F` of the form `x^2 + x` has cardinality `q/2`. -/
theorem card_image_sq_add_self : #(univ.image Œª x : F ‚Ü¶ x ^ 2 + x) = q / 2 := by
  rw [‚Üê two_mul_card_image_sq_add_self, Nat.mul_div_cancel_left _ Nat.two_pos]

/-- If `q ‚â† 2`, then there exists `x‚ÇÄ ‚â† 1` in `F` not of the form `x^2 + x`. -/
theorem exists_ne_one_ne_sq_add_self (hF : q ‚â† 2) : ‚àÉ x‚ÇÄ ‚â† 1, ‚àÄ x : F, x ^ 2 + x ‚â† x‚ÇÄ := by
  ---- It suffices to show that `#({1} ‚à™ {x^2 + x : x ‚àà F}) < q`.
  suffices #(insert 1 (univ.image Œª x : F ‚Ü¶ x ^ 2 + x)) < q by
    obtain ‚ü®x‚ÇÄ, -, hx‚ÇÄ‚ü© : ‚àÉ x‚ÇÄ ‚àà univ, x‚ÇÄ ‚àâ insert 1 (univ.image Œª x : F ‚Ü¶ x ^ 2 + x) :=
      exists_mem_notMem_of_card_lt_card this
    rw [mem_insert, mem_image_univ_iff_mem_range, Set.mem_range, not_or, not_exists] at hx‚ÇÄ
    exact ‚ü®x‚ÇÄ, hx‚ÇÄ‚ü©
  ---- Note that `2 ‚à£ q` and `q/2 > 1`.
  have hF0 : ringChar F = 2 := ringChar.eq F 2
  replace hF0 : 2 ‚à£ q := Nat.dvd_of_mod_eq_zero (FiniteField.even_card_of_char_two hF0)
  replace hF : 1 < q / 2 :=
    (Nat.lt_div_iff_mul_lt' hF0 1).mpr (Nat.lt_of_le_of_ne Fintype.one_lt_card hF.symm)
  ---- Now we calculate.
  calc #(insert 1 (univ.image Œª x : F ‚Ü¶ x ^ 2 + x))
    _ ‚â§ #(univ.image Œª x : F ‚Ü¶ x ^ 2 + x) + 1 := card_insert_le _ _
    _ = q / 2 + 1 := by rw [card_image_sq_add_self]
    _ < q / 2 + q / 2 := Nat.add_lt_add_left hF _
    _ = q := by rw [‚Üê Nat.two_mul, Nat.mul_div_cancel' hF0]

omit [DecidableEq F] in
/-- If `x‚ÇÄ` is not of the form `x^2 + x`, then `‚àè_{x ‚àà F} (x^2 + x - x‚ÇÄ) = 1`. -/
theorem prod_sq_add_self_sub_eq_one {x‚ÇÄ : F} (hx‚ÇÄ : ‚àÄ x, x ^ 2 + x ‚â† x‚ÇÄ) :
    ‚àè x, (x ^ 2 + x - x‚ÇÄ) = 1 := by
  suffices (‚àè x, (x ^ 2 + x - x‚ÇÄ)) ^ 2 = ‚àè x, (x ^ 2 + x - x‚ÇÄ) by
    rw [sq, mul_left_eq_self‚ÇÄ] at this
    exact this.resolve_right (prod_ne_zero_iff.mpr Œª x _ ‚Ü¶ sub_ne_zero_of_ne (hx‚ÇÄ x))
  calc (‚àè x, (x ^ 2 + x - x‚ÇÄ)) ^ 2
    _ = ‚àè x, (x ^ 2 + x - x‚ÇÄ) ^ 2 := (prod_pow _ _ _).symm
    _ = ‚àè x, ((x ^ 2) ^ 2 + x ^ 2 + x‚ÇÄ ^ 2) :=
      Fintype.prod_congr _ _ Œª _ ‚Ü¶ by
        rw [CharTwo.sub_eq_add, CharTwo.add_sq, CharTwo.add_sq]
    _ = ‚àè x, (x ^ 2 + x + x‚ÇÄ ^ 2) :=
      (frobeniusEquiv F 2).prod_comp (Œª x ‚Ü¶ x ^ 2 + x + x‚ÇÄ ^ 2)
    _ = ‚àè x, ((x + x‚ÇÄ) ^ 2 + (x + x‚ÇÄ) + x‚ÇÄ ^ 2) :=
      (Equiv.prod_comp (Equiv.addRight x‚ÇÄ) _).symm
    _ = ‚àè x, (x ^ 2 + x - x‚ÇÄ) := by
      refine Fintype.prod_congr _ _ Œª x ‚Ü¶ ?_
      rw [CharTwo.add_sq, add_add_add_comm, add_assoc, CharTwo.sub_eq_add,
        add_right_inj, add_comm, CharTwo.add_cancel_left]

omit [CharP F 2] in
/-- Let `f : F ‚Üí F` be a function such that `f(0) = f(1)` and `f(F) = FÀ£`.
  Then `‚àè_{x ‚â† 0} f(x) = ‚àè_{x ‚â† 0} x`. (This holds even if `char(F) ‚â† 2`.) -/
theorem prod_univ_erase_zero_eq_one_of_image
    {f : F ‚Üí F} (hf : f 0 = f 1) (hf0 : ‚àÄ r, f r ‚â† 0) (hf1 : ‚àÄ x ‚â† 0, ‚àÉ r, f r = x) :
    ‚àè x with x ‚â† 0, f x = ‚àè x with x ‚â† 0, x := by
  ---- Define `g : F ‚Üí F` by `g(x) = f(x)` if `x ‚â† 0` and `g(0) = 0`.
  let g (x : F) : F := if x = 0 then 0 else f x
  ---- Then `g` is surjective.
  have hg : g.Surjective := by
    intro y
    obtain rfl | hy : y = 0 ‚à® y ‚â† 0 := eq_or_ne _ _
    ¬∑ exact ‚ü®0, if_pos rfl‚ü©
    obtain ‚ü®x, rfl‚ü© : ‚àÉ x, f x = y := hf1 y hy
    obtain rfl | hx : x = 0 ‚à® x ‚â† 0 := eq_or_ne _ _
    exacts [‚ü®1, (if_neg one_ne_zero).trans hf.symm‚ü©, ‚ü®x, if_neg hx‚ü©]
  ---- But then `g` is bijective.
  replace hg : g.Bijective := hg.bijective_of_finite
  ---- Now do the calculations.
  calc ‚àè x with x ‚â† 0, f x
    _ = ‚àè x with x ‚â† 0, g x :=
      prod_congr rfl Œª x hx ‚Ü¶ (if_neg ((mem_filter_univ x).mp hx)).symm
    _ = ‚àè x with x ‚â† 0, x := by
      refine prod_bijective g hg (Œª r ‚Ü¶ ?_) (Œª _ _ ‚Ü¶ rfl)
      simp_rw [g, mem_filter_univ, Ne, ite_eq_left_iff, _root_.not_imp, iff_self_and]
      rintro -; exact hf0 r

/-- If `q ‚â† 2`, then every function from `F` to itself is good. -/
theorem good_of_card_ne_two (hF : q ‚â† 2) : ‚àÄ f : F ‚Üí F, good f := by
  ---- Reduce to the case where `f(0) = f(1)` and `f(F) = FÀ£`.
  refine good_of_forall_map_pair_eq_of_image_eq_units zero_ne_one Œª f hf hf0 hf1 ‚Ü¶ ?_
  ---- Pick some `x‚ÇÄ ‚â† 1` such that `x‚ÇÄ ‚â† x^2 + x` for all `x`.
  obtain ‚ü®x‚ÇÄ, hx‚ÇÄ1, hx‚ÇÄ‚ü© : ‚àÉ x‚ÇÄ ‚â† 1, ‚àÄ x : F, x ^ 2 + x ‚â† x‚ÇÄ :=
    exists_ne_one_ne_sq_add_self hF
  ---- Define `g(x) = f(x)/(x^2 + x - x‚ÇÄ)` for every `x : F`.
  let g (x : F) : F := f x / (x ^ 2 + x - x‚ÇÄ)
  ---- Then it suffices to show that `g` does not attain some non-zero value.
  suffices ‚àÉ a ‚â† 0, ‚àÄ x, g x ‚â† a by
    rcases this with ‚ü®a, ha, ha0‚ü©
    refine ‚ü®a, a, a * -x‚ÇÄ, Œª x ‚Ü¶ ?_‚ü©
    have hx : x ^ 2 + x - x‚ÇÄ ‚â† 0 := sub_ne_zero_of_ne (hx‚ÇÄ x)
    have hx0 : f x ‚â† a * (x ^ 2 + x - x‚ÇÄ) := mt (div_eq_of_eq_mul hx) (ha0 x)
    rw [‚Üê mul_add, ‚Üê mul_add, ‚Üê sub_eq_add_neg, mul_ne_zero_iff]
    exact ‚ü®mul_ne_zero ha hx, sub_ne_zero_of_ne hx0‚ü©
  ---- Suppose for the sake of contradiction that every `a ‚â† 0` takes that form.
  by_contra! hg1
  ---- Note that `g(0) = g(1)` and `g` cannot attain zero.
  have hg : g 0 = g 1 := by
    refine congrArg‚ÇÇ _ hf ?_
    rw [sq, zero_mul, zero_add, one_pow, CharTwo.add_self_eq_zero]
  have hg0 (r : F) : g r ‚â† 0 :=
    div_ne_zero (hf0 r) (sub_ne_zero_of_ne (hx‚ÇÄ r))
  ---- Thus we have `‚àè_{x ‚â† 0} g(x) = ‚àè_{x ‚â† 0} f(x) = ‚àè_{x ‚â† 0} x`.
  replace h : ‚àè x with x ‚â† 0, g x = ‚àè x with x ‚â† 0, f x :=
    (prod_univ_erase_zero_eq_one_of_image hg hg0 hg1).trans
      (prod_univ_erase_zero_eq_one_of_image hf hf0 hf1).symm
  ---- But then `‚àè_{x ‚â† 0} (x^2 + x - x‚ÇÄ) = 1`.
  replace h : ‚àè x with x ‚â† 0, (x ^ 2 + x - x‚ÇÄ) = 1 := by
    rw [prod_div_distrib, div_eq_mul_inv, mul_right_eq_self‚ÇÄ, inv_eq_one] at h
    exact h.resolve_right (prod_ne_zero_iff.mpr Œª r _ ‚Ü¶ hf0 r)
  ---- Thus `1 = ‚àè_x (x^2 + x - x‚ÇÄ) = x‚ÇÄ`; contradiction.
  replace h : x‚ÇÄ = 1 := calc
    _ = (‚àè x ‚â† 0, (x ^ 2 + x - x‚ÇÄ)) * (0 ^ 2 + 0 - x‚ÇÄ) := by
      rw [‚Üê filter_ne', h, one_mul, sq, zero_mul, zero_add, zero_sub, CharTwo.neg_eq]
    _ = ‚àè x, (x ^ 2 + x - x‚ÇÄ) := prod_erase_mul _ _ (mem_univ _)
    _ = 1 := prod_sq_add_self_sub_eq_one hx‚ÇÄ
  exact hx‚ÇÄ1 h

/-- If `q ‚â† 2`, then `F` is nice. -/
theorem nice_of_card_ne_two (hF : q ‚â† 2) : nice F :=
  Œª P ‚Ü¶ good_of_card_ne_two hF P.eval

end CharTwo





/-! ### Every finite field other than `ùîΩ‚ÇÇ` is good -/

/-- A finite field of cardinality `> 2` is nice. -/
theorem nice_of_card_ne_two (hF : q ‚â† 2) : nice F := by
  obtain hF0 | hF0 : ringChar F = 2 ‚à® ringChar F ‚â† 2 := eq_or_ne _ _
  ---- Case 1: `char(F) = 2`.
  ¬∑ haveI : CharP F 2 := CharP.congr _ hF0
    exact CharTwo.nice_of_card_ne_two hF
  ---- Case 2: `char(F) ‚â† 2`.
  ¬∑ exact nice_of_char_ne_two hF0

omit [DecidableEq F] in
/-- Let `F` be a finite field of cardinality `2`. Then `F` is not nice. -/
theorem not_nice_of_card_eq_two (hF : q = 2) : ¬¨nice F :=
  let œÜ : ZMod 2 ‚âÉ+* F := ZMod.ringEquivOfPrime F Nat.prime_two hF
  Œª hF0 ‚Ü¶ not_nice_ZMod2 (hF0.of_surjection œÜ œÜ.surjective)

/-- A finite field if nice if and only if it has cardinality not equal to `2`. -/
theorem nice_iff_card_ne_two : nice F ‚Üî Fintype.card F ‚â† 2 :=
  ‚ü®Œª hF hF0 ‚Ü¶ not_nice_of_card_eq_two hF0 hF, nice_of_card_ne_two‚ü©

end FiniteField





/-! ### More properties of boolean rings -/

namespace IsBoolean

/-- The product of boolean rings is boolean. -/
theorem Pi {R : Œπ ‚Üí Type*} [‚àÄ i, Ring (R i)] (hf : ‚àÄ i, IsBoolean (R i)) :
    IsBoolean ((i : Œπ) ‚Üí R i) :=
  Œª x ‚Ü¶ funext Œª i ‚Ü¶ hf i (x i)

/-- A ring that injects into a boolean ring is boolean. -/
theorem of_injection [Ring R] [Ring S] (hS : IsBoolean S)
    (œÜ : R ‚Üí+* S) (hœÜ : Function.Injective œÜ) : IsBoolean R :=
  Œª x ‚Ü¶ hœÜ ((œÜ.map_pow x 2).trans (hS _))

/-- The boolean property is preserved under ring isomorphism. -/
theorem of_RingEquiv [Ring R] [Ring S] (hR : IsBoolean R) (œÜ : R ‚âÉ+* S) : IsBoolean S :=
  hR.of_injection œÜ.symm.toRingHom œÜ.symm.injective

/-- A ring of cardinality `2` is boolean. -/
theorem of_card_eq_two [Ring R] [Fintype R] (hR : Fintype.card R = 2) : IsBoolean R :=
  ZMod2.of_RingEquiv (ZMod.ringEquivOfPrime R Nat.prime_two hR)

/-- A reduced finite (commutative) ring whose maximal ideals have index `2` is boolean. -/
theorem of_reduced_maximal_quotient_card [CommRing R] [Fintype R] [IsReduced R]
    (hR : ‚àÄ m : MaximalSpectrum R, Fintype.card (R ‚ß∏ m.asIdeal) = 2) : IsBoolean R :=
  of_RingEquiv (Pi Œª m ‚Ü¶ of_card_eq_two (hR m)) (IsArtinianRing.equivPi R).symm

end IsBoolean





/-! ### More theory of Artinian rings -/

/-- The intersection of finitely many pairwise coprime ideals is equal to their product. -/
theorem Ideal_inf_eq_prod_of_pairwise_isCoprime [CommSemiring R] [DecidableEq Œπ]
    (S : Finset Œπ) (f : Œπ ‚Üí Ideal R) (hf : Pairwise (Function.onFun IsCoprime f)) :
    S.inf f = ‚àè i ‚àà S, f i := by
  ---- Immediate by induction on `S`.
  induction S using Finset.induction with
  | empty => exact Ideal.one_eq_top.symm
  | insert i S hiS S_ih =>
      have h : IsCoprime (f i) (‚®Ö a ‚àà S, f a) :=
        Ideal.isCoprime_biInf Œª j hj ‚Ü¶ hf (ne_of_mem_of_not_mem hj hiS).symm
      replace h : f i ‚äì ‚®Ö a ‚àà S, f a = f i * ‚®Ö a ‚àà S, f a := Ideal.inf_eq_mul_of_isCoprime h
      rw [inf_insert, prod_insert hiS, ‚Üê S_ih, Finset.inf_eq_iInf, h]

/-- The maximal ideals of a ring are pairwise coprime. -/
theorem Pairwise_isCoprime_maximalSpectrum [CommSemiring R] :
    Pairwise (Function.onFun IsCoprime (MaximalSpectrum.asIdeal (R := R))) :=
  Œª _ _ h ‚Ü¶ Ideal.isCoprime_of_isMaximal (mt MaximalSpectrum.ext_iff.mpr h)


section

open Classical

variable [CommRing R] [IsArtinianRing R]

/-- A local instance stating that `MaxSpec` of an artinian ring is a `Fintype`.
  We need this to express the Jacobson radical as a product. -/
noncomputable local instance : Fintype (MaximalSpectrum R) :=
  Fintype.ofFinite (MaximalSpectrum R)

/-- If `R` is artinian, its Jacobson radical is the product of all maximal ideals. -/
theorem jacobson_eq_prod_MaximalSpectrum :
    Ring.jacobson R = ‚àè m : MaximalSpectrum R, m.asIdeal :=
  calc Ring.jacobson R
  _ = sInf {m : Ideal R | m.IsMaximal} := Ring.jacobson_eq_sInf_isMaximal R
  _ = ‚®Ö m : {m : Ideal R | m.IsMaximal}, ‚Üëm := sInf_eq_iInf' _
  _ = iInf MaximalSpectrum.asIdeal :=
    (MaximalSpectrum.equivSubtype R).symm.iInf_congr Œª _ ‚Ü¶ rfl
  _ = univ.inf MaximalSpectrum.asIdeal := (inf_univ_eq_iInf _).symm
  _ = ‚àè m : MaximalSpectrum R, m.asIdeal :=
    Ideal_inf_eq_prod_of_pairwise_isCoprime _ _ Pairwise_isCoprime_maximalSpectrum

/-- An artinian ring `R` such that `m^2 = m` for every maximal ideal `m` is reduced. -/
theorem reduced_of_forall_maximal_sq_eq_self
    (hR : ‚àÄ m : MaximalSpectrum R, m.asIdeal ^ 2 = m.asIdeal) : IsReduced R := by
  ---- In this case the Jacobson radical `J` satisfies `J^2 = J`.
  replace hR : Ring.jacobson R ^ 2 = Ring.jacobson R := by
    simp_rw [jacobson_eq_prod_MaximalSpectrum, ‚Üê prod_pow, hR]
  ---- Letting `I` be the nilradical, in this case we have `J = I`, so `I^2 = I`.
  have h : Ring.jacobson R = nilradical R := by
    rw [‚Üê Ideal.jacobson_bot, IsArtinianRing.jacobson_eq_radical]; rfl
  replace hR : nilradical R ^ 2 = nilradical R := by rw [‚Üê h, hR]
  ---- By induction, we have `I^{n + 1} = I` for all `n`.
  replace hR (n : ‚Ñï) : nilradical R ^ (n + 1) = nilradical R := by
    induction n with
    | zero => exact pow_one _
    | succ n n_ih => rw [pow_succ, n_ih, ‚Üê sq, hR]
  ---- But `I` is nilpotent, so the above equality implies `I = 0` and we are done.
  obtain ‚ü®n, hn‚ü© : IsNilpotent (nilradical R) := IsArtinianRing.isNilpotent_nilradical
  replace hR : nilradical R = 0 := by rw [‚Üê hR n, pow_succ, hn, zero_mul]
  exact nilradical_eq_bot_iff.mp hR

end





/-! ### All non-boolean rings are nice -/

/-- If `m` is a maximal ideal, `a ‚àâ m^2`, and `b ‚àâ m`, then `ab ‚àâ m^2`. -/
theorem mul_notMem_sq_of_left [CommSemiring R] {m : Ideal R} (hm : m.IsMaximal)
    {a b : R} (ha : a ‚àâ m ^ 2) (hb : b ‚àâ m) : a * b ‚àâ m ^ 2 := by
  intro hab
  have ha0 : a * b ‚àà m := m.pow_le_self (Nat.succ_ne_zero 1) hab
  replace ha0 : a ‚àà m := by_contra Œª h ‚Ü¶ hm.isPrime.mul_notMem h hb ha0
  obtain ‚ü®c, d, hd, h‚ü© : ‚àÉ c, ‚àÉ d ‚àà m, c * b + d = 1 := hm.exists_inv hb
  have h0 : c * (a * b) + a * d ‚àà m ^ 2 :=
    add_mem (Ideal.mul_mem_left _ _ hab) (sq m ‚ñ∏ Ideal.mul_mem_mul ha0 hd)
  rw [mul_left_comm, ‚Üê mul_add, h, mul_one] at h0
  exact ha h0

/-- If `m` is a ideal such that `R/m` has characteristic `2`,
  then for any `a, b : R` with `a - b ‚àà m`, we have `a^2 - b^2 ‚àà m^2`. -/
theorem sq_sub_sq_mem_sq [CommRing R] (m : Ideal R) [CharP (R ‚ß∏ m) 2]
    {a b : R} (hab : a - b ‚àà m) : a ^ 2 - b ^ 2 ‚àà m ^ 2 := by
  have hm : 2 ‚àà m := Ideal.Quotient.eq_zero_iff_mem.mp (CharP.ofNat_eq_zero _ 2)
  have hab0 : a + b ‚àà m := by
    rw [‚Üê sub_add_add_cancel a b b, ‚Üê two_mul, m.add_mem_iff_right hab]
    exact m.mul_mem_right _ hm
  rw [sq_sub_sq, sq]
  exact Ideal.mul_mem_mul hab0 hab

/-- If `m^2 < m` and `#(R/m) = 2` for some maximal ideal `m < R`, then `R` is nice. -/
theorem nice_of_maximal_sq_lt_card_quotient [CommRing R] [Fintype R] [DecidableEq R]
    {m : Ideal R} (hm : m.IsMaximal) (hm0 : m ^ 2 < m) (hm1 : Fintype.card (R ‚ß∏ m) = 2) :
    nice R := by
  haveI : CharP (R ‚ß∏ m) 2 := charP_of_card_eq_prime hm1
  ---- For convenience, let `œÜ : R ‚Üí R/m` denote the projection map.
  let œÜ : R ‚Üí+* R ‚ß∏ m := Ideal.Quotient.mk m
  have hœÜ : Function.Surjective œÜ := Ideal.Quotient.mk_surjective
  ---- Also, we note that `R/m = {0, 1}`, so for all `r`, one of `r` or `r - 1` is in `m`.
  have hm2 (x : R ‚ß∏ m) : x = 0 ‚à® x = 1 :=
    eq_zero_or_one_of_sq_eq_self (IsBoolean.of_card_eq_two hm1 x)
  have hm3 (r : R) : r ‚àà m ‚à® r - 1 ‚àà m :=
    (hm2 (œÜ r)).imp Ideal.Quotient.eq_zero_iff_mem.mp
      (Ideal.Quotient.mk_eq_one_iff_sub_mem _).mp
  ---- Now start; fix a polynomial `P : R[X]`.
  intro P
  ---- Find `a, c : R` with `c, a + c ‚àâ m^2` and `œÜ(P(0) - c) = œÜ(P(1) - (a + c)) = 1`.
  obtain ‚ü®a, c, hm4, hm5‚ü© : ‚àÉ a c, (c ‚àâ m ^ 2 ‚àß a + c ‚àâ m ^ 2) ‚àß
      ((P.map œÜ).eval 0 - œÜ c = 1 ‚àß (P.map œÜ).eval 1 - œÜ (a + c) = 1) := by
    -- Take an arbitrary `œÄ ‚àà m \ m^2` and prove some miscellaneous lemmas.
    obtain ‚ü®œÄ, hœÄm, hœÄm0‚ü© : ‚àÉ œÄ ‚àà m, œÄ ‚àâ m ^ 2 := Set.exists_of_ssubset hm0
    have hœÄm1 : œÜ œÄ = 0 := Ideal.Quotient.eq_zero_iff_mem.mpr hœÄm
    have hm4 : 1 ‚àâ m := Œª h ‚Ü¶ hm.ne_top (m.eq_top_iff_one.mpr h)
    have hm5 {r} (hr : r ‚àâ m) : r ‚àâ m ^ 2 := Œª h ‚Ü¶ hr (hm0.le h)
    -- Now we divide into four cases based on `œÜ(P(0))` and `œÜ(P(1))`.
    obtain hP0 | hP0 : (P.map œÜ).eval 0 = 0 ‚à® (P.map œÜ).eval 0 = 1 := hm2 _
    all_goals obtain hP1 | hP1 : (P.map œÜ).eval 1 = 0 ‚à® (P.map œÜ).eval 1 = 1 := hm2 _
    -- Case 1: `œÜ(P(0)) = 0` and `œÜ(P(1)) = 0`; then choose `a = 0` and `c = 1`.
    ¬∑ refine ‚ü®0, 1, ?_‚ü©
      rw [zero_add, and_self, hP0, hP1, and_self, zero_sub, œÜ.map_one]
      exact ‚ü®hm5 hm4, CharTwo.neg_eq _‚ü©
    -- Case 2: `œÜ(P(0)) = 0` and `œÜ(P(1)) = 1`; then choose `a = 1` and `c = œÄ - 1`.
    ¬∑ refine ‚ü®1, œÄ - 1, ?_‚ü©
      rw [add_sub_cancel, hP0, hP1, œÜ.map_sub, hœÄm1, œÜ.map_one]
      exact ‚ü®‚ü®hm5 Œª h ‚Ü¶ hm4 ((m.sub_mem_iff_right hœÄm).mp h), hœÄm0‚ü©,
        ‚ü®sub_sub_cancel _ _, sub_zero _‚ü©‚ü©
    -- Case 3: `œÜ(P(0)) = 1` and `œÜ(P(1)) = 0`; then choose `a = 1` and `c = œÄ`.
    ¬∑ refine ‚ü®1, œÄ, ‚ü®hœÄm0, hm5 Œª h ‚Ü¶ hm4 ((m.add_mem_iff_left hœÄm).mp h)‚ü©, ?_‚ü©
      rw [hP0, hP1, œÜ.map_add, hœÄm1, œÜ.map_one, sub_add_cancel_right]
      exact ‚ü®sub_zero _, CharTwo.neg_eq _‚ü©
    -- Case 4: `œÜ(P(0)) = 1` and `œÜ(P(1)) = 1`; then choose `a = 0` and `c = œÄ`.
    ¬∑ refine ‚ü®0, œÄ, ?_‚ü©
      rw [zero_add, and_self, hP0, hP1, and_self, hœÄm1]
      exact ‚ü®hœÄm0, sub_zero _‚ü©
  ---- From `a, a + c ‚àâ m^2` we get `ar^2 + c ‚àâ m^2` for any `r : R`.
  replace hm4 (r : R) : a * r ^ 2 + c ‚àâ m ^ 2 := by
    intro hr
    obtain hr0 | hr0 : r ‚àà m ‚à® r - 1 ‚àà m := hm3 r
    -- If `r ‚àà m` then `ar^2 + c ‚â° c ‚â¢ 0 (mod m^2)`.
    ¬∑ replace hr0 : r ^ 2 ‚àà m ^ 2 := Ideal.pow_mem_pow hr0 2
      exact hm4.1 ((Ideal.add_mem_iff_right _ (Ideal.mul_mem_left _ a hr0)).mp hr)
    -- If `r - 1 ‚àà m` then `ar^2 + c ‚â° a + c ‚â¢ 0 (mod m^2)`.
    ¬∑ replace hr0 : r ^ 2 - 1 ^ 2 ‚àà m ^ 2 := sq_sub_sq_mem_sq _ hr0
      replace hr0 : a * r ^ 2 + c - a * (r ^ 2 - 1 ^ 2) ‚àà m ^ 2 :=
        Ideal.sub_mem _ hr (Ideal.mul_mem_left _ a hr0)
      rw [one_pow, mul_sub_one, add_sub_sub_cancel, add_comm] at hr0
      exact hm4.2 hr0
  ---- From `œÜ(P(0) - c) = œÜ(P(1) - (a + c)) = 1` we get `P(r) - (ar^2 + c) ‚àâ m` for any `r`.
  replace hm5 (r : R) : P.eval r - (a * r ^ 2 + c) ‚àâ m := by
    intro hr
    replace hr : (P.map œÜ).eval (œÜ r) - (œÜ a * œÜ r ^ 2 + œÜ c) = 0 := by
      rw [eval_map_apply, ‚Üê œÜ.map_pow, ‚Üê œÜ.map_mul, ‚Üê œÜ.map_add, ‚Üê œÜ.map_sub]
      exact Ideal.Quotient.eq_zero_iff_mem.mpr hr
    obtain hr0 | hr0 : r ‚àà m ‚à® r - 1 ‚àà m := hm3 r
    -- If `r ‚àà m` then `P(r) - (ar^2 + c) ‚â° P(0) - c ‚â¢ 0 (mod m)`.
    ¬∑ replace hr0 : œÜ r = 0 := Ideal.Quotient.eq_zero_iff_mem.mpr hr0
      rw [hr0, sq, zero_mul, mul_zero, zero_add, hm5.1] at hr
      exact one_ne_zero hr
    -- If `r ‚àà m` then `P(r) - (ar^2 + c) ‚â° P(1) - (a + c) ‚â¢ 0 (mod m)`.
    ¬∑ replace hr0 : œÜ r = 1 := (Ideal.Quotient.mk_eq_one_iff_sub_mem _).mpr hr0
      rw [hr0, one_pow, mul_one, ‚Üê œÜ.map_add, hm5.2] at hr
      exact one_ne_zero hr
  ---- Thus picking the same `a` and `c` with `b = 0` works.
  refine ‚ü®a, 0, c, Œª r hr ‚Ü¶ ?_‚ü©
  replace hr : (a * r ^ 2 + c) * (P.eval r - (a * r ^ 2 + c)) = 0 := by
    rwa [zero_mul, add_zero] at hr
  replace h : (a * r ^ 2 + c) * (P.eval r - (a * r ^ 2 + c)) ‚àâ m ^ 2 :=
    mul_notMem_sq_of_left hm (hm4 r) (hm5 r)
  exact h (hr ‚ñ∏ Ideal.zero_mem _)

open Classical in
/-- A finite commutative ring that is not nice is boolean. -/
theorem IsBoolean.of_not_nice [CommRing R] [Fintype R] [DecidableEq R] (hR : ¬¨nice R) :
    IsBoolean R := by
  ---- For any maximal ideal `m`, since `R/m` is a field and not nice, `#(R/m) = 2`.
  have hR0 (m : MaximalSpectrum R) : Fintype.card (R ‚ß∏ m.asIdeal) = 2 := by
    have h : nice (R ‚ß∏ m.asIdeal) ‚Üî Fintype.card (R ‚ß∏ m.asIdeal) ‚â† 2 :=
      @FiniteField.nice_iff_card_ne_two _ (Ideal.Quotient.field m.asIdeal) _ _
    have h0 : ¬¨nice (R ‚ß∏ m.asIdeal) :=
      Œª hm ‚Ü¶ hR (hm.of_surjection _ Ideal.Quotient.mk_surjective)
    exact (iff_not_comm.mp h).mpr h0
  ---- By `nice_of_maximal_sq_lt_card_quotient`, we have `m^2 = m` for all `m`.
  replace hR (m : MaximalSpectrum R) : m.asIdeal ^ 2 = m.asIdeal :=
    (Ideal.pow_le_self (Nat.succ_ne_zero 1)).lt_or_eq.resolve_left
      Œª hm ‚Ü¶ hR (nice_of_maximal_sq_lt_card_quotient m.isMaximal hm (hR0 m))
  ---- Since `R` is artinian (finite), we get that `R` is reduced.
  haveI : IsReduced R := reduced_of_forall_maximal_sq_eq_self hR
  ---- We are now done by `IsBoolean.of_reduced_maximal_quotient_card`.
  exact of_reduced_maximal_quotient_card hR0





/-! ### Summary -/

section

variable [CommRing R] [Fintype R] [DecidableEq R]

/-- A finite commutative ring is nice iff it is not boolean. -/
theorem nice_iff_not_IsBoolean : nice R ‚Üî ¬¨IsBoolean R :=
  ‚ü®Œª hR hR0 ‚Ü¶ hR0.not_nice hR, Œª hR ‚Ü¶ by_contra Œª hR0 ‚Ü¶ hR (IsBoolean.of_not_nice hR0)‚ü©

/-- Final solution to the generalized version -/
theorem Generalization.final_solution : nice R ‚Üî ¬¨‚àÄ r : R, r ^ 2 = r :=
  nice_iff_not_IsBoolean

end
