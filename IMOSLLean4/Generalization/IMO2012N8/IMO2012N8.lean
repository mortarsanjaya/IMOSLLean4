/-
Copyright (c) 2025 Gian Cordana Sanjaya. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Gian Cordana Sanjaya
-/

import IMOSLLean4.Main.IMO2012.N8
import Mathlib.Data.Nat.Factorization.PrimePow

/-!
# IMO 2012 N8 (Generalization)

Find all finite fields $F$ with the following property:
  for any $r âˆˆ F$, there exists $a, b âˆˆ F$ such that $a^2 + b^5 = r$.

### Answer

Any field of cardinality $q â‰  11$.

### Implementation details

The condition being asked on $F$ is implemented via the predicate `good`.
For the case $q = 11$ and $q = 31$, we show that `good` is preserved under field isomorphisms
  via `good.of_RingEquiv`, so we just have to check the fields `ZMod 11` and `ZMod 31`.
For `ZMod 11`, we show by computer search that $a^2 + b^5 â‰  7$ for any $a, b âˆˆ F$.
For `ZMod 31`, we show that every element of $ð”½_{31}$, other than $22 = 4^2 - 5^5$ and
  $27 = 1^2 + 6^5$, takes the form $a^2$, $a^2 + 1$, or $a^2 - 6^5$ for some $a âˆˆ ð”½_{31}$.
-/

namespace IMOSL
namespace IMO2012N8

open Finset

/-- Auxiliary definition, which is not given in the main file since they aren't as
  focused on representing elements of `F` specifically as `a^2 + b^5`. -/
def good (R) [Add R] [Monoid R] := âˆ€ r : R, âˆƒ a b, a ^ 2 + b ^ 5 = r

/-- If two rings, `R` and `S`, are isomorphic and `R` is good, then `S` is good. -/
theorem good.of_RingEquiv [Semiring R] [Semiring S] (hR : good R) (Ï† : R â‰ƒ+* S) :
    good S := by
  ---- Pick any `s âˆˆ S` and write `s = Ï†(r)` for some `r âˆˆ R`.
  intro s; obtain âŸ¨r, rflâŸ© : âˆƒ r : R, Ï† r = s := Ï†.surjective s
  ---- Now `r = a^2 + b^5` for some `a, b âˆˆ R`.
  obtain âŸ¨a, b, rflâŸ© : âˆƒ a b : R, a ^ 2 + b ^ 5 = r := hR r
  ---- Then `s = Ï†(r) = Ï†(a)^2 + Ï†(b)^5`.
  refine âŸ¨Ï† a, Ï† b, ?_âŸ©
  rw [Ï†.map_add, Ï†.map_pow, Ï†.map_pow]



/-! ### Classifying good finite fields -/

variable {F} [Field F] [Fintype F] [DecidableEq F]
local notation "q" => Fintype.card F

/-- A finite field of cardinality `q > 40` is `good`. -/
theorem good_of_card_big_enough (hF : 40 < q) : good F := by
  obtain hF0 | hF0 : ringChar F â‰  2 âˆ¨ ringChar F = 2 := ne_or_eq _ _
  ---- We have done the `char(F) â‰  2` case.
  Â· exact IMO2012N8.exists_eq_sq_add_pow_five hF0 hF
  ---- If `char(F) = 2`, then everything is already a square and we are done.
  Â· intro r
    obtain âŸ¨a, rflâŸ© : âˆƒ a, r = a ^ 2 := (FiniteField.isSquare_of_char_two hF0 r).exists_sq r
    refine âŸ¨a, 0, ?_âŸ©
    rw [zero_pow (Nat.succ_ne_zero 4), add_zero]

/-- If `n` is coprime with `q - 1`, then every element of `F` is an `n`th power. -/
theorem exists_eq_pow_n_of_gcd_eq_one (hn : n â‰  0) (h : Nat.Coprime n (q - 1)) (x : F) :
    âˆƒ y : F, y ^ n = x := by
  ---- If `x = 0` then the statement is trivial.
  obtain rfl | hx : x = 0 âˆ¨ IsUnit x := (eq_or_ne x 0).imp_right Ne.isUnit
  Â· exact âŸ¨0, zero_pow hnâŸ©
  ---- If `x` is a unit then `x^k` works where `k` is an integer with `nk â‰¡ 1 (mod q - 1)`.
  lift x to FË£ using hx
  refine âŸ¨x ^ n.gcdA (q - 1), ?_âŸ©
  calc (x.1 ^ n.gcdA (q - 1)) ^ n
    _ = x.1 ^ (n * n.gcdA (q - 1)) := by rw [Int.mul_comm, zpow_mul, zpow_natCast]
    _ = (x ^ (n * n.gcdA (q - 1))).1 := by rw [Units.val_zpow_eq_zpow_val]
    _ = x.1 := congrArg Units.val ?_
  calc x ^ (n * n.gcdA (q - 1))
    _ = x ^ (n.gcd (q - 1) - (q - 1 : â„•) * n.gcdB (q - 1) : â„¤) := by
      rw [Nat.gcd_eq_gcd_ab, Int.add_sub_cancel]
    _ = x * ((x ^ (q - 1)) ^ n.gcdB (q - 1))â»Â¹ := by
      rw [zpow_sub, h, Int.natCast_one, zpow_one, zpow_mul, zpow_natCast]
    _ = x := by rw [â† Fintype.card_units, pow_card_eq_one, one_zpow, inv_one, mul_one]

/-- A finite field of cardinality `q â‰¢ 1 (mod 2)` is `good`. -/
theorem good_of_card_mod_2_ne_one (hF : Â¬2 âˆ£ q - 1) : good F := by
  intro r
  ---- Find `a âˆˆ F` such that `a^2 = r`.
  obtain âŸ¨a, rflâŸ© : âˆƒ a, a ^ 2 = r :=
    exists_eq_pow_n_of_gcd_eq_one (Nat.succ_ne_zero 1)
      (Nat.prime_two.coprime_iff_not_dvd.mpr hF) _
  ---- Then `b = 0` works, as `a^2 + 0^5 = r`.
  refine âŸ¨a, 0, ?_âŸ©
  rw [zero_pow (Nat.succ_ne_zero 4), add_zero]

/-- A finite field of cardinality `q â‰¢ 1 (mod 5)` is `good`. -/
theorem good_of_card_mod_5_ne_one (hF : Â¬5 âˆ£ q - 1) : good F := by
  intro r
  ---- Find `b âˆˆ F` such that `b^5 = r`.
  obtain âŸ¨b, rflâŸ© : âˆƒ b, b ^ 5 = r :=
    exists_eq_pow_n_of_gcd_eq_one (Nat.succ_ne_zero 4)
      (Nat.prime_five.coprime_iff_not_dvd.mpr hF) _
  ---- Then `a = 0` works, as `0^2 + b^5 = r`.
  refine âŸ¨0, b, ?_âŸ©
  rw [zero_pow (Nat.succ_ne_zero 1), zero_add]

/-- A finite field of cardinality `q â‰¢ 1 (mod 10)` is `good`. -/
theorem good_of_card_mod_10_ne_one (hF : Â¬10 âˆ£ q - 1) : good F := by
  obtain h | h : Â¬2 âˆ£ q - 1 âˆ¨ Â¬5 âˆ£ q - 1 := by rwa [â† not_and_or, â† Nat.lcm_dvd_iff]
  exacts [good_of_card_mod_2_ne_one h, good_of_card_mod_5_ne_one h]

/-- `ZMod 11` is not `good`. -/
theorem ZMod11_is_not_good : Â¬good (ZMod 11) :=
  not_forall_of_exists_not âŸ¨7, by decideâŸ©

omit [DecidableEq F] in
/-- A field of cardinality `11` is not `good`. -/
theorem not_good_of_card_eq_11 (hF : q = 11) : Â¬good F :=
  Î» h â†¦ ZMod11_is_not_good
    (h.of_RingEquiv (ZMod.ringEquivOfPrime F Nat.prime_eleven hF).symm)

/-- `ZMod 31` is `good`. -/
theorem ZMod31_is_good : good (ZMod 31) := by
  intro r
  /- All elements of `ð”½_{31}` are of the form
    `a^2`, `a^2 + 1`, or `a^2 + 5`, except `22` and `27`. -/
  obtain âŸ¨a, rfl | rfl | rflâŸ© | rfl | rfl :
    (âˆƒ a, a ^ 2 = r âˆ¨ a ^ 2 + 1 = r âˆ¨ a ^ 2 + 5 = r) âˆ¨ (r = 22 âˆ¨ r = 27) := by
      decide +revert
  ---- Now just brute force all the cases.
  exacts [âŸ¨a, 0, add_zero _âŸ©, âŸ¨a, 1, rflâŸ©, âŸ¨a, -6, rflâŸ©, âŸ¨4, -5, rflâŸ©, âŸ¨1, 6, rflâŸ©]

/-- The integer `31` is prime. -/
theorem prime_31 : Nat.Prime 31 := by decide

omit [DecidableEq F] in
/-- A field of cardinality `31` is `good`. -/
theorem good_of_card_eq_31 (hF : q = 31) : good F :=
  ZMod31_is_good.of_RingEquiv (ZMod.ringEquivOfPrime F prime_31 hF)

/-- The integer `21` is not a prime power. -/
theorem not_IsPrimePow_21 : Â¬IsPrimePow 21 := by
  ---- Somehow direct decision procedute doesn't work anymore...
  intro h
  replace h0 : Nat.Coprime 3 7 := rfl
  replace h : 21 âˆ£ 3 âˆ¨ 21 âˆ£ 7 := (h0.isPrimePow_dvd_mul h).mp (Nat.dvd_refl 21)
  revert h; decide

/-- Final solution to the generalized version -/
theorem Generalization.final_solution : good F â†” Â¬q = 11 := by
  ---- As fields of cardinality `11` are not good, we now assume `q â‰  11`.
  refine âŸ¨Î» hF hF0 â†¦ not_good_of_card_eq_11 hF0 hF, Î» hF â†¦ ?_âŸ©
  ---- If `10 âˆ¤ q - 1`, then we proved that `F` is good.
  obtain hF0 | âŸ¨k, hâŸ© : Â¬10 âˆ£ q - 1 âˆ¨ 10 âˆ£ q - 1 := dec_em' _
  Â· exact good_of_card_mod_10_ne_one hF0
  ---- Now write `q = 10k + 1`.
  replace h : q = 10 * k + 1 := Nat.eq_add_of_sub_eq Fintype.card_pos h
  ----  If `k â‰¥ 4` then `q > 40` and so `F` is good.
  obtain h0 | h0 : 4 â‰¤ k âˆ¨ k < 4 := le_or_gt _ _
  Â· apply good_of_card_big_enough
    calc 40 â‰¤ 10 * k := Nat.mul_le_mul_left 10 h0
         _  < 10 * k + 1 := Nat.lt_succ_self _
         _  = q := h.symm
  ---- If `k < 4`, then divide into four cases: `q = 1, 11, 21, 31`, respectively.
  lift k to Fin 4 using h0
  fin_cases k
  exacts [
    absurd h Fintype.one_lt_card.ne.symm,
    absurd h hF,
    absurd (FiniteField.isPrimePow_card F) (h â–¸ not_IsPrimePow_21),
    good_of_card_eq_31 h]
