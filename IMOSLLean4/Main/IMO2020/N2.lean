/-
Copyright (c) 2025 Gian Cordana Sanjaya. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Gian Cordana Sanjaya
-/

import Mathlib.Combinatorics.SimpleGraph.Acyclic

/-!
# IMO 2020 N2

For any finite commutative ring $R$, let $G_R$ be the (simple) graph with vertex set $R$,
  where two distinct vertices $x, y ‚àà R$ are connected by an edge if and only if
  either $y = x^2 + 1$ or $x = y^2 + 1$.
Prove that the graph $G_{ùîΩ_p}$ is disconnected for infinitely many primes $p$.

### Solution

We follow Solution 1 of the
  [official solution](https://www.imo-official.org/problems/IMO2020SL.pdf).
With the same proof, we show more: $G_{ùîΩ_p}$ is disconnected if $p ‚â° 1 (mod 3)$.
-/

namespace IMOSL
namespace IMO2020N2

open Finset

/-- The graph `G_R`, defined for all commutative rings `R`. -/
def RGraph (R) [CommRing R] : SimpleGraph R where
  Adj x y := x ‚â† y ‚àß (x = y ^ 2 + 1 ‚à® y = x ^ 2 + 1)
  symm _ _ h := ‚ü®h.1.symm, h.2.symm‚ü©
  loopless _ h := h.1 rfl

/-- An unordered pair is an edge of `G_R` if and only if it has the form `(x^2 + 1, x)`. -/
theorem mem_edgeSet_RGraph_iff [CommRing R] :
    e ‚àà (RGraph R).edgeSet ‚Üî ‚àÉ x, x ^ 2 + 1 ‚â† x ‚àß e = s(x^2 + 1, x) := by
  refine ‚ü®Œª h ‚Ü¶ ?_, Œª h ‚Ü¶ ?_‚ü©
  ---- The `‚Üí` direction.
  ¬∑ induction e with | h x y => ?_
    change x ‚â† y ‚àß (x = y ^ 2 + 1 ‚à® y = x ^ 2 + 1) at h
    rcases h with ‚ü®h, rfl | rfl‚ü©
    exacts [‚ü®y, h, rfl‚ü©, ‚ü®x, h.symm, Sym2.eq_swap‚ü©]
  ---- The `‚Üê` direction.
  ¬∑ rcases h with ‚ü®x, hx, rfl‚ü©
    exact ‚ü®hx, Or.inl rfl‚ü©


section

variable [CommRing R] [Fintype R] [DecidableEq R]

/-- The number of edges of `G_R` is less than or equal to
  the number of `x ‚àà R` with `x^2 + 1 ‚â† x`. -/
theorem card_RGraph_le : Nat.card (RGraph R).edgeSet ‚â§ #{x : R | x ^ 2 + 1 ‚â† x} := by
  ---- Consider `f : {x : x^2 + 1 ‚â† x} ‚Ü¶ E(G_R)` defined by `f(x) = (x^2 + 1, x)`.
  let f (x : ({x : R | x ^ 2 + 1 ‚â† x} : Finset R)) : (RGraph R).edgeSet :=
    ‚ü®s(x ^ 2 + 1, x), mem_edgeSet_RGraph_iff.mpr ‚ü®x, (mem_filter.mp x.2).2, rfl‚ü©‚ü©
  ---- Then `f` is surjective.
  have hf : f.Surjective := by
    rintro ‚ü®e, he‚ü©
    obtain ‚ü®x, hx, rfl‚ü© : ‚àÉ x, x ^ 2 + 1 ‚â† x ‚àß e = s(x^2 + 1, x) :=
      mem_edgeSet_RGraph_iff.mp he
    exact ‚ü®‚ü®x, (mem_filter_univ _).mpr hx‚ü©, rfl‚ü©
  ---- Now do the calculations.
  calc Nat.card (RGraph R).edgeSet
    _ ‚â§ Nat.card ({x : R | x ^ 2 + 1 ‚â† x} : Finset R) :=
      Nat.card_le_card_of_surjective f hf
    _ = #{x : R | x ^ 2 + 1 ‚â† x} := Nat.card_eq_finsetCard _

/-- If `x^2 + 1 = x` for some `x ‚àà R` with `2x ‚â† 1`, then `G_R` is disconnected. -/
theorem RGraph_disconnected_of_cyclotomic3_has_root
    {x : R} (hx : x ^ 2 + 1 = x) (hx0 : 2 * x ‚â† 1) : ¬¨(RGraph R).Connected := by
  ---- It suffices to show that `G_R` has less than `|R| - 1` edges.
  refine mt SimpleGraph.Connected.card_vert_le_card_edgeSet_add_one (Nat.not_le_of_gt ?_)
  ---- First note that `(1 - x)^2 + 1 = 1 - x`, and `2x ‚â† 1` means `x ‚â† 1 - x`.
  have hx1 : (1 - x) ^ 2 + 1 = 1 - x := by
    rw [sub_sq, one_pow, mul_one, add_assoc, hx, two_mul, ‚Üê sub_sub, sub_add_cancel]
  replace hx0 : x ‚â† 1 - x := by rwa [Ne, eq_sub_iff_add_eq, ‚Üê two_mul]
  ---- Now do the calculations.
  calc Nat.card (RGraph R).edgeSet + 1
    _ < #{x : R | x ^ 2 + 1 ‚â† x} + 2 :=
      Nat.add_lt_add_of_le_of_lt card_RGraph_le Nat.one_lt_two
    _ = #{x : R | x ^ 2 + 1 ‚â† x} + #{x, 1 - x} := by rw [card_pair hx0]
    _ = #(({x : R | x ^ 2 + 1 ‚â† x} : Finset R) ‚à™ {x, 1 - x}) := by
      refine (card_union_of_disjoint (disjoint_right.mpr Œª t ht ‚Ü¶ ?_)).symm
      rw [mem_filter, and_iff_right (mem_univ _), Decidable.not_not]
      rw [mem_insert, mem_singleton] at ht
      rcases ht with rfl | rfl <;> assumption
    _ ‚â§ #(univ : Finset R) := card_le_card (subset_univ _)
    _ = Nat.card R := by rw [card_univ, Fintype.card_eq_nat_card]

/-- There exists infinitely many primes `p` such that `x^2 + 1 = x` for some `x ‚àà ùîΩ_p`. -/
theorem exists_infinite_prime_cyclotomic3_has_root (N) :
    ‚àÉ p > N, Nat.Prime p ‚àß ‚àÉ x : ZMod p, x ^ 2 + 1 = x := by
  /- We take `p` to be the minimal prime factor of `M^2 + 1 - M`, where `M = 2N!`.
    Note that we choose `M` in this way so that `M^2 + 1 - M > 1`. -/
  let M := 2 * N.factorial
  have hM : M + 1 < M ^ 2 + 1 := by
    rw [Nat.pow_two, Nat.add_lt_add_iff_right, Nat.lt_mul_self_iff]
    exact Nat.le_mul_of_pos_right 2 (Nat.factorial_pos _)
  have hM0 : M ‚â§ M ^ 2 + 1 := Nat.le_of_lt hM.le
  have hM1 : (M ^ 2 + 1 - M).minFac ‚à£ M ^ 2 + 1 - M := Nat.minFac_dvd _
  refine ‚ü®(M ^ 2 + 1 - M).minFac, ?_, ?_, ?_‚ü©
  ---- First show that `M^2 + 1 - M` has no prime factor less than or equal to `N`.
  ¬∑ refine Nat.lt_of_not_ge Œª h ‚Ü¶ ?_
    replace h : (M ^ 2 + 1 - M).minFac ‚à£ M :=
      calc (M ^ 2 + 1 - M).minFac
        _ ‚à£ N.factorial := Nat.dvd_factorial (Nat.minFac_pos _) h
        _ ‚à£ 2 * N.factorial := Nat.dvd_mul_left N.factorial 2
    rw [Nat.pow_two] at hM hM0 hM1 h
    rw [Nat.dvd_sub_iff_left hM0 h, Nat.dvd_add_right (Nat.dvd_mul_right_of_dvd h M),
      Nat.dvd_one, Nat.minFac_eq_one_iff, Nat.sub_eq_iff_eq_add' hM0] at hM1
    exact hM.ne.symm hM1
  ---- Next show that `p` is indeed prime.
  ¬∑ exact Nat.minFac_prime_iff.mpr (Nat.lt_sub_iff_add_lt'.mpr hM).ne.symm
  ---- Finally, show that `x = M (mod p)` works.
  ¬∑ refine ‚ü®M, ?_‚ü©
    rwa [‚Üê Nat.cast_pow, ‚Üê Nat.cast_add_one, ‚Üê sub_eq_zero,
      ‚Üê Nat.cast_sub hM0, ZMod.natCast_eq_zero_iff]

/-- Final solution -/
theorem final_solution (N) : ‚àÉ p > N, Nat.Prime p ‚àß ¬¨(RGraph (ZMod p)).Connected := by
  ---- Choose a prime `p > max{N, 3}` such that `x^2 + 1 = x` for some `x ‚àà ùîΩ_p`.
  obtain ‚ü®p, hpN, hp, ‚ü®x, hx‚ü©‚ü© : ‚àÉ p > max N 3, Nat.Prime p ‚àß ‚àÉ x : ZMod p, x ^ 2 + 1 = x :=
    exists_infinite_prime_cyclotomic3_has_root _
  haveI h : NeZero p := NeZero.of_gt hpN
  refine ‚ü®p, (le_max_left _ _).trans_lt hpN, hp,
    RGraph_disconnected_of_cyclotomic3_has_root hx ?_‚ü©
  ---- The only work we need to do is to check that `2x ‚â† 1`, which is due to `p > 3`.
  replace hpN : ¬¨p ‚à£ 3 :=
    Nat.not_dvd_of_pos_of_lt (Nat.succ_pos 2) ((Nat.le_max_right _ _).trans_lt hpN)
  replace hx : (2 * x) ^ 2 + 1 + 3 = 2 * (2 * x) := by
    rw [add_assoc, add_comm 1, three_add_one_eq_four, ‚Üê two_add_two_eq_four,
      ‚Üê two_mul, mul_pow, sq, ‚Üê mul_add_one, hx, mul_assoc]
  intro h; rw [h, one_pow, two_mul, add_eq_left] at hx
  exact hpN ((ZMod.natCast_eq_zero_iff _ _).mp hx)
