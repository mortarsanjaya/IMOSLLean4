/-
Copyright (c) 2025 Gian Cordana Sanjaya. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Gian Cordana Sanjaya
-/

import Mathlib.FieldTheory.Finite.Basic
import Mathlib.Algebra.Order.Chebyshev

/-!
# IMO 2012 N8

Let $p > 100$ be a prime number.
Prove that for any integer $r$, there exists two integers $a$ and $b$ such that
$$ p ‚à£ a^2 + b^5 - r. $$

### Solution

We follow Solution 1 of the official solution, with some simplification.
See [here](https://www.imo-official.org/problems/IMO2012SL.pdf).
However, we do things more generally, as follows.

Let $F$ be a finite field with $q$ elements.
Let $n$ be a positive integer, and suppose that $q > 2n(n - 1)$.
Then we show that for any $r ‚àà F$, there exists $a, b ‚àà F$ such that $a^2 + b^n = r$.
The original statement is recovered by taking $F = ùîΩ_p$.

Using the double-counting technique from Solution 1, we get that the number of elements
  of $F$ of the form $a^2 + b^n$ is at least $\dfrac{q^3}{(q - 1)(q + n) + 1}$.
We simplify the bound to $q - (n - 1)$.
Thus, we proved that number of elements **not** of the form $a^2 + b^n$ is at most $n - 1$.
To give a lower bound on the number of such elements, we consider the action of $FÀ£$
  only on the **units** $u$ not of the form $a^2 + b^n$, given by $g ‚Ä¢ u = g^{2n} u$.
By ...

Note that the case where $F$ has characteristic $2$ is trivial, as squaring is surjective.
We deal with this characteristic $2$ case separately in the beginning.

### Generalization

Actually, we can show more when $n = 5$: any $q ‚â† 11$ works.
The map $a ‚Ü¶ a^n$ is bijective on $F$ if $\gcd(q - 1, n) = 1$.
Thus, we are also done if $q$ is even or if $5 ‚à§ q - 1$.
The remaining case is $q ‚â° 1 (mod 10)$, for which $q ‚â§ 40$ implies $q ‚àà \{11, 31\}$.
By computer search, $q = 31$ succeeds, while $q = 11$ fails with $r = 7$.

See `Generalization/IMO2012N8/IMO2012N8.lean` for the implementation.

### TODO

Implement the generalization.
-/

namespace IMOSL
namespace IMO2012N8

open Finset

variable {F} [Field F] [Fintype F] [DecidableEq F]
local notation "q" => Fintype.card F

/-! ### The characteristic 2 case -/

omit [DecidableEq F] in
/-- If `char(F) = 2`, then every element is of the form `a^2 + f(b)`, whatever `f` is. -/
theorem exists_eq_sq_add_map_of_char_eq_two (hF : ringChar F = 2)
    [hŒ≤ : Nonempty Œ≤] (f : Œ≤ ‚Üí F) (r : F) :
    ‚àÉ a b, a ^ 2 + f b = r := by
  refine Nonempty.elim hŒ≤ Œª b : Œ≤ ‚Ü¶ ?_
  obtain ‚ü®a, ha‚ü© : IsSquare (r - f b) := FiniteField.isSquare_of_char_two hF _
  refine ‚ü®a, b, ?_‚ü©
  rw [sq, ‚Üê ha, sub_add_cancel]





/-! ### Double counting methods -/

/-- `Finset.card_eq_sum_card_fiberwise` with both sides being `Finset.univ`.
  TODO: Remove this lemma once it gets into `mathlib`, because it should. -/
theorem Fintype_card_eq_sum_card_fiberwise
    [Fintype Œ±] [Fintype Œ≤] [DecidableEq Œ≤] (f : Œ± ‚Üí Œ≤) :
    Fintype.card Œ± = ‚àë b, #{a | f a = b} :=
  card_eq_sum_card_fiberwise Œª _ _ ‚Ü¶ mem_univ _

/-- Cardinality of pairs `(i, j)` with `f(i) = g(j)` as a sum over fibers. -/
theorem Fintype_card_fiber_product_eq_fiberwise
    [Fintype Œ±] [Fintype Œ≤] [Fintype Œ∫] [DecidableEq Œ∫] (f : Œ± ‚Üí Œ∫) (g : Œ≤ ‚Üí Œ∫) :
    #{x : Œ± √ó Œ≤ | f x.1 = g x.2} = ‚àë k, #{a | f a = k} * #{b | g b = k} :=
  calc #{x : Œ± √ó Œ≤ | f x.1 = g x.2}
  _ = ‚àë x : Œ± √ó Œ≤, if f x.1 = g x.2 then 1 else 0 := (sum_boole _ _).symm
  _ = ‚àë a, ‚àë b, if f a = g b then 1 else 0 := Fintype.sum_prod_type _
  _ = ‚àë k, ‚àë a : {a // f a = k}, ‚àë b, if f a = g b then 1 else 0 :=
    (Fintype.sum_fiberwise _ _).symm
  _ = ‚àë k, #{a : Œ± | f a = k} * #{b : Œ≤ | g b = k} :=
    ---- Term-wise matching
    Fintype.sum_congr _ _ Œª k ‚Ü¶
      calc ‚àë a : {a // f a = k}, ‚àë b, if f a = g b then 1 else 0
      _ = ‚àë a : {a // f a = k}, ‚àë b, if g b = k then 1 else 0 :=
        Fintype.sum_congr _ _ Œª i ‚Ü¶ Fintype.sum_congr _ _ Œª j ‚Ü¶
          if_congr (by rw [i.2, eq_comm]) rfl rfl
      _ = #{a | f a = k} * #{b | g b = k} := by
        rw [sum_const, card_univ, Fintype.card_subtype,
          smul_eq_mul, sum_boole, Nat.cast_id]

/-- Cardinality of pairs `(i, j)` with `f(i) = f(j)` as a sum of squares. -/
theorem Fintype_card_eqpair_eq_fiberwise
    [Fintype Œπ] [Fintype Œ∫] [DecidableEq Œ∫] (f : Œπ ‚Üí Œ∫) :
    #{x : Œπ √ó Œπ | f x.1 = f x.2} = ‚àë k, #{i | f i = k} ^ 2 := by
  conv => right; right; ext; rw [sq]
  exact Fintype_card_fiber_product_eq_fiberwise f f

/-- Double-counting `4`-tuples `(i, j, i', j')` such that `f(i) + g(j) = f(i') + g(j')`,
  where `f : Œ± ‚Üí G` and `g : Œ≤ ‚Üí G` are functions to a finite abelian group `G`. -/
theorem Fintype_quad_fiber_product_double_count [Fintype Œ±] [Fintype Œ≤]
    [AddCommGroup G] [Fintype G] [DecidableEq G] (f : Œ± ‚Üí G) (g : Œ≤ ‚Üí G) :
    ‚àë x, #{p : Œ± √ó Œ± | f p.1 - f p.2 = x} * #{p : Œ≤ √ó Œ≤ | g p.1 - g p.2 = x}
      = ‚àë x, #{p : Œ± √ó Œ≤ | f p.1 + g p.2 = x} ^ 2 :=
  calc ‚àë x, #{p : Œ± √ó Œ± | f p.1 - f p.2 = x} * #{p : Œ≤ √ó Œ≤ | g p.1 - g p.2 = x}
  _ = ‚àë x, #{p : Œ± √ó Œ± | f p.1 - f p.2 = x} * #{p : Œ≤ √ó Œ≤ | g p.2 - g p.1 = x} :=
    Fintype.sum_congr _ _ Œª x ‚Ü¶ congrArg (_ * ¬∑) <|
      card_equiv (Equiv.prodComm Œ≤ Œ≤) Œª p ‚Ü¶ by simp only [mem_filter_univ]; rfl
  _ = #{p : (Œ± √ó Œ±) √ó (Œ≤ √ó Œ≤) | f p.1.1 - f p.1.2 = g p.2.2 - g p.2.1} :=
    (Fintype_card_fiber_product_eq_fiberwise
      (Œª p : Œ± √ó Œ± ‚Ü¶ f p.1 - f p.2) (Œª p : Œ≤ √ó Œ≤ ‚Ü¶ g p.2 - g p.1)).symm
  _ = #{p : (Œ± √ó Œ±) √ó (Œ≤ √ó Œ≤) | f p.1.1 + g p.2.1 = f p.1.2 + g p.2.2} := by
    conv => left; congr; congr; ext; rw [sub_eq_sub_iff_add_eq_add, add_comm (g _)]
  _ = #{p : (Œ± √ó Œ≤) √ó (Œ± √ó Œ≤) | f p.1.1 + g p.1.2 = f p.2.1 + g p.2.2} :=
    card_equiv (Equiv.prodProdProdComm Œ± Œ± Œ≤ Œ≤) Œª ((a, a'), (b, b')) ‚Ü¶ by
      simp only [Equiv.prodProdProdComm_apply, mem_filter_univ]
  _ = ‚àë x, #{p : Œ± √ó Œ≤ | f p.1 + g p.2 = x} ^ 2 :=
    Fintype_card_eqpair_eq_fiberwise (Œª p : Œ± √ó Œ≤ ‚Ü¶ f p.1 + g p.2)

/-- Cauchy-Schwarz inequality on the double-counting formula for `4`-tuples
  `(i, j, i', j')` such that `f(i) + g(j) = f(i') + g(j')`. -/
theorem Fintype_quad_fiber_product_CauchySchwarz [Fintype Œ±] [Fintype Œ≤]
    [AddCommGroup G] [Fintype G] [DecidableEq G] (f : Œ± ‚Üí G) (g : Œ≤ ‚Üí G) :
    (Fintype.card Œ± * Fintype.card Œ≤) ^ 2 ‚â§
      #{x | ‚àÉ a b, f a + g b = x} *
        ‚àë x, #{p : Œ± √ó Œ± | f p.1 - f p.2 = x} * #{p : Œ≤ √ó Œ≤ | g p.1 - g p.2 = x} :=
  calc (Fintype.card Œ± * Fintype.card Œ≤) ^ 2
  _ = (‚àë x with #{p : Œ± √ó Œ≤ | f p.1 + g p.2 = x} ‚â† 0,
        #{p : Œ± √ó Œ≤ | f p.1 + g p.2 = x}) ^ 2 := by
    rw [sum_filter_ne_zero, ‚Üê Fintype.card_prod, ‚Üê Fintype_card_eq_sum_card_fiberwise]
  _ ‚â§ #{x | #{p : Œ± √ó Œ≤ | f p.1 + g p.2 = x} ‚â† 0} *
        ‚àë x with #{p : Œ± √ó Œ≤ | f p.1 + g p.2 = x} ‚â† 0,
          #{p : Œ± √ó Œ≤ | f p.1 + g p.2 = x} ^ 2 :=
    sq_sum_le_card_mul_sum_sq
  _ = #{x | ‚àÉ a b, f a + g b = x} * ‚àë x, _ := by
    ---- Split into two parts.
    refine congrArg‚ÇÇ _ ?_ ?_
    ---- `#{(a, b) : f(a) + g(b) = x} ‚â† 0` iff `f(a) + g(b) = x` for some `a` and `b`.
    ¬∑ refine congrArg _ (ext Œª x ‚Ü¶ ?_)
      simp only [mem_filter_univ, card_ne_zero, Finset.Nonempty, Prod.exists]
    ---- Show a double counting identity.
    ¬∑ rw [sum_filter_of_ne Œª x _ ‚Ü¶ by exact Nat.ne_zero_of_mul_ne_zero_right,
        Fintype_quad_fiber_product_double_count]





/- ### Counting pairs `(a, b) ‚àà F^2` such that `a^2 - b^2 = r` when `char(F) ‚â† 2` -/

/-- Number of pairs `(a, b) ‚àà R^2` such that `ab = 0`, where `R` is a domain. -/
theorem card_mul_eq_zero [Ring R] [IsDomain R] [Fintype R] [DecidableEq R] :
    #{p : R √ó R | p.1 * p.2 = 0} = 2 * Fintype.card R - 1 :=
  calc #{p : R √ó R | p.1 * p.2 = 0}
  _ = #({(0 : R)} √óÀ¢ univ ‚à™ univ √óÀ¢ {(0 : R)}) := by
    refine congrArg card (ext Œª (a, b) ‚Ü¶ ?_)
    rw [mem_filter_univ, mul_eq_zero, mem_union]
    simp only [mem_product, mem_singleton, mem_univ, true_and, and_true]
  _ = #({(0 : R)} √óÀ¢ (univ : Finset R)) + #((univ : Finset R) √óÀ¢ {(0 : R)})
      - #({(0 : R)} √óÀ¢ univ ‚à© univ √óÀ¢ {(0 : R)}) :=
    card_union _ _
  _ = 2 * Fintype.card R - 1 := by
    rw [card_product, card_product, product_inter_product, inter_univ, univ_inter,
      card_product, card_singleton, card_univ, Nat.one_mul, Nat.mul_one, Nat.two_mul]

/-- Number of pairs `(a, b) ‚àà G^2` such that `ab = g`, where `g ‚àà G`; `G` is a group. -/
theorem card_group_antidiagonal [Group G] [Fintype G] [DecidableEq G] (g : G) :
    #{p : G √ó G | p.1 * p.2 = g} = Fintype.card G :=
  calc #{p : G √ó G | p.1 * p.2 = g}
  _ = ‚àë p : G √ó G, if p.1 * p.2 = g then 1 else 0 := (sum_boole _ _).symm
  _ = ‚àë a, ‚àë b, if a * b = g then 1 else 0 := Fintype.sum_prod_type _
  _ = ‚àë a, ‚àë b, if a‚Åª¬π * g = b then 1 else 0 := by
    conv => right; right; ext; right; ext; congr; rw [inv_mul_eq_iff_eq_mul, eq_comm]
  _ = ‚àë a : G, 1 := Fintype.sum_congr _ _ Œª a ‚Ü¶ Fintype.sum_ite_eq _ _
  _ = Fintype.card G := Fintype.card_eq_sum_ones.symm

/-- Number of pairs `(a, b) ‚àà R^2` such that `ab = r`, where `r ‚àà RÀ£`. -/
theorem card_mul_eq_unit [CommRing R] [Fintype R] [DecidableEq R] (r : RÀ£) :
    #{p : R √ó R | p.1 * p.2 = r} = Fintype.card RÀ£ := by
  ---- Consider the map `f : RÀ£ ‚Üí R √ó R` defined by `x ‚Ü¶ (x, x‚Åª¬π r)`.
  refine (card_nbij (Œª x : RÀ£ ‚Ü¶ (x.val, (x‚Åª¬π * r).val)) ?_ ?_ ?_).symm
  ---- We first need to show that the image of the map is in `{(a, b) : R √ó R | ab = r}`.
  ¬∑ rintro x -; rw [mem_coe, mem_filter_univ, ‚Üê Units.val_mul, mul_inv_cancel_left]
  ---- Next we need to show that the map is injective.
  ¬∑ rintro x - y - h; exact Units.val_inj.mp (congrArg Prod.fst h)
  ---- Finally, we need to show that the map surjects into `{(a, b) : R √ó R | ab = r}`.
  ¬∑ rintro ‚ü®x, y‚ü© h
    rw [mem_coe, mem_filter_univ] at h
    -- First lift `x` to a unit.
    lift x to RÀ£ using isUnit_of_mul_eq_one x (y * r‚Åª¬π) (by rw [‚Üê mul_assoc, h, r.mul_inv])
    refine ‚ü®x, mem_univ _, Prod.ext rfl ?_‚ü©
    -- Now it remains to show that `y = x‚Åª¬π r`.
    dsimp only; rw [Units.val_mul, ‚Üê h, Units.inv_mul_cancel_left]

/-- Number of pairs `(a, b) ‚àà F^2` such that `ab = r`. -/
theorem FiniteField_card_mul_fiber (r : F) :
    #{p : F √ó F | p.1 * p.2 = r} = if r = 0 then 2 * q - 1 else q - 1 := by
  ---- If `r = 0`, then the count holds since `F` is a domain. If `r ‚â† 0`, `r` is a unit.
  split_ifs with h
  ¬∑ exact h ‚ñ∏ card_mul_eq_zero
  ¬∑ lift r to FÀ£ using isUnit_iff_ne_zero.mpr h
    rw [card_mul_eq_unit, Fintype.card_units]

/-- Number of pairs `(a, b) ‚àà F^2` with `a^2 - b^2 = r`, where `char(F) ‚â† 2`. -/
theorem FiniteField_card_sq_sub_sq_fiber_of_two_ne_zero (hF : ringChar F ‚â† 2) (r : F) :
    #{p : F √ó F | p.1 ^ 2 - p.2 ^ 2 = r} = if r = 0 then 2 * q - 1 else q - 1 := by
  replace hF (a : F) : (a + a) / 2 = a := by
    rw [‚Üê two_mul, mul_div_cancel_left‚ÇÄ a (Ring.two_ne_zero hF)]
  ---- Constructing permutation `e` on `F √ó F` sending `(a, b)` to `(a + b, a - b)`.
  let e : F √ó F ‚âÉ F √ó F :=
    { toFun p := (p.1 + p.2, p.1 - p.2)
      invFun p := ((p.1 + p.2) / 2, (p.1 - p.2) / 2)
      left_inv p := by dsimp only; rw [add_add_sub_cancel, hF, add_sub_sub_cancel, hF]
      right_inv p := by dsimp only; rw [‚Üê add_div, add_add_sub_cancel, hF,
        ‚Üê sub_div, add_sub_sub_cancel, hF] }
  ---- The bijection implies `#{(a, b) | a^2 - b^2 = r} = #{(a, b) | ab = r} = RHS`.
  refine (card_equiv e Œª p ‚Ü¶ ?_).trans (FiniteField_card_mul_fiber r)
  rw [mem_filter_univ, mem_filter_univ, sq_sub_sq]; rfl

/-- Number of pairs `(a, b) ‚àà F^2` with `a^2 - b^2 = r`, where `char(F) ‚â† 2`. -/
theorem FiniteField_card_sq_sub_sq_fiber_of_two_ne_zero' (hF : ringChar F ‚â† 2) (r : F) :
    #{p : F √ó F | p.1 ^ 2 - p.2 ^ 2 = r} = q - 1 + if r = 0 then q else 0 := by
  rw [FiniteField_card_sq_sub_sq_fiber_of_two_ne_zero hF, add_ite,
    Nat.add_zero, ‚Üê Nat.self_add_sub_one, Nat.add_comm]





/-! ### Lower bound on the number of elements of the form `a^2 + b^n`, `n > 1` -/

section

open Polynomial

/-- Bound on the number of `a ‚àà F` such that `P(a) = r`. -/
theorem card_Polynomial_fiber_le_degree {P : F[X]} (hP : 0 < P.natDegree) (r : F) :
    #{a : F | P.eval a = r} ‚â§ P.natDegree :=
  have hP0 : 0 < P.degree := natDegree_pos_iff_degree_pos.mp hP
  calc #{a | P.eval a = r}
  _ ‚â§ (P - C r).roots.card :=
    Multiset.card_le_card <| Finset.val_le_iff_val_subset.mpr Œª a ‚Ü¶ by
      rw [mem_val, mem_filter_univ, mem_roots_sub_C hP0]; exact id
  _ ‚â§ P.natDegree := card_roots_sub_C' hP0

/-- Bound on the number of `(a, b) ‚àà F^2` such that `P(a) = P(b)`. -/
theorem card_Polynomial_eqpair_le_of_degree_mul_q {P : F[X]} (hP : 0 < P.natDegree) :
    #{p : F √ó F | P.eval p.1 = P.eval p.2} ‚â§ P.natDegree * q :=
  calc #{p : F √ó F | P.eval p.1 = P.eval p.2}
  _ = ‚àë r, #{a | P.eval a = r} * #{b | P.eval b = r} :=
    Fintype_card_fiber_product_eq_fiberwise P.eval P.eval
  _ ‚â§ ‚àë r, P.natDegree * #{b | P.eval b = r} :=
    Finset.sum_le_sum Œª r _ ‚Ü¶ Nat.mul_le_mul_right _ (card_Polynomial_fiber_le_degree hP r)
  _ = P.natDegree * q := by rw [‚Üê mul_sum, ‚Üê Fintype_card_eq_sum_card_fiberwise]

/-- Bound on the number of elements of `F` of the form `a^2 + f(b)`. -/
theorem card_sq_add_fn_fiber_lower_bound (hF : ringChar F ‚â† 2) (f : F ‚Üí F) :
    q ^ 3 ‚â§ #{r | ‚àÉ a b, a ^ 2 + f b = r} *
      ((q - 1) * q + #{p : F √ó F | f p.1 = f p.2}) := by
  ---- First de-cancel a factor of `q` from both sides.
  refine Nat.le_of_mul_le_mul_right (c := q) ?_ Fintype.card_pos
  ---- Reduce to evaluating `‚àë r, #{(a, b) | a^2 - b^2 = r} #{(a, b) | f(a) - f(b) = r}`.
  calc q ^ 4
    _ = (q * q) ^ 2 := by rw [‚Üê Nat.pow_two, ‚Üê Nat.pow_mul]
    _ ‚â§ #{r | ‚àÉ a b, a ^ 2 + f b = r} *
          ‚àë r, #{p : F √ó F | p.1 ^ 2 - p.2 ^ 2 = r} * #{p : F √ó F | f p.1 - f p.2 = r} :=
      Fintype_quad_fiber_product_CauchySchwarz _ f
    _ = _ * (((q - 1) * q + #{p : F √ó F | f p.1 = f p.2}) * q) := congrArg (_ * ¬∑) ?_
    _ = _ := (Nat.mul_assoc _ _ _).symm
  ---- Now evaluate `‚àë r, #{(a, b) | a^2 - b^2 = r} #{(a, b) | f(a) - f(b) = r}`.
  calc ‚àë r, #{p : F √ó F | p.1 ^ 2 - p.2 ^ 2 = r} * #{p : F √ó F | f p.1 - f p.2 = r}
    _ = ‚àë r, ((q - 1) * #{p : F √ó F | f p.1 - f p.2 = r} +
          if 0 = r then q * #{p : F √ó F | f p.1 - f p.2 = r} else 0) := by
      refine Fintype.sum_congr _ _ Œª r ‚Ü¶ ?_
      rw [FiniteField_card_sq_sub_sq_fiber_of_two_ne_zero' hF,
        Nat.add_mul, ite_zero_mul, if_congr eq_comm rfl rfl]
    _ = (q - 1) * ‚àë r, #{p : F √ó F | f p.1 - f p.2 = r} +
          q * #{p : F √ó F | f p.1 - f p.2 = 0} := by
      rw [sum_add_distrib, mul_sum, Fintype.sum_ite_eq]
    _ = (q - 1) * (q * q) + #{p : F √ó F | f p.1 = f p.2} * q :=
      congrArg‚ÇÇ _ (by rw [‚Üê Fintype_card_eq_sum_card_fiberwise, Fintype.card_prod])
        (by simp only [Nat.mul_comm q, sub_eq_zero])
    _ = ((q - 1) * q + #{p : F √ó F | f p.1 = f p.2}) * q := by
      rw [Nat.add_mul, Nat.mul_assoc]

/-- Bound on the number of elements of `F` of the form `a^2 + P(b)`, `P ‚àà F[X]`. -/
theorem card_sq_add_Polynomial_fiber_lower_bound {P : F[X]} (hP : 0 < P.natDegree) :
    q ^ 2 ‚â§ #{r | ‚àÉ a b, a ^ 2 + P.eval b = r} * (q + (P.natDegree - 1)) := by
  ---- The case `char(F) = 2` is easier.
  obtain hF | hF : ringChar F = 2 ‚à® ringChar F ‚â† 2 := eq_or_ne _ _
  ¬∑ have h : ({r : F | ‚àÉ a b, a ^ 2 + P.eval b = r} : Finset F) = univ :=
      eq_univ_of_forall Œª r ‚Ü¶ (mem_filter_univ _).mpr <|
        exists_eq_sq_add_map_of_char_eq_two hF _ _
    rw [h, sq, Nat.mul_add]; exact Nat.le_add_right _ _
  ---- Now assume `char(F) ‚â† 2`. First de-cancel a factor of `q` from both sides.
  have hq : 1 ‚â§ q := Fintype.card_pos
  refine Nat.le_of_mul_le_mul_right (c := q) ?_ hq
  ---- Now estimate.
  calc q ^ 3
    _ ‚â§ #{r | ‚àÉ a b, a ^ 2 + P.eval b = r} *
          ((q - 1) * q + #{p : F √ó F | P.eval p.1 = P.eval p.2}) :=
      card_sq_add_fn_fiber_lower_bound hF P.eval
    _ ‚â§ _ * ((q - 1) * q + P.natDegree * q) :=
      Nat.mul_le_mul_left _
        (Nat.add_le_add_left (card_Polynomial_eqpair_le_of_degree_mul_q hP) _)
    _ = _ * (q - 1 + P.natDegree) * q := by rw [Nat.mul_assoc, ‚Üê Nat.add_mul]
    _ = _ * (q + (P.natDegree - 1)) * q := by
      rw [Nat.add_comm, ‚Üê Nat.add_sub_assoc hq, Nat.add_comm, Nat.add_sub_assoc hP]

/-- Simple bound on the number of elements of `F` of the form `a^2 + P(b)`, `P ‚àà F[X]`. -/
theorem card_sq_add_Polynomial_fiber_lower_bound_simple {P : F[X]} (hP : 1 < P.natDegree) :
    q < #{r | ‚àÉ a b, a ^ 2 + P.eval b = r} + (P.natDegree - 1) := by
  ---- Let `n' = deg(P) - 1`, and first write down `n' > 0`.
  set n' : ‚Ñï := P.natDegree - 1
  have hn' : 0 < n' := Nat.sub_pos_of_lt hP
  ---- First de-cancel a factor of `q + n - 1` from both sides, where `n = deg(P)`.
  refine Nat.lt_of_mul_lt_mul_right (a := q + n') ?_
  ---- Now estimate.
  calc q * (q + n')
    _ = q ^ 2 + q * n' := by rw [Nat.mul_add, Nat.pow_two]
    _ < #{r | ‚àÉ a b, a ^ 2 + P.eval b = r} * (q + n') + (q + n') * n' :=
      Nat.add_lt_add_of_le_of_lt
        (card_sq_add_Polynomial_fiber_lower_bound (Nat.zero_lt_of_lt hP))
        (Nat.mul_lt_mul_of_pos_right (Nat.lt_add_of_pos_right hn') hn')
    _ = _ := by rw [Nat.mul_comm _ n', Nat.add_mul]





/-! ### Upper bound on the number of elements of `FÀ£` not of the form `a^2 + b^n` -/

/-- Simple bound on the number elements of `F` not of the form `a^2 + P(b)`, `P ‚àà F[X]`. -/
theorem card_sq_add_Polynomial_not_fiber_upper_bound {P : F[X]} (hP : 1 < P.natDegree) :
    #{r | ¬¨‚àÉ a b, a ^ 2 + P.eval b = r} < P.natDegree - 1 := by
  rw [‚Üê Nat.add_lt_add_iff_left, filter_card_add_filter_neg_card_eq_card]
  exact card_sq_add_Polynomial_fiber_lower_bound_simple hP

/-- Simple bound on the number elements of `F` not of the form `a^2 + b^n`, `n > 1`. -/
theorem card_sq_add_pow_not_fiber_upper_bound (hn : 1 < n) :
    #{r : F | ¬¨‚àÉ a b, a ^ 2 + b ^ n = r} < n - 1 := by
  have hn0 : (X ^ n : F[X]).natDegree = n := natDegree_X_pow n
  simpa only [eval_pow, eval_X, hn0] using
    card_sq_add_Polynomial_not_fiber_upper_bound (hn.trans_eq hn0.symm)

/-- Simple bound on the number elements of `FÀ£` not of the form `a^2 + b^n`, `n > 1`. -/
theorem card_sq_add_pow_not_unit_fiber_upper_bound (hn : 1 < n) :
    #{r : FÀ£ | ¬¨‚àÉ a b, a ^ 2 + b ^ n = r.val} < n - 1 :=
  calc #{r : FÀ£ | ¬¨‚àÉ a b, a ^ 2 + b ^ n = r.val}
  _ ‚â§ #{r : F | ¬¨‚àÉ a b, a ^ 2 + b ^ n = r} :=
    card_le_card_of_injOn Units.val
      (Œª r hr ‚Ü¶ by simpa only [mem_coe, mem_filter_univ] using hr)
      (Set.injOn_of_injective Units.val_injective)
  _ < n - 1 := card_sq_add_pow_not_fiber_upper_bound hn

end





/-! ### Lower bound on the number of elements of `FÀ£` not of the form `a^2 + b^n` -/

section

/-- Two distinct orbits of `‚ü®x‚ÇÄ‚ü©` above any subset is pairwise disjoint. -/
theorem orbit_zpowers_PairwiseDisjoint
    [Group G] [DecidableEq G] (H : Subgroup G) [Fintype H] [DecidableEq H] :
    (Set.range Œª b : G ‚Ü¶ image (Œª a : H ‚Ü¶ a * b) univ).PairwiseDisjoint id := by
  ---- Pick two orbits, say `‚ü®x‚ü©` and `‚ü®y‚ü©`. Claim: if they're not disjoint, `‚ü®x‚ü© = ‚ü®y‚ü©`.
  rintro _ ‚ü®x, rfl‚ü© _ ‚ü®y, rfl‚ü© h
  refine disjoint_right.mpr Œª a ha ha0 ‚Ü¶ h (ext Œª g ‚Ü¶ ?_)
  simp only [id, mem_image, mem_univ, true_and] at ha ha0 ‚ä¢
  rcases ha with ‚ü®z, rfl‚ü©; rcases ha0 with ‚ü®w, h‚ü©
  ---- Since `‚ü®x‚ü©` and `‚ü®y‚ü©` are not disjoint, `x = vy` for some `v ‚àà H`.
  replace h : ((z‚Åª¬π * w : H) : G) * x = y := by
    rwa [‚Üê inv_mul_eq_iff_eq_mul, ‚Üê mul_assoc] at h
  generalize z‚Åª¬π * w = v at h
  subst h; clear z w
  ---- Now just split into cases and bash.
  refine ‚ü®?_, ?_‚ü©
  ¬∑ rintro ‚ü®a, rfl‚ü©; refine ‚ü®a * v‚Åª¬π, (?_ : a * (v : G)‚Åª¬π * _ = a * x)‚ü©
    rw [mul_assoc, ‚Üê mul_assoc _ _ x, inv_mul_cancel, one_mul]
  ¬∑ rintro ‚ü®a, rfl‚ü©; exact ‚ü®a * v, mul_assoc _ _ _‚ü©

variable [Group G] [Fintype G] [DecidableEq G] {x‚ÇÄ : G}
variable {S : Finset G} (hS : ‚àÄ n : ‚Ñ§, ‚àÄ s ‚àà S, x‚ÇÄ ^ n * s ‚àà S)
include hS

/-- If `S` is invariant under left multiplication by `x‚ÇÄ`, then the
  binary image of `‚ü®x‚ÇÄ‚ü© √ó S` under pointwise multiplication is exactly `S`. -/
theorem image_mul_zpowers_eq_self_of_mul_invariant :
    image‚ÇÇ (Œª (x : Subgroup.zpowers x‚ÇÄ) (s : G) ‚Ü¶ x * s) univ S = S := by
  ext x; simp only [mem_image‚ÇÇ, mem_univ, true_and, Subgroup.exists_zpowers]
  exact ‚ü®Œª ‚ü®m, y, hy, h‚ü© ‚Ü¶ h ‚ñ∏ hS m y hy, Œª hx ‚Ü¶ ‚ü®0, x, hx, by rw [zpow_zero, one_mul]‚ü©‚ü©

/-- If `S` is invariant under left multiplication by `x‚ÇÄ`, then `ord(x_0) ‚à£ S`. -/
theorem order_dvd_card_Finset_of_mul_invariant : orderOf x‚ÇÄ ‚à£ #S :=
  calc orderOf x‚ÇÄ
  _ = Fintype.card (Subgroup.zpowers x‚ÇÄ) := Fintype.card_zpowers.symm
  _ ‚à£ #(image‚ÇÇ (Œª (x : Subgroup.zpowers x‚ÇÄ) (s : G) ‚Ü¶ x * s) univ S) :=
    card_dvd_card_image‚ÇÇ_left (Œª b _ x y h ‚Ü¶ by simpa using h)
      (Set.PairwiseDisjoint.subset (orbit_zpowers_PairwiseDisjoint _)
        (Set.image_subset_range _ _))
  _ = #S := congrArg card (image_mul_zpowers_eq_self_of_mul_invariant hS)

end


/-- `(q - 1)/gcd(q - 1, 2n)` divides the number of units not of the form `a^2 + b^n`. -/
theorem dvd_card_sq_add_pow_not_unit_fiber :
    (q - 1) / (q - 1).gcd (2 * n) ‚à£ #{r : FÀ£ | ¬¨‚àÉ a b, a ^ 2 + b ^ n = r.val} := by
  ---- Find a generator `g` for `FÀ£`.
  obtain ‚ü®g, hg‚ü© : ‚àÉ g : FÀ£, ‚àÄ x : FÀ£, x ‚àà Subgroup.zpowers g := IsCyclic.exists_generator
  /- Reduce calculation to showing that the bad set of elements not of the form
    `a^2 + b^5` is invariant under left multiplication by `x‚ÇÄ^{2n}`. -/
  calc (q - 1) / (q - 1).gcd (2 * n)
    _ = orderOf (g ^ (2 * n)) := by
      rw [orderOf_pow, orderOf_eq_card_of_forall_mem_zpowers hg,
        Nat.card_eq_fintype_card, Fintype.card_units]
    _ ‚à£ _ := order_dvd_card_Finset_of_mul_invariant Œª k r hr ‚Ü¶ ?_
  ---- Finally, show that the bad set is indeed invariant with respect to `x‚ÇÄ^{2n}`.
  rw [mem_filter_univ] at hr ‚ä¢
  rintro ‚ü®a, b, h‚ü©
  refine hr ‚ü®(g‚Åª¬π ^ k) ^ n * a, (g‚Åª¬π ^ k) ^ 2 * b, ?_‚ü©
  calc ((g‚Åª¬π ^ k) ^ n * a) ^ 2 + ((g‚Åª¬π ^ k) ^ 2 * b) ^ n
    _ = (g‚Åª¬π ^ k) ^ (2 * n) * ((g ^ (2 * n)) ^ k * r : FÀ£) := by
      rw [mul_pow, mul_pow, ‚Üê pow_mul, ‚Üê pow_mul, Nat.mul_comm, ‚Üê mul_add, h]
    _ = (g‚Åª¬π ^ k : FÀ£) ^ (2 * n) * (((g ^ (2 * n)) ^ k * r) : FÀ£) := by
      rw [‚Üê Units.val_zpow_eq_zpow_val]
    _ = ((g‚Åª¬π ^ k) ^ ((2 * n : ‚Ñï) : ‚Ñ§) * ((g ^ ((2 * n : ‚Ñï) : ‚Ñ§)) ^ k * r) : FÀ£) := rfl
    _ = (g‚Åª¬π ^ (k * (2 * n : ‚Ñï)) * g ^ ((2 * n : ‚Ñï) * k) * r : FÀ£) := by
      rw [‚Üê zpow_mul, ‚Üê zpow_mul, mul_assoc]
    _ = r := by rw [Int.mul_comm, ‚Üê mul_zpow, inv_mul_cancel, one_zpow, one_mul]

/-- `q - 1` divides `2n` times the number of units not of the form `a^2 + b^n`. -/
theorem dvd_two_mul_exp_mul_card_sq_add_pow_not_unit_fiber (hn : 0 < n) :
    q - 1 ‚à£ 2 * n * #{r : FÀ£ | ¬¨‚àÉ a b, a ^ 2 + b ^ n = r.val} := by
  have h : (q - 1).gcd (2 * n) ‚à£ q - 1 := Nat.gcd_dvd_left _ _
  have h0 : 0 < (q - 1).gcd (2 * n) :=
    Nat.gcd_pos_of_pos_right _ (Nat.mul_pos Nat.two_pos hn)
  rw [‚Üê Nat.dvd_gcd_mul_iff_dvd_mul, ‚Üê Nat.div_dvd_iff_dvd_mul h h0]
  exact dvd_card_sq_add_pow_not_unit_fiber





/-! ### Summary -/

/-- If `q > 2n(n - 1)`, every element of `F` is of the form `a^2 + b^n`. -/
theorem exists_eq_sq_add_pow (hn : 1 < n) (h : 2 * n * (n - 1) < q) :
    ‚àÄ r : F, ‚àÉ a b : F, a ^ 2 + b ^ n = r := by
  ---- Reduce to just the units.
  suffices ‚àÄ r : FÀ£, ‚àÉ a b : F, a ^ 2 + b ^ n = r by
    intro r; obtain rfl | hr : r = 0 ‚à® r ‚â† 0 := eq_or_ne r 0
    ¬∑ refine ‚ü®0, 0, ?_‚ü©
      rw [zero_pow (Nat.succ_ne_zero 1), zero_add, zero_pow (Nat.ne_zero_of_lt hn)]
    ¬∑ lift r to FÀ£ using Ne.isUnit hr
      exact this r
  ---- Now the statement is equivalent to `N = #{r : FÀ£ | ‚àÉ a b, a^2 + b^n = r.val} = 0`.
  let N : ‚Ñï := #{r : FÀ£ | ¬¨‚àÉ a b, a ^ 2 + b ^ n = r.val}
  suffices N = 0 by
    simpa only [N, card_filter_eq_zero_iff, mem_univ, forall_const, not_not] using this
  ---- We have `N < n - 1` and `q - 1 ‚à£ 2nN`.
  have hn0 : 0 < n := Nat.zero_lt_of_lt hn
  replace hn : N < n - 1 := card_sq_add_pow_not_unit_fiber_upper_bound hn
  have h0 : q - 1 ‚à£ 2 * n * N := dvd_two_mul_exp_mul_card_sq_add_pow_not_unit_fiber hn0
  ---- Then `2nN < 2n(n - 1) ‚â§ q - 1`, so `2nN = 0` and thus `N = 0`.
  replace hn0 : 0 < 2 * n := Nat.mul_pos Nat.two_pos hn0
  replace h0 : 2 * n * N = 0 := Nat.eq_zero_of_dvd_of_lt h0 <| by
    calc 2 * n * N
      _ < 2 * n * (n - 1) := Nat.mul_lt_mul_of_pos_left hn hn0
      _ ‚â§ q - 1 := Nat.le_sub_one_of_lt h
  exact (Nat.mul_eq_zero.mp h0).resolve_left hn0.ne.symm

/-- If `q > 40`, every element of `F` is of the form `a^2 + b^5`. -/
theorem exists_eq_sq_add_pow_five (hF : 40 < q) :
    ‚àÄ r : F, ‚àÉ a b : F, a ^ 2 + b ^ 5 = r :=
  exists_eq_sq_add_pow (n := 5) (Nat.one_lt_succ_succ 3 : 1 < 5) hF

/-- Final solution -/
theorem final_solution {p : ‚Ñï} [Fact p.Prime] (hp : 40 < p) (r : ‚Ñ§) :
    ‚àÉ a b : ‚Ñ§, a ^ 2 + b ^ 5 ‚â° r [ZMOD p] := by
  obtain ‚ü®a, b, h0‚ü© : ‚àÉ a b : ZMod p, a ^ 2 + b ^ 5 = r :=
    exists_eq_sq_add_pow_five (hp.trans_eq (ZMod.card p).symm) r
  exact ‚ü®a.val, b.val, (ZMod.intCast_eq_intCast_iff _ _ _).mp (by simpa using h0)‚ü©
