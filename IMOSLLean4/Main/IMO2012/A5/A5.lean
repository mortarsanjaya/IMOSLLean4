/-
Copyright (c) 2024 Gian Cordana Sanjaya. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Gian Cordana Sanjaya
-/

import IMOSLLean4.Main.IMO2012.A5.A5Cases.Case1
import IMOSLLean4.Main.IMO2012.A5.A5Cases.Case2
import IMOSLLean4.Main.IMO2012.A5.A5Cases.Case3
import IMOSLLean4.Main.IMO2012.A5.A5Answers.Common
import IMOSLLean4.Main.IMO2012.A5.A5Answers.ZeroMap
import IMOSLLean4.Main.IMO2012.A5.A5General.A5Periodic

/-!
# IMO 2012 A5

Let $R$ be a ring and $S$ be a domain.
Find all functions $f : R â†’ S$ such that, for any $x, y âˆˆ R$,
$$ f(xy + 1) = f(x) f(y) + f(x + y). $$

### Implementation details

The solution is broken up into several files.
We highlight some of the most important ones.
* The file `IMOSLLean4/IMO2012/A5/A5Defs.lean` defines the functional equation.
* The file `IMOSLLean4/IMO2012/A5/A5Answers/Common.lean` defines the predicate
    `isNontrivialAnswer`, which describes all guesses for the solution up to homomorphism.
  It also proves that all the guess functions work.
* The file `IMOSLLean4/IMO2012/A5/A5General/A5Periodic.lean`
    defines the quotient of $R$ by $f$-periods.
  It also proves that the set $I$ of $f$-periods form an ideal, and
    the induced quotient function $\tilde{f} : R/I â†’ S$ is good.

Three files prove that no other functions are good:
* `IMOSLLean4/IMO2012/A5/A5Cases/Case1.lean` for case `f(-1) â‰  0`.
* `IMOSLLean4/IMO2012/A5/A5Cases/Case2.lean` for case `f(-1) = 0`, `char(R) â‰  2`.
* `IMOSLLean4/IMO2012/A5/A5Cases/Case3.lean` for case `char(R) = 2`.
-/

namespace IMOSL
namespace IMO2012A5

open BundledRingFun

variable [Ring R] [Ring S] [NoZeroDivisors S] {f : R â†’ S}

theorem ReducedGood.isNontrivialAnswer (hf : ReducedGood f) : isNontrivialAnswer (ofFun f) :=
  (em' <| f (-1) = 0).elim
    ---- Case 1: `f(-1) â‰  0`
    (Î» h â†¦ (Case1.solution hf h).imp
      (Î» âŸ¨Ï†, h0âŸ© â†¦ âŸ¨_, isPolyGoodMap.SubOne R, RingHom.id R, Ï†, h0âŸ©)
      (Î» âŸ¨Ï†, h0âŸ© â†¦ âŸ¨_, isFinGoodMap.ð”½â‚ƒMap1, Ï†, Int.castRingHom S, h0âŸ©))
  Î» h â†¦ (em' <| f 2 = -1).elim
    ---- Case 2: `f(-1) = 0`, `f(2) â‰  -1`
    (Î» h0 â†¦ (Case2.solution hf h h0).imp
      (Î» âŸ¨R', _, Ï†, Î¹, h1âŸ© â†¦ âŸ¨_, isPolyGoodMap.SqSubOne R', Ï†, Î¹, h1âŸ©)
    Î» h1 â†¦ h1.elim
      (Î» âŸ¨Ï†, h1âŸ© â†¦ âŸ¨_, isFinGoodMap.â„¤â‚„Map, Ï†, Int.castRingHom S, h1âŸ©)
      (Î» âŸ¨Ï†, h1âŸ© â†¦ âŸ¨_, isFinGoodMap.ð”½â‚ƒMap2, Ï†, Int.castRingHom S, h1âŸ©))
    ---- Case 3: `f(2) = -1`
    (Î» h0 â†¦ have : Extra.CharTwo R :=
      Extra.CharTwo.Semiring_of_two_eq_zero (Case2.CharTwo'_of_map_two hf h h0)
    (Case3.solution hf).imp
      (Î» âŸ¨Ï†, h0âŸ© â†¦ âŸ¨_, isPolyGoodMap.SubOne R, RingHom.id R, Ï†, h0âŸ©)
    Î» h1 â†¦ h1.elim
      (Î» âŸ¨Ï†, h1âŸ© â†¦ âŸ¨_, isFinGoodMap.ð”½â‚‚ÎµMap, Ï†, Int.castRingHom S, h1âŸ©)
    Î» h1 â†¦ h1.elim
      (Î» âŸ¨Ï†, Î¹, h1âŸ© â†¦ âŸ¨_, isFinGoodMap.ð”½â‚„Map, Ï†, Î¹, h1âŸ©)
      (Î» âŸ¨Ï†, h1âŸ© â†¦ âŸ¨_, isFinGoodMap.ð”½â‚‚Map, Ï†, Int.castRingHom S, h1âŸ©))

theorem isNontrivialAnswer.trans_Hom {X Y : BundledRingFun.{u, v}}
    (hA : isNontrivialAnswer X) (F : X.Hom Y) : isNontrivialAnswer Y :=
  hA.imp (Î» âŸ¨A, h, âŸ¨GâŸ©âŸ© â†¦ âŸ¨A, h, âŸ¨F.trans GâŸ©âŸ©) (Î» âŸ¨A, h, âŸ¨GâŸ©âŸ© â†¦ âŸ¨A, h, âŸ¨F.trans GâŸ©âŸ©)

theorem NontrivialGood.isNontrivialAnswer (hf : NontrivialGood f) :
    isNontrivialAnswer (ofFun f) :=
  (PeriodEquiv.toQuotientMap_ReducedGood hf).isNontrivialAnswer.trans_Hom
    (Hom.ofRingHom_right (ofFun (PeriodEquiv.toQuotientMap hf))
      (PeriodEquiv.toRingCon hf).mk')





/-! ### Summary -/

/-- Final solution for `NontrivialGood` -/
theorem NontrivialGood_iff_isNontrivialAnswer :
    NontrivialGood f â†” isNontrivialAnswer (ofFun f) :=
  âŸ¨NontrivialGood.isNontrivialAnswer, isNontrivialAnswer.NontrivialGoodâŸ©

/-- Final solution -/
theorem final_solution : good f â†” isNontrivialAnswer (ofFun f) âˆ¨ f = 0 :=
  good_iff_Nontrivial_or_eq_zero.trans <|
    or_congr_left NontrivialGood_iff_isNontrivialAnswer
