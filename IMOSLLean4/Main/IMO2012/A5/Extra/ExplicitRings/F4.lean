/-
Copyright (c) 2023 Gian Cordana Sanjaya. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Gian Cordana Sanjaya
-/

import Mathlib.Algebra.Ring.Hom.Defs
import IMOSLLean4.Extra.CharTwo.Ring

/-!
# Explicit construction of `ð”½â‚„`

In this file, we explicitly construct `ð”½â‚„`, the field of 4 elements.
We prove that it is a ring, and we construct ring homomorphisms from `ð”½â‚„`.
-/

namespace IMOSL
namespace IMO2012A5

open Extra

inductive ð”½â‚„
  | O : ð”½â‚„
  | I : ð”½â‚„
  | X : ð”½â‚„
  | Y : ð”½â‚„


namespace ð”½â‚„

protected def add : ð”½â‚„ â†’ ð”½â‚„ â†’ ð”½â‚„
  | O, x => x
  | I, O => I
  | I, I => O
  | I, X => Y
  | I, Y => X
  | X, O => X
  | X, I => Y
  | X, X => O
  | X, Y => I
  | Y, O => Y
  | Y, I => X
  | Y, X => I
  | Y, Y => O

protected def mul : ð”½â‚„ â†’ ð”½â‚„ â†’ ð”½â‚„
  | O, _ => O
  | I, x => x
  | X, O => O
  | X, I => X
  | X, X => Y
  | X, Y => I
  | Y, O => O
  | Y, I => Y
  | Y, X => I
  | Y, Y => X

instance : Zero ð”½â‚„ := âŸ¨OâŸ©
instance : One ð”½â‚„ := âŸ¨IâŸ©
instance : Add ð”½â‚„ := âŸ¨ð”½â‚„.addâŸ©
instance : Neg ð”½â‚„ := âŸ¨idâŸ©
instance : Mul ð”½â‚„ := âŸ¨ð”½â‚„.mulâŸ©





/-! ### `ð”½â‚„` is a commutative group -/

protected theorem add_zero : âˆ€ x : ð”½â‚„, x + 0 = x
  | O => rfl
  | I => rfl
  | X => rfl
  | Y => rfl

protected theorem zero_add (x : ð”½â‚„) : 0 + x = x := rfl

protected theorem add_comm : âˆ€ x y : ð”½â‚„, x + y = y + x
  | O, x => x.add_zero.symm
  | x, O => x.add_zero
  | I, I => rfl
  | I, X => rfl
  | I, Y => rfl
  | X, I => rfl
  | X, X => rfl
  | X, Y => rfl
  | Y, I => rfl
  | Y, X => rfl
  | Y, Y => rfl

protected theorem add_assoc : âˆ€ x y z : ð”½â‚„, x + y + z = x + (y + z)
  | O, _, _ => rfl
  | x, O, z => congr_arg (Â· + z) x.add_zero
  | x, y, O => y.add_zero.symm â–¸ (x + y).add_zero
  | I, I, I => rfl
  | I, I, X => rfl
  | I, I, Y => rfl
  | I, X, I => rfl
  | I, X, X => rfl
  | I, X, Y => rfl
  | I, Y, I => rfl
  | I, Y, X => rfl
  | I, Y, Y => rfl
  | X, I, I => rfl
  | X, I, X => rfl
  | X, I, Y => rfl
  | X, X, I => rfl
  | X, X, X => rfl
  | X, X, Y => rfl
  | X, Y, I => rfl
  | X, Y, X => rfl
  | X, Y, Y => rfl
  | Y, I, I => rfl
  | Y, I, X => rfl
  | Y, I, Y => rfl
  | Y, X, I => rfl
  | Y, X, X => rfl
  | Y, X, Y => rfl
  | Y, Y, I => rfl
  | Y, Y, X => rfl
  | Y, Y, Y => rfl

protected theorem neg_add_cancel : âˆ€ x : ð”½â‚„, -x + x = 0
  | O => rfl
  | I => rfl
  | X => rfl
  | Y => rfl

instance : AddCommGroup ð”½â‚„ :=
  { add_assoc := ð”½â‚„.add_assoc
    zero_add := ð”½â‚„.zero_add
    add_zero := ð”½â‚„.add_zero
    add_comm := ð”½â‚„.add_comm
    neg_add_cancel := ð”½â‚„.neg_add_cancel
    nsmul := nsmulRec
    zsmul := zsmulRec }

instance : CharTwo ð”½â‚„ := âŸ¨neg_add_cancelâŸ©





/-! ### `ð”½â‚„` is a ring -/

protected theorem zero_mul (x : ð”½â‚„) : 0 * x = 0 := rfl

protected theorem mul_zero : âˆ€ x : ð”½â‚„, x * 0 = 0
  | O => rfl
  | I => rfl
  | X => rfl
  | Y => rfl

protected theorem mul_one : âˆ€ x : ð”½â‚„, x * 1 = x
  | O => rfl
  | I => rfl
  | X => rfl
  | Y => rfl

protected theorem one_mul (x : ð”½â‚„) : 1 * x = x := rfl

protected theorem mul_comm : âˆ€ x y : ð”½â‚„, x * y = y * x
  | I, x => x.mul_one.symm
  | x, I => x.mul_one
  | O, O => rfl
  | O, X => rfl
  | O, Y => rfl
  | X, O => rfl
  | X, X => rfl
  | X, Y => rfl
  | Y, O => rfl
  | Y, X => rfl
  | Y, Y => rfl

protected theorem mul_assoc : âˆ€ x y z : ð”½â‚„, x * y * z = x * (y * z)
  | O, _, _ => rfl
  | I, _, _ => rfl
  | X, O, _ => rfl
  | X, I, _ => rfl
  | Y, O, _ => rfl
  | Y, I, _ => rfl
  | X, X, O => rfl
  | X, X, I => rfl
  | X, X, X => rfl
  | X, X, Y => rfl
  | X, Y, O => rfl
  | X, Y, I => rfl
  | X, Y, X => rfl
  | X, Y, Y => rfl
  | Y, X, O => rfl
  | Y, X, I => rfl
  | Y, X, X => rfl
  | Y, X, Y => rfl
  | Y, Y, O => rfl
  | Y, Y, I => rfl
  | Y, Y, X => rfl
  | Y, Y, Y => rfl

protected theorem mul_add : âˆ€ x y z : ð”½â‚„, x * (y + z) = x * y + x * z
  | O, _, _ => rfl
  | I, _, _ => rfl
  | X, O, _ => rfl
  | X, I, O => rfl
  | X, I, I => rfl
  | X, I, X => rfl
  | X, I, Y => rfl
  | X, X, O => rfl
  | X, X, I => rfl
  | X, X, X => rfl
  | X, X, Y => rfl
  | X, Y, O => rfl
  | X, Y, I => rfl
  | X, Y, X => rfl
  | X, Y, Y => rfl
  | Y, O, _ => rfl
  | Y, I, O => rfl
  | Y, I, I => rfl
  | Y, I, X => rfl
  | Y, I, Y => rfl
  | Y, X, O => rfl
  | Y, X, I => rfl
  | Y, X, X => rfl
  | Y, X, Y => rfl
  | Y, Y, O => rfl
  | Y, Y, I => rfl
  | Y, Y, X => rfl
  | Y, Y, Y => rfl

protected theorem add_mul (x y z : ð”½â‚„) : (x + y) * z = x * z + y * z :=
  by rw [ð”½â‚„.mul_comm, ð”½â‚„.mul_add, z.mul_comm, z.mul_comm]

instance : CommRing ð”½â‚„ :=
  { ð”½â‚„.instAddCommGroup with
    zero_mul := ð”½â‚„.zero_mul
    mul_zero := ð”½â‚„.mul_zero
    mul_assoc := ð”½â‚„.mul_assoc
    one_mul := ð”½â‚„.one_mul
    mul_one := ð”½â‚„.mul_one
    mul_comm := ð”½â‚„.mul_comm
    left_distrib := ð”½â‚„.mul_add
    right_distrib := ð”½â‚„.add_mul }





/-! ### Ring homomorphism from `ð”½â‚„` -/

open CharTwo

def cast [AddMonoidWithOne R] (r : R) : ð”½â‚„ â†’ R
  | O => 0
  | I => 1
  | X => r
  | Y => r + 1

variable [NonAssocSemiring R] [CharTwo R]

theorem cast_add (r : R) : âˆ€ x y : ð”½â‚„, cast r (x + y) = cast r x + cast r y
  | O, _ => (zero_add _).symm
  | x, O => x.add_zero.symm â–¸ (add_zero _).symm
  | I, I => (add_self_eq_zero _).symm
  | I, X => CharTwo.add_comm r 1
  | I, Y => (add_add_cancel_middleâ‚ 1 r).symm
  | X, I => rfl
  | X, X => (add_self_eq_zero _).symm
  | X, Y => (add_add_cancel_left _ _).symm
  | Y, I => (add_add_cancel_right _ _).symm
  | Y, X => (add_add_cancel_middleâ‚‚ _ _).symm
  | Y, Y => (add_self_eq_zero _).symm

variable {r : R} (h : r * r + r = 1)
include h

theorem cast_mul : âˆ€ x y : ð”½â‚„, cast r (x * y) = cast r x * cast r y
  | O, _ => (zero_mul _).symm
  | I, _ => (one_mul _).symm
  | x, O => x.mul_zero.symm â–¸ (mul_zero _).symm
  | x, I => x.mul_one.symm â–¸ (mul_one _).symm
  | X, X => add_eq_iff_eq_add'''.mpr h.symm
  | X, Y => h.symm.trans (mul_add_one r r).symm
  | Y, X => h.symm.trans (add_one_mul r r).symm
  | Y, Y => (add_eq_iff_eq_add''.mp h).trans (add_one_mul_self r).symm

def castRingHom : ð”½â‚„ â†’+* R :=
  { toFun := cast r
    map_one' := rfl
    map_mul' := cast_mul h
    map_zero' := rfl
    map_add' := cast_add r }

theorem castRingHom_injective (h0 : (1 : R) â‰  0) :
    Function.Injective (castRingHom h) :=
  (injective_iff_map_eq_zero _).mpr Î» x h1 â†¦ match x with
    | O => rfl
    | I => Not.elim h0 h1
    | X => Not.elim h0 ((cast_mul h X Y).trans (mul_eq_zero_of_left h1 _))
    | Y => Not.elim h0 ((cast_mul h Y X).trans (mul_eq_zero_of_left h1 _))
