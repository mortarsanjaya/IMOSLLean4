/-
Copyright (c) 2024 Gian Cordana Sanjaya. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Gian Cordana Sanjaya
-/

import IMOSLLean4.Main.IMO2012.A5.A5Answers.SubOneMap
import IMOSLLean4.Main.IMO2012.A5.A5Answers.F2Map
import IMOSLLean4.Main.IMO2012.A5.A5Answers.F2eMap
import IMOSLLean4.Main.IMO2012.A5.A5Answers.F4Map
import IMOSLLean4.Main.IMO2012.A5.A5General.A5CommLift
import IMOSLLean4.Main.IMO2012.A5.A5General.A5QuasiPeriodic
import IMOSLLean4.Extra.CharTwo.Ring
import IMOSLLean4.Extra.CharTwo.Hom
import Mathlib.Algebra.Ring.Equiv

/-!
# IMO 2012 A5 (Case 3: `char(R) âˆ£ 2`)

We solve the case where `f` is reduced good and `char(R) âˆ£ 2`.

TODO: Optimize/clean up the proofs if possible, starting from `R_elts_claim1`.
-/

namespace IMOSL
namespace IMO2012A5
namespace Case3

open Extra Extra.CharTwo

/-! ### General lemmas -/

section

variable [Semiring R] [CharTwo R] [Ring S] {f : R â†’ S} (hf : NontrivialGood f)
include hf

/-- (3.1) -/
lemma Eq1 (x) : f (x * (x + 1) + 1) = f x * f (x + 1) := by
  rw [hf.is_good, add_add_cancel_left, hf.map_one, add_zero]

/-- (3.2) -/
lemma Eq2 (x) : f (x * x + 1) = f x ^ 2 - 1 := by
  rw [sq, hf.is_good, add_self_eq_zero, hf.map_zero, sub_eq_add_neg]

lemma Eq2_v2 (x) : f (x * x) = f (x + 1) ^ 2 - 1 := by
  rw [â† Eq2 hf, add_one_mul_self, add_add_cancel_right]

/-- (3.3) -/
lemma Eq3 (x) : f x * f (x * x + x) =
    (f (x + 1) ^ 2 - 1) * (f (x + 1) - 1) + f x * f (x + 1) := by
  have h : x * (x + 1) = x * x + x := mul_add_one x x
  rw [â† Eq2_v2 hf, â† Eq1 hf, mul_sub_one, â† add_sub_right_comm, h, add_assoc,
    â† hf.is_good, mul_assoc, hf.is_good, h, add_add_cancel_middleâ‚, add_sub_cancel_right]

lemma Eq3_v2 (x) : f (x + 1) * f (x * x + x) =
    (f x ^ 2 - 1) * (f x - 1) + f (x + 1) * f x := by
  have h := Eq3 hf (x + 1)
  rwa [add_add_cancel_right, add_one_mul_self, add_add_add_cancel_right] at h

end





/-! ### Lemmas in commutative case -/

namespace CommCase

variable [CommSemiring R] [CharTwo R] [CommRing S]

/-- Big ring identity 1 used in `Thm1` -/
lemma Thm1_ring_id1 (a b : S) :
    a * ((a ^ 2 - 1) * (a - 1) + b * a) - b * ((b ^ 2 - 1) * (b - 1) + a * b)
      = (a ^ 2 + b ^ 2 - 1) * (a + b - 1) * (a - b) := by ring

/-- Big ring identity 2 used in `Thm1` -/
lemma Thm1_ring_id2 (a : S) :
    a ^ 2 * ((a ^ 2 - 1) ^ 2 + 1) - ((a ^ 2 - 1) * (a - 1) + a * a) ^ 2
      = (1 - 2 * a) * (a ^ 2 - 1) := by ring

variable [NoZeroDivisors S] {f : R â†’ S} (hf : NontrivialGood f)
include hf

/-- (3.4) -/
theorem Thm1 (x) : f x ^ 2 + f (x + 1) ^ 2 = 1 âˆ¨ f x + f (x + 1) = 1 := by
  ---- Step 1: Either the above holds or `f(x) = f(x + 1)`, then assume the latter
  have h := Thm1_ring_id1 (f x) (f (x + 1))
  rw [â† Eq3 hf, â† Eq3_v2 hf, mul_left_comm, sub_self, zero_eq_mul,
    mul_eq_zero, sub_eq_zero, sub_eq_zero, sub_eq_zero] at h
  cases h with | inl h => exact h | inr h => ?_
  right
  ---- Step 2: Prove either `2 f(x) = 1` or `f(x)^2 - 1 = 0`, then assume the latter
  rw [â† h, â† two_mul]
  have h0 : _ ^ 2 = _ ^ 2 := congrArg (Î» x â†¦ x ^ 2) (Eq3 hf x)
  rw [mul_pow, â† add_eq_of_eq_sub (Eq2 hf (x * x + x)), â† h, add_mul_self,
    â† mul_add_one (x * x), Eq1 hf, Eq2 hf, Eq2_v2 hf, â† h,
    â† sub_eq_zero, â† sq, Thm1_ring_id2, mul_eq_zero] at h0
  cases h0 with | inl h0 => exact (eq_of_sub_eq_zero h0).symm | inr h0 => ?_
  ---- Step 3: Resolve the case `f(x)^2 - 1 = 0`
  have h1 : f (x * x) = 0 := by rw [Eq2_v2 hf, â† h, h0]
  replace h := Eq3_v2 hf (x * x)
  rw [h1, mul_zero, add_zero, sq, zero_mul, zero_sub,
    neg_mul_neg, one_mul, Eq2 hf, h0, zero_mul] at h
  rw [â† mul_one (2 * f x), â† h, mul_zero]

/-- Main theorem 1: We are (almost!) done if `char(S) âˆ£ 2` -/
theorem SCharTwo_map_add_one [CharTwo S] (x) : f (x + 1) = f x + 1 := by
  have h := Thm1 hf x
  rwa [â† CharTwo.add_sq, CharTwo.sq_eq_one_iff, or_self, add_eq_iff_eq_add''] at h

omit [NoZeroDivisors S] in
lemma pow_four_Eq1 (x) : f ((x ^ 2) ^ 2) = (f x ^ 2 - 1) ^ 2 - 1 := by
  rw [â† add_add_cancel_right (x ^ 2) 1, add_one_sq, sq, sq, Eq2 hf, Eq2 hf]

omit [NoZeroDivisors S] in
lemma pow_four_Eq2 (x) : f ((x ^ 2) ^ 2 + 1) = (f (x + 1) ^ 2 - 1) ^ 2 - 1 := by
  rw [â† pow_four_Eq1 hf, add_one_sq, add_one_sq]

omit [CommSemiring R] [CharTwo R] hf [NoZeroDivisors S] in
/-- Big ring identity used in `SCharNeTwo_main` -/
lemma SCharNeTwo_main_ring_id (a b : S) :
    ((a - 1) ^ 2 - 1) * ((b - 1) ^ 2 - 1) - ((a * b - 1) ^ 2 - 1)
      = 2 * ((a * b) * ((2 + 1) - (a + b))) := by ring

/-- Main theorem 2: Case division when `char(S) âˆ¤ 2` -/
theorem SCharNeTwo_cases (h : (2 : S) â‰  0) (x) :
    (f x = 0 âˆ¨ f (x + 1) = 0) âˆ¨ (f x + f (x + 1) = 1 âˆ§ f x * f (x + 1) = -1) := by
  ---- Step 1: `f(x) f(x + 1) = 0` or `f(x)^2 + f(x + 1)^2 = 3`
  have h0 := pow_four_Eq2 hf (x * (x + 1))
  rw [Eq1 hf, mul_pow, mul_pow, add_one_sq, add_one_sq, Eq1 hf, pow_four_Eq1 hf,
    pow_four_Eq2 hf, â† sub_eq_zero, mul_pow, SCharNeTwo_main_ring_id, mul_eq_zero,
    or_iff_right h, mul_eq_zero, â† mul_pow, sq_eq_zero_iff] at h0
  revert h0; refine Or.imp mul_eq_zero.mp Î» h0 â†¦ ?_
  rw [sub_eq_zero, eq_comm] at h0
  ---- Step 2: If `f(x)^2 + f(x + 1)^2 = 3`, then `x` is ğ”½â‚„-primitive.
  refine (Thm1 hf x).elim (Î» h1 â†¦ Not.elim h ?_) (Î» h1 â†¦ âŸ¨h1, ?_âŸ©)
  Â· rwa [h0, add_left_eq_self] at h1
  apply congrArg (Î» y â†¦ y ^ 2) at h1
  rw [one_pow, add_sq', h0, add_right_comm, add_left_eq_self,
     mul_assoc, â† mul_one_add (2 : S), mul_eq_zero] at h1
  exact eq_neg_of_add_eq_zero_right (h1.resolve_left h)

end CommCase





/-! ### Transferring results from commutative case -/

variable [Ring R] [CharTwo R] [Ring S] [NoZeroDivisors S]

/-- Solution for `char(S) âˆ£ 2` -/
theorem SCharTwo.solution [CharTwo S] {f : R â†’ S} (hf : NontrivialGood f) :
    âˆƒ Ï† : R â†’+* S, âˆ€ x, f x = Ï† (x - 1) :=
  sub_one_solver hf Î» x â†¦ by
    rcases CommSubring.oneVarCommLiftDomain_exists hf x with
      âŸ¨R', R'comm, Ï†, hÏ†, âŸ¨x, rflâŸ©, S', S'comm, S'nzd, Ï, hÏ, f', h, hf'âŸ©
    have R'char := pullback_of_inj Ï†.toAddMonoidHom hÏ†
    have S'char := pullback_of_inj Ï.toAddMonoidHom hÏ
    rw [h, â† Ï†.map_one, â† Ï†.map_add, h, â† Ï.map_one, â† Ï.map_add]
    exact Ï.congr_arg (CommCase.SCharTwo_map_add_one hf' x)


namespace SCharNeTwo

section

variable (hS : (2 : S) â‰  0) {f : R â†’ S} (hf : NontrivialGood f)
include hS hf

/-- (3.5) -/
lemma main_cases (x) :
    (f x = 0 âˆ¨ f (x + 1) = 0) âˆ¨ (f x + f (x + 1) = 1 âˆ§ f x * f (x + 1) = -1) := by
  rcases CommSubring.oneVarCommLiftDomain_exists hf x with
    âŸ¨R', R'comm, Ï†, hÏ†, âŸ¨x, rflâŸ©, S', S'comm, S'nzd, Ï, -, f', h, hf'âŸ©
  have R'char := pullback_of_inj Ï†.toAddMonoidHom hÏ†
  have S'char : (2 : S') â‰  0 := Î» h1 â†¦ hS <| by rw [â† map_ofNat Ï 2, h1, Ï.map_zero]
  rw [h, â† Ï†.map_one, â† Ï†.map_add, h, â† Ï.map_zero]
  refine (CommCase.SCharNeTwo_cases hf' S'char x).imp
    (Or.imp Ï.congr_arg Ï.congr_arg) (And.imp (Î» h0 â†¦ ?_) (Î» h0 â†¦ ?_))
  Â· rw [â† Ï.map_add, h0, Ï.map_one]
  Â· rw [â† Ï.map_mul, h0, Ï.map_neg, Ï.map_one]

lemma map_add_one_eq_zero_iff_map_eq {x} : f (x + 1) = 0 â†” f x ^ 2 = 1 := by
  refine âŸ¨Î» h0 â†¦ ?_, Î» h0 â†¦ ?_âŸ©
  Â· have h1 := Eq3_v2 hf x
    rw [h0, zero_mul, zero_mul, add_zero, zero_eq_mul] at h1
    refine h1.elim eq_of_sub_eq_zero Î» h1 â†¦ ?_
    rw [eq_of_sub_eq_zero h1, one_pow]
  Â· rcases main_cases hS hf x with (h1 | h1) | âŸ¨h1, h2âŸ©
    Â· rw [h1, sq, zero_mul] at h0
      rw [â† mul_one (f _), â† h0, mul_zero]
    Â· exact h1
    Â· apply congrArg (f x * Â·) at h2
      rw [â† mul_assoc, â† sq, h0, one_mul, mul_neg_one] at h2
      rw [h2, add_neg_cancel] at h1
      rw [â† mul_one (f _), â† h1, mul_zero]

lemma map_eq_neg_one_imp_map_add_one {x} (h : f x = -1) : f (x + 1) = 0 :=
  (map_add_one_eq_zero_iff_map_eq hS hf).mpr (h â–¸ neg_one_sq)





/-! ### Reduction lemmas for ğ”½â‚‚-zeroes -/

namespace ReductionLemmas

variable (h : f r = -1)
include h

lemma Lem1 : f (r * r) = -1 := by
  rw [Eq2_v2 hf, map_eq_neg_one_imp_map_add_one hS hf h, sq, zero_mul, zero_sub]

lemma Lem2 : f (r * r + r) = -1 := by
  have h0 := Eq3 hf r
  rwa [map_eq_neg_one_imp_map_add_one hS hf h, mul_zero, add_zero,
    sq, mul_zero, zero_sub, mul_neg_one, h, neg_one_mul, neg_inj] at h0

omit [CharTwo R] [NoZeroDivisors S] hS in
lemma Lem3 (x) : f (r * x + 1) = f (r + x) - f x := by
  rw [hf.is_good, h, neg_one_mul, neg_add_eq_sub]

 omit [NoZeroDivisors S] hS in
lemma Lem4 {x} (h0 : âˆƒ y, x = r * y + 1) : f (r * r + x) = -f x := by
  rcases h0 with âŸ¨x, rflâŸ©
  rw [Lem3 hf h, â† add_assoc, â† mul_add, Lem3 hf h, add_add_cancel_left, neg_sub]

lemma Lem5 {x} (h0 : âˆƒ y, x = r * r * y + 1) :
    f ((r * r + r) * (r * r + r) + x) = f x := by
  rcases h0 with âŸ¨x, rflâŸ©
  have h1 : Commute (r * r) r := mul_assoc r r r
  rw [add_mul_self_of_Commute h1, add_assoc, Lem4 hf (Lem1 hS hf h), Lem4 hf h, neg_neg]
  exacts [âŸ¨r * x, by rw [mul_assoc]âŸ©, âŸ¨1 + x, by rw [â† add_assoc, mul_one_add (r * r)]âŸ©]

lemma Lem6 : QuasiPeriodic f (r * r * (r + 1)) :=
  (QuasiPeriodic.iff_left2 hf).mpr Î» x â†¦ by
    ---- `f(r^4 + r^2 + r^2 (r + 1) x + 1) = f(r^2 (r + 1) x + 1)`
    have h0 : âˆƒ y, r * r * (r + 1) * x + 1 = r * r * y + 1 :=
      âŸ¨(r + 1) * x, by rw [â† mul_assoc]âŸ©
    apply Lem5 hS hf h at h0
    ---- `f(r^4 + r^2 + r^2 (r + 1) x + 1) = -f(r^2 (r + 1) x + 1)`
    have h1 : âˆƒ y, r * r * (r + 1) * x + 1 = (r * r + r) * y + 1 :=
      âŸ¨r * x, by rw [â† mul_assoc, add_mul, mul_add_one (r * r)]âŸ©
    apply Lem4 hf (Lem2 hS hf h) at h1
    ---- Finish
    rw [h1, neg_eq_iff_add_eq_zero, â† two_mul, mul_eq_zero] at h0
    exact h0.resolve_left hS

end ReductionLemmas

end






/-! ### Characterization of elements of `R` -/

variable (hS : (2 : S) â‰  0) {f : R â†’ S} (hf : ReducedGood f)
include hS hf

theorem map_eq_neg_one_reduced_imp (h : f r = -1) : r = 0 := by
  let hf' := hf.toNontrivialGood
  ---- First show that `r^4 + r^2 = 0`
  have h0 : (r * r + r) * (r * r + r) = 0 := by
    refine (QuasiPeriodic.reduced_eq_zero_iff hf ?_).mpr
      (ReductionLemmas.Lem1 hS hf' (ReductionLemmas.Lem2 hS hf' h))
    have h0 : Commute (r * r) r := mul_assoc r r r
    rw [add_mul_self_of_Commute h0, â† mul_add_one (r * r), â† add_one_mul_self, â† mul_assoc]
    exact (ReductionLemmas.Lem6 hS hf' h).mul_right hf' (r + 1)
  ---- Next show that `r^2 + r = 0`
  replace h0 : r * r + r = 0 := by
    refine (QuasiPeriodic.reduced_eq_zero_iff hf <|
      (QuasiPeriodic.iff_left2 hf').mpr Î» x â†¦ ?_).mpr (ReductionLemmas.Lem2 hS hf' h)
    have h1 := ReductionLemmas.Lem4 hf' (ReductionLemmas.Lem2 hS hf' h) âŸ¨x, rflâŸ©
    rw [h0, zero_add, eq_neg_iff_add_eq_zero, â† two_mul, mul_eq_zero] at h1
    exact h1.resolve_left hS
  ---- Now go for the final goal
  refine (QuasiPeriodic.reduced_eq_zero_iff hf <|
    (QuasiPeriodic.iff_left2 hf').mpr Î» x â†¦ ?_).mpr h
  have h1 := hf.is_good (r + 1) (r * (x - 1))
  rwa [map_eq_neg_one_imp_map_add_one hS hf' h, zero_mul, zero_add,
    â† mul_assoc, add_one_mul r, h0, zero_mul, zero_add, hf.map_one,
    â† add_rotate, â† mul_add_one r, sub_add_cancel, eq_comm] at h1

lemma map_add_one_eq_zero_imp (h : f (x + 1) = 0) : x * x = 0 :=
  map_eq_neg_one_reduced_imp hS hf <| by
    rw [Eq2_v2 hf.toNontrivialGood, h, sq, zero_mul, zero_sub]

theorem reduced_eq_zero_iff : r = 0 â†” f r = -1 :=
  âŸ¨Î» h â†¦ h â–¸ hf.map_zero, map_eq_neg_one_reduced_imp hS hfâŸ©

theorem reduced_ğ”½â‚‚Îµ_iff : r * r = 0 â†” f (r + 1) = 0 := by
  rw [reduced_eq_zero_iff hS hf, Eq2_v2 hf.toNontrivialGood,
    sub_eq_neg_self, sq_eq_zero_iff]

theorem reduced_ğ”½â‚„_iff :
    r * (r + 1) + 1 = 0 â†” (f r + f (r + 1) = 1 âˆ§ f r * f (r + 1) = -1) := by
  rw [reduced_eq_zero_iff hS hf, Eq1 hf.toNontrivialGood, iff_and_self]
  intro h; refine (main_cases hS hf.toNontrivialGood r).elim (Î» h0 â†¦ ?_) And.left
  rw [â† mul_eq_zero, h, neg_eq_zero] at h0
  rw [â† mul_one (_ + _), h0, mul_zero]

theorem R_elts_cases (r : R) :
    ((r + 1) * (r + 1) = 0 âˆ¨ r * r = 0) âˆ¨ r * (r + 1) + 1 = 0 := by
  rw [reduced_ğ”½â‚‚Îµ_iff hS hf, reduced_ğ”½â‚‚Îµ_iff hS hf,
    reduced_ğ”½â‚„_iff hS hf, add_add_cancel_right]
  exact main_cases hS hf.toNontrivialGood r

theorem R_elts_claim1 {r s : R} (hr : r * r = 0) (hs : s * s = 0) :
    r = 0 âˆ¨ s = 0 âˆ¨ r = s := by
  ---- First show that `(rs)^2 = 0`
  have h : (r * s) * (r * s) = 0 := by
    have h : (r + s) * (r + s) = r * s + s * r := by
      rw [add_mul, mul_add, hr, zero_add, mul_add, hs, add_zero]
    rcases R_elts_cases hS hf (r + s) with (h0 | h0) | h0
    Â· rw [add_one_mul_self, h, add_eq_zero_iff_eq] at h0
      have h1 : (r * s) * (r * s) = r * s := by
        apply congrArg (Î» x â†¦ r * x * s) at h0
        rwa [mul_add, â† mul_assoc, hr, zero_mul, zero_add,
          mul_one, mul_assoc, mul_assoc, â† mul_assoc] at h0
      rcases R_elts_cases hS hf (r * s) with (h2 | h2) | h2
      Â· rw [add_one_mul_self, h1, add_eq_zero_iff_eq] at h2
        rw [h2, add_right_eq_self] at h0
        apply congrArg (r * Â·) at h0
        rw [mul_zero, â† mul_assoc, h2, one_mul] at h0
        rw [h0, zero_mul, zero_mul]
      Â· exact h2
      Â· rw [mul_add_one (r * s), h1, add_self_eq_zero, zero_add] at h2
        rw [â† mul_one (r * s * _), h2, mul_zero]
    Â· rw [h, add_eq_zero_iff_eq] at h0
      rw [â† mul_assoc, mul_assoc r, â† h0, mul_assoc, mul_assoc, hs, mul_zero, mul_zero]
    Â· rw [mul_add_one (r + s), h] at h0
      replace h : r * _ = r * 0 := congrArg (r * Â·) h0
      rw [mul_zero, mul_add_one r, mul_add, mul_add, â† mul_assoc,
        mul_add, hr, zero_mul, zero_add, zero_add, â† mul_assoc] at h
      have h1 : _ * r = 0 * r := congrArg (Â· * r) h
      rw [zero_mul, add_mul, add_mul, mul_assoc, hr, add_zero, mul_zero, zero_add] at h1
      rw [h1, zero_add, add_eq_zero_iff_eq] at h
      rw [h, hr]
  ---- Now plug in back to the functional equation
  rw [reduced_ğ”½â‚‚Îµ_iff hS hf, hf.is_good] at h
  rw [reduced_ğ”½â‚‚Îµ_iff hS hf, map_add_one_eq_zero_iff_map_eq hS hf.toNontrivialGood,
    _root_.sq_eq_one_iff, or_comm] at hr hs
  have h0 {x} : f x = -1 â†’ x = 0 := map_eq_neg_one_reduced_imp hS hf
  revert hr; refine Or.imp h0 Î» hr â†¦ ?_
  revert hs; refine Or.imp h0 Î» hs â†¦ ?_
  rw [hr, hs, mul_one, â† neg_eq_iff_add_eq_zero, eq_comm] at h
  exact add_eq_zero_iff_eq.mp (h0 h)

theorem R_elts_claim2 {r s : R} (hr : r * r = 0) (hs : s * (s + 1) + 1 = 0) : r = 0 := by
  rcases R_elts_cases hS hf (r + s) with (h | h) | h
  ---- Case 1: `(r + s + 1)^2 = 0`
  Â· rcases R_elts_claim1 hS hf hr h with hr | h | h0
    Â· exact hr
    Â· rw [add_assoc, add_eq_zero_iff_eq] at h
      rw [h, add_one_mul s, add_eq_zero_iff_eq] at hr
      rw [hr, add_add_cancel_right] at hs
      rwa [â† h, hs, zero_mul, eq_comm] at hr
    Â· rw [add_assoc, self_eq_add_right] at h0
      rw [h0, mul_zero, zero_add] at hs
      rw [â† mul_one r, hs, mul_zero]
  ---- Case 2: `(r + s)^2 = 0`
  Â· rcases R_elts_claim1 hS hf hr h with hr | h | h0
    Â· exact hr
    Â· rw [â† add_eq_zero_iff_eq.mp h, mul_add_one r, hr, zero_add] at hs
      rw [add_eq_zero_iff_eq.mp hs, mul_one] at hr
      rwa [hr, add_zero] at hs
    Â· rw [self_eq_add_right] at h0
      rw [h0, zero_mul, zero_add] at hs
      rw [â† mul_one r, hs, mul_zero]
  ---- Case 3: `(r + s)^2 = (r + s) + 1`
  Â· rw [add_assoc, mul_add, add_mul, hr, zero_add,
      add_mul, â† add_assoc, add_assoc, hs, add_zero] at h
    have h0 : r * _ * s = r * 0 * s := congrArg (r * Â· * s) h
    rw [mul_zero, zero_mul, mul_add, â† mul_assoc,
      â† mul_assoc, hr, zero_mul, add_zero, mul_assoc] at h0
    rcases R_elts_claim1 hS hf hr h0 with hr | h0 | h0
    Â· exact hr
    Â· apply congrArg (Â· * (s + 1)) at h0
      rwa [zero_mul, mul_assoc, add_eq_zero_iff_eq.mp hs, mul_one] at h0
    Â· apply congrArg (r * Â·) at hs
      rwa [mul_zero, mul_add_one r, â† mul_assoc, â† h0,
        mul_add_one r, â† h0, add_add_cancel_right] at hs

omit [NoZeroDivisors S] in
theorem ğ”½â‚‚_solution (hR : âˆ€ r : R, r = 0 âˆ¨ r = 1) :
    âˆƒ Ï† : R â†’+* ğ”½â‚‚, âˆ€ x, f x = ğ”½â‚‚Map (Ï† x) :=
  have h : (ğ”½â‚‚.cast : ğ”½â‚‚ â†’ R).Bijective :=
    âŸ¨ğ”½â‚‚.castRingHom_injective Î» h â†¦ hS <| by
      apply congrArg f at h; rw [hf.map_one, hf.map_zero, zero_eq_neg] at h
      rw [â† mul_one 2, h, mul_zero],
    Î» x â†¦ (hR x).elim (Î» h â†¦ âŸ¨ğ”½â‚‚.O, h.symmâŸ©) (Î» h â†¦ âŸ¨ğ”½â‚‚.I, h.symmâŸ©)âŸ©
  have h0 : âˆ€ x, f (ğ”½â‚‚.cast x) = ğ”½â‚‚Map x
    | ğ”½â‚‚.O => by
        change f 0 = ((-1 : â„¤) : S)
        rw [hf.map_zero, Int.cast_neg, Int.cast_one]
    | ğ”½â‚‚.I => (hf.map_one).trans Int.cast_zero.symm
  let Ï := RingEquiv.ofBijective ğ”½â‚‚.castRingHom h
  âŸ¨Ï.symm, Î» x â†¦ h0 _ â–¸ congrArg f (Equiv.apply_symm_apply Ï.toEquiv _).symmâŸ©

theorem ğ”½â‚‚Îµ_solution {r : R} (hr : r â‰  0) (hr0 : r * r = 0) :
    âˆƒ Ï† : R â†’+* ğ”½â‚‚Îµ, âˆ€ x, f x = ğ”½â‚‚ÎµMap (Ï† x) :=
  have h : (ğ”½â‚‚Îµ.cast r : ğ”½â‚‚Îµ â†’ R).Bijective := by
    refine âŸ¨ğ”½â‚‚Îµ.castRingHom_injective hr0 hr, Î» x â†¦ ?_âŸ©
    rcases R_elts_cases hS hf x with (h0 | h0) | h0
    Â· exact ((R_elts_claim1 hS hf hr0 h0).resolve_left hr).elim
        (Î» h1 â†¦ âŸ¨1, (add_eq_zero_iff_eq.mp h1).symmâŸ©)
        (Î» h1 â†¦ âŸ¨ğ”½â‚‚Îµ.Y, add_eq_iff_eq_add.mpr h1âŸ©)
    Â· exact ((R_elts_claim1 hS hf hr0 h0).resolve_left hr).elim
        (Î» h1 â†¦ âŸ¨0, h1.symmâŸ©) (Î» h1 â†¦ âŸ¨ğ”½â‚‚Îµ.X, h1âŸ©)
    Â· exact absurd (R_elts_claim2 hS hf hr0 h0) hr
  have h0 : âˆ€ x, f (ğ”½â‚‚Îµ.cast r x) = ğ”½â‚‚ÎµMap x
    | ğ”½â‚‚Îµ.O => by
        change f 0 = ((-1 : â„¤) : S)
        rw [hf.map_zero, Int.cast_neg, Int.cast_one]
    | ğ”½â‚‚Îµ.I => (hf.map_one).trans Int.cast_zero.symm
    | ğ”½â‚‚Îµ.X => by
        change f r = ((1 : â„¤) : S)
        have h0 := Eq2 hf.toNontrivialGood r
        rw [hr0, zero_add, hf.map_one, eq_comm, sub_eq_zero, _root_.sq_eq_one_iff] at h0
        refine (h0.resolve_right Î» h1 â†¦ hr ?_).trans Int.cast_one.symm
        exact map_eq_neg_one_reduced_imp hS hf h1
    | ğ”½â‚‚Îµ.Y => ((reduced_ğ”½â‚‚Îµ_iff hS hf).mp hr0).trans Int.cast_zero.symm
  let Ï := RingEquiv.ofBijective (ğ”½â‚‚Îµ.castRingHom hr0) h
  âŸ¨Ï.symm, Î» x â†¦ h0 _ â–¸ congrArg f (Equiv.apply_symm_apply Ï.toEquiv _).symmâŸ©

theorem ğ”½â‚„_solution {r : R} (hr : r * (r + 1) + 1 = 0) :
    âˆƒ (Ï† : R â†’+* ğ”½â‚„) (Î¹ : â„¤Ï† â†’+* S), âˆ€ x, f x = Î¹ (ğ”½â‚„Map (Ï† x)) := by
  have X : (1 : R) â‰  0 := Î» X â†¦ hS <| by
    apply congrArg f at X; rw [hf.map_one, hf.map_zero, zero_eq_neg] at X
    rw [â† mul_one 2, X, mul_zero]
  obtain âŸ¨hr0, hr1âŸ© := (reduced_ğ”½â‚„_iff hS hf).mp hr
  have hr' : r * r + r = 1 := by rwa [â† mul_add_one r, â† add_eq_zero_iff_eq]
  ---- Bijectivity of `R â†’ ğ”½â‚„`
  have h : (ğ”½â‚„.cast r : ğ”½â‚„ â†’ R).Bijective := by
    refine âŸ¨ğ”½â‚„.castRingHom_injective hr' X, Î» x â†¦ ?_âŸ©
    have h0 {x : R} (h0 : x * x = 0) : x = 0 := R_elts_claim2 hS hf h0 hr
    rcases R_elts_cases hS hf x with (h1 | h1) | h1
    Â· exact âŸ¨1, (add_eq_zero_iff_eq.mp (h0 h1)).symmâŸ©
    Â· exact âŸ¨0, (h0 h1).symmâŸ©
    rcases R_elts_cases hS hf (x + r) with (h2 | h2) | h2
    Â· exact âŸ¨ğ”½â‚„.Y, (add_eq_zero_iff_eq.mp <| (add_assoc x r 1).symm.trans (h0 h2)).symmâŸ©
    Â· exact âŸ¨ğ”½â‚„.X, (add_eq_zero_iff_eq.mp (h0 h2)).symmâŸ©
    rw [_root_.add_comm x r, add_assoc, mul_add, add_mul, add_mul, add_assoc, add_assoc,
      add_assoc, h1, add_zero, mul_add_one r, â† add_assoc (x * r), add_left_comm, hr'] at h2
    rcases R_elts_cases hS hf (x * r) with (h3 | h3) | h3
    Â· apply h0 at h3
      rw [add_right_comm, h3, zero_add] at h2
      apply congrArg (r * Â·) at h3
      rw [mul_zero, mul_add_one r, â† mul_assoc, h2, zero_mul, zero_add] at h3
      rw [h3, zero_mul, zero_add] at hr
      exact absurd hr X
    Â· apply h0 at h3
      rw [h3, zero_add, add_eq_zero_iff_eq] at h2
      have h4 : r = 0 := by rw [â† one_mul r, â† h2, mul_assoc, h3, mul_zero]
      rw [h4, zero_mul, zero_add] at hr
      exact absurd hr X
    Â· rw [add_right_comm, add_eq_zero_iff_eq] at h2
      rw [mul_add_one x, add_assoc, add_eq_zero_iff_eq] at h1
      rw [h2, â† mul_assoc, mul_assoc x, add_eq_iff_eq_add'.mp hr', mul_add_one x, add_mul,
        h1, add_assoc, add_add_cancel_right, â† add_one_mul _ x, h2, mul_assoc, h1] at h3
      apply congrArg (Â· * x) at h3
      rw [zero_mul, mul_assoc, add_one_mul x, h1, add_add_cancel_middleâ‚‚, mul_one] at h3
      rw [h3, zero_mul, zero_add] at hr
      exact absurd hr X
  ---- Value check
  rw [â† eq_sub_iff_add_eq'] at hr0
  rw [hr0, â† neg_sub, mul_neg, neg_inj, mul_sub_one, sub_eq_iff_eq_add'] at hr1
  let Î¹ := â„¤Ï†.castRingHom hr1
  have h0 : âˆ€ x, f (ğ”½â‚„.cast r x) = Î¹ (ğ”½â‚„Map x)
    | ğ”½â‚„.O => by change f 0 = Î¹ (-1)
                 rw [hf.map_zero, Î¹.map_neg, Î¹.map_one]
    | ğ”½â‚„.I => (hf.map_one).trans Î¹.map_zero.symm
    | ğ”½â‚„.X => (â„¤Ï†.cast_Ï† _).symm
    | ğ”½â‚„.Y => by change f (r + 1) = ((1 : â„¤) : S) + (-1 : â„¤) â€¢ f r
                 rw [Int.cast_one, neg_one_zsmul, hr0, sub_eq_add_neg]
  ---- Summary
  let Ï := RingEquiv.ofBijective (ğ”½â‚„.castRingHom hr') h
  exact âŸ¨Ï.symm, Î¹, Î» x â†¦ h0 _ â–¸ congrArg f (Equiv.apply_symm_apply Ï.toEquiv _).symmâŸ©

theorem solution :
    (âˆƒ Ï† : R â†’+* ğ”½â‚‚Îµ, âˆ€ x, f x = ğ”½â‚‚ÎµMap (Ï† x)) âˆ¨
    (âˆƒ (Ï† : R â†’+* ğ”½â‚„) (Î¹ : â„¤Ï† â†’+* S), âˆ€ x, f x = Î¹ (ğ”½â‚„Map (Ï† x))) âˆ¨
    (âˆƒ Ï† : R â†’+* ğ”½â‚‚, âˆ€ x, f x = ğ”½â‚‚Map (Ï† x)) :=
  (em (âˆƒ r : R, r â‰  0 âˆ§ r * r = 0)).imp
    (Î» âŸ¨_, h, h0âŸ© â†¦ ğ”½â‚‚Îµ_solution hS hf h h0)
  Î» h â†¦ (em (âˆƒ r : R, r * (r + 1) + 1 = 0)).imp
    (Î» âŸ¨_, hâŸ© â†¦ ğ”½â‚„_solution hS hf h)
  Î» h0 â†¦ ğ”½â‚‚_solution hS hf Î» r â†¦
    ((R_elts_cases hS hf r).resolve_right Î» h1 â†¦ h0 âŸ¨r, h1âŸ©).symm.imp
      (Î» h1 â†¦ by_contra Î» h2 â†¦ h âŸ¨r, h2, h1âŸ©)
      (Î» h1 â†¦ add_eq_zero_iff_eq.mp <| by_contra Î» h2 â†¦ h âŸ¨r + 1, h2, h1âŸ©)

end SCharNeTwo





/-! ### Summary -/

theorem solution {f : R â†’ S} (hf : ReducedGood f) :
    (âˆƒ Ï† : R â†’+* S, âˆ€ x, f x = Ï† (x - 1)) âˆ¨
    (âˆƒ Ï† : R â†’+* ğ”½â‚‚Îµ, âˆ€ x, f x = ğ”½â‚‚ÎµMap (Ï† x)) âˆ¨
    (âˆƒ (Ï† : R â†’+* ğ”½â‚„) (Î¹ : â„¤Ï† â†’+* S), âˆ€ x, f x = Î¹ (ğ”½â‚„Map (Ï† x))) âˆ¨
    (âˆƒ Ï† : R â†’+* ğ”½â‚‚, âˆ€ x, f x = ğ”½â‚‚Map (Ï† x)) :=
  (em ((2 : S) = 0)).imp
    (Î» h â†¦ haveI : CharTwo S := Semiring_of_two_eq_zero h
           SCharTwo.solution hf.toNontrivialGood)
    (Î» h â†¦ SCharNeTwo.solution h hf)
