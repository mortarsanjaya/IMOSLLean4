/-
Copyright (c) 2024 Gian Cordana Sanjaya. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Gian Cordana Sanjaya
-/

import IMOSLLean4.Main.IMO2012.A5.A5Answers.SqSubOneMap
import IMOSLLean4.Main.IMO2012.A5.A5Answers.F3Map2
import IMOSLLean4.Main.IMO2012.A5.A5Answers.Z4Map
import IMOSLLean4.Main.IMO2012.A5.A5General.A5CommLift
import IMOSLLean4.Main.IMO2012.A5.A5General.A5QuasiPeriodic
import IMOSLLean4.Extra.SquareLike

/-!
# IMO 2012 A5 (Case 2: `f(-1) = 0`, `char(R) ‚à§ 2`)

We solve the case where `f` is reduced good, `f(-1) = 0`, and `char(R) ‚à§ 2`.
Actually, `f(-1) = 0` implies that `f` is even, so the latter is assumed instead.
-/

namespace IMOSL
namespace IMO2012A5
namespace Case2

/-! ### General lemmas -/

section

variable [NonAssocRing R] [NonAssocSemiring S] {f : R ‚Üí S}

theorem map_even_of_map_one (hf : good f) (h : f (-1) = 0) (x) : f (-x) = f x := by
  specialize hf (x + 1) (-1)
  rwa [h, mul_zero, zero_add, add_neg_cancel_right,
    mul_neg_one, neg_add, neg_add_cancel_right] at hf

variable (hf : NontrivialGood f) (h : ‚àÄ x, f (-x) = f x)
include hf h

/-- (2.1) -/
theorem Eq1 (x y) : f (x * y - 1) = f x * f y + f (x - y) := by
  rw [‚Üê h y, sub_eq_add_neg x, ‚Üê hf.is_good, mul_neg, neg_add_eq_sub, ‚Üê neg_sub, h]

omit h in
/-- (2.2) -/
theorem Eq2 (x) : f (x * 2 - 1) = f (x - 1) * f 2 + f (x + 1) := by
  have h0 := hf.is_good (x - 1) (1 + 1)
  rwa [sub_add_add_cancel, one_add_one_eq_two, mul_two, add_assoc,
    sub_add_cancel, ‚Üê add_sub_right_comm, ‚Üê mul_two] at h0

/-- (2.3) -/
theorem Eq3 (x) : f (x * 2 + 1) = f (x + 1) * f 2 + f (x - 1) := by
  have h0 := Eq2 hf (-x)
  rwa [neg_mul, ‚Üê neg_add', h, ‚Üê neg_add', h, neg_add_eq_sub, ‚Üê neg_sub, h] at h0

/-- (2.5) -/
theorem Eq5 {x} (h0 : f x = 0) (h1 : f (x + 1) = 0) : ‚àÄ y, f (y + (2 * x + 1)) = f y :=
  suffices ‚àÄ y, f (x + y + 1) = f (x - y) from Œª y ‚Ü¶ by
    rw [two_mul, ‚Üê add_assoc, add_left_comm, this, sub_add_cancel_right, h]
  Œª y ‚Ü¶ by
    have h2 : f (x * ((x + 1) * y) + 1) = f ((x + 1) * (x * y) + 1) := by
      rw [add_one_mul x, mul_add, add_one_mul x]
    have h3 : x + (x + 1) * y = (x + 1) * (y + 1) - 1 := by
      rw [mul_add_one _ y, add_sub_assoc, add_sub_cancel_right, add_comm]
    rwa [hf.is_good, h3, Eq1 hf h, hf.is_good, ‚Üê add_rotate, ‚Üê mul_add_one x,
      hf.is_good, h0, h1, zero_mul, zero_add, zero_mul, zero_add, zero_add,
      zero_mul, zero_add, add_sub_add_right_eq_sub, ‚Üê add_assoc, eq_comm] at h2

end


namespace CommCase

variable [Ring R] [CommRing S] [NoZeroDivisors S] {f : R ‚Üí S}
  (hf : NontrivialGood f) (h : ‚àÄ x, f (-x) = f x)
include hf h

omit [NoZeroDivisors S] in
/-- (2.4) (commutative version only) -/
theorem Eq4 (x) : f x * f (x * 2 - 1) = (f (x - 1) + 1) * f (x * 2 + 1) := by
  have h0 : x * (x + 1) - 1 = (x - 1) * (x + 1 + 1) + 1 := by
    rw [mul_add_one (x - 1), add_assoc, sub_add_cancel, sub_one_mul,
      ‚Üê add_sub_right_comm, add_comm, add_sub_add_right_eq_sub]
  apply congrArg f at h0
  rw [Eq1 hf h, hf.is_good, sub_add_cancel_left, h, hf.map_one, sub_add_add_cancel,
    add_zero, add_assoc, one_add_one_eq_two, ‚Üê add_assoc, ‚Üê mul_two] at h0
  rw [Eq2 hf, mul_add, h0, ‚Üê add_assoc, add_one_mul (f _),
    add_left_inj, mul_left_comm, ‚Üê mul_add, ‚Üê hf.is_good]

omit [NoZeroDivisors S] in
/-- (2.4), alternate version -/
theorem Eq4_alt (x) : f x * f (x * 2 + 1) = (f (x + 1) + 1) * f (x * 2 - 1) := by
  have h0 := Eq4 hf h (-x)
  rwa [h, neg_mul, ‚Üê neg_add', h, ‚Üê neg_add', h, neg_add_eq_sub, ‚Üê neg_sub, h] at h0

/-- `R` has characteristic `2` if `f(2) = -1` -/
theorem two_periodic_of_map_two (h0 : f 2 = -1) : ‚àÄ x, f (x + 2) = f x :=
  have h1 (x) : f (x * 2 + 1) = f (x + 2) - f x := by
    rw [hf.is_good, h0, mul_neg_one, neg_add_eq_sub]
  have h2 (x) : f (x * 2 + 1) = -f (x * 2 - 1) := by
    rw [Eq2 hf, Eq3 hf h, h0, mul_neg_one, mul_neg_one, neg_add_rev, neg_neg]
  ---- First get the main ineq
  have h3 (x) : f x + f (x + 1) = -1 ‚à® f (x * 2 - 1) = 0 := by
    have h3 := Eq4_alt hf h x
    rw [h2, mul_neg, neg_eq_iff_add_eq_zero, ‚Üê add_mul, mul_eq_zero, ‚Üê add_assoc] at h3
    exact h3.imp_left eq_neg_of_add_eq_zero_left
  ---- Now
  Œª x ‚Ü¶ (h3 (x + 1)).symm.elim
    (Œª h4 ‚Ü¶ by rwa [mul_two, add_sub_assoc, add_sub_cancel_right,
      add_right_comm, ‚Üê mul_two, h1, sub_eq_zero] at h4)
    Œª h4 ‚Ü¶ (h3 x).symm.elim
      (Œª h5 ‚Ü¶ by rwa [‚Üê neg_eq_zero, ‚Üê h2, h1, sub_eq_zero] at h5)
      (Œª h5 ‚Ü¶ by rwa [‚Üê h5, add_comm, add_left_inj, add_assoc, one_add_one_eq_two] at h4)

omit [CommRing S] [NoZeroDivisors S] hf h in
theorem Eq6_ring_id {S} [CommRing S] (a b c d : S) :
    a * (c * d + b) - a * (b * d + c) - ((c + 1) * (b * d + c) - (b + 1) * (c * d + b))
      = (b + c - (a + 1) * (d - 1)) * (b - c) := by ring

/-- (2.6), commutative case -/
theorem Eq6 (h0 : f 2 ‚â† -1) : ‚àÄ x, f (x + 1) + f (x - 1) = (f x + 1) * (f 2 - 1) := by
  ---- First, either the goal holds or `f(x + 1) = f(x - 1)`
  have h1 (x) : f (x + 1) + f (x - 1) = (f x + 1) * (f 2 - 1) ‚à® f (x + 1) = f (x - 1) := by
    have h1 : _ - _ = _ - _ := congrArg‚ÇÇ (¬∑ - ¬∑) (Eq4 hf h x) (Eq4_alt hf h x)
    rw [Eq2 hf, Eq3 hf h, ‚Üê sub_eq_zero, Eq6_ring_id, mul_eq_zero] at h1
    exact h1.imp eq_of_sub_eq_zero eq_of_sub_eq_zero
  ---- Continue
  intro x; refine (h1 x).elim id Œª h2 ‚Ü¶ ?_
  specialize h1 (x + 1)
  rw [add_sub_cancel_right, add_assoc, one_add_one_eq_two] at h1
  rcases h1 with h1 | h1
  ¬∑ have h2 := Eq3 hf h x
    rw [hf.is_good, eq_sub_of_add_eq h1, add_sub_left_comm, ‚Üê mul_sub_one,
      add_one_mul (f _), add_assoc, ‚Üê one_add_mul (f x), mul_sub_one,
      ‚Üê add_sub_right_comm, add_sub_assoc, add_right_inj, add_comm] at h2
    exact (eq_add_of_sub_eq' h2).symm
  ¬∑ have h0 : f 2 + 1 ‚â† 0 := Œª X ‚Ü¶ h0 (eq_neg_of_add_eq_zero_left X)
    have h3 := Eq3 hf h x
    rw [‚Üê h2, hf.is_good, h1, ‚Üê mul_add_one (f x), ‚Üê mul_add_one (f _),
      ‚Üê sub_eq_zero, ‚Üê sub_mul, mul_eq_zero, or_iff_left h0, sub_eq_zero] at h3
    have h4 := Eq4 hf h x
    rw [Eq3 hf h, Eq2 hf, ‚Üê h2, ‚Üê h3, ‚Üê sub_eq_zero, ‚Üê sub_mul, sub_add_cancel_left,
      neg_one_mul, neg_eq_zero, ‚Üê mul_add_one (f x), mul_eq_zero, or_iff_left h0] at h4
    rw [eq_comm, h4] at h3; rw [eq_comm, h3] at h2
    have h5 := Eq5 hf h h4 h3 0
    rw [zero_add, hf.is_good, add_comm 2 x, h1, h4, mul_zero,
      add_zero, hf.map_zero, eq_comm, neg_eq_zero] at h5
    rw [‚Üê sub_eq_zero, ‚Üê one_mul (_ - _), h5, zero_mul]

/-- (2.7), commutative case -/
theorem Eq7 (h0 : f 2 ‚â† -1) (h1 : f 2 ‚â† 1) (x) :
    (f (x + 1) + 1) * (f (x - 1) + 1) = f x ^ 2 := by
  ---- Reduce to the case where `f(2x + 1) = f(2x - 1) = 0`
  have h2 : _ * _ = _ * _ := congrArg (f x * ¬∑) (Eq4_alt hf h x)
  rw [‚Üê mul_assoc, ‚Üê sq, mul_left_comm, Eq4 hf h,
    ‚Üê mul_assoc, ‚Üê sub_eq_zero, ‚Üê sub_mul, mul_eq_zero] at h2
  rcases h2 with h2 | h2; exact (eq_of_sub_eq_zero h2).symm
  have h3 : _ * _ = _ * _ := congrArg (f x * ¬∑) (Eq4 hf h x)
  rw [‚Üê mul_assoc, ‚Üê sq, mul_left_comm, Eq4_alt hf h,
    ‚Üê mul_assoc, ‚Üê sub_eq_zero, ‚Üê sub_mul, mul_eq_zero] at h3
  rcases h3 with h3 | h3; rwa [sub_eq_zero, eq_comm, mul_comm] at h3
  ---- Solve the case where `f(2x + 1) = f(2x - 1) = 0`
  rw [Eq3 hf h] at h2
  replace h3 : _ + _ = _ + _ := congrArg‚ÇÇ (¬∑ + ¬∑) h2 h3
  rw [Eq2 hf, add_add_add_comm, add_zero, ‚Üê add_mul,
    add_comm (f _), ‚Üê mul_add_one (Œ± := S), mul_eq_zero,
    or_iff_left (h0 ‚àò eq_neg_of_add_eq_zero_left), ‚Üê eq_neg_iff_add_eq_zero] at h3
  have X : f 2 - 1 ‚â† 0 := h1 ‚àò eq_of_sub_eq_zero
  rw [h3, ‚Üê sub_eq_add_neg, ‚Üê mul_sub_one, mul_eq_zero, or_iff_left X] at h2
  rw [h2, neg_zero] at h3
  have h4 := Eq6 hf h h0 x
  rw [h2, h3, add_zero, zero_eq_mul, or_iff_left X] at h4
  rw [h2, h3, zero_add, one_mul, eq_neg_of_add_eq_zero_left h4, neg_one_sq]

end CommCase








/-! ### Restriction to the case `f(2) ‚â† -1` -/

structure GoodCase2 [NonAssocRing R] [NonAssocRing S] (f : R ‚Üí S)
    extends NontrivialGood f : Prop where
  map_even : ‚àÄ x, f (-x) = f x
  map_two_ne_neg_one : f 2 ‚â† -1

structure RGoodCase2 [NonAssocRing R] [NonAssocRing S] (f : R ‚Üí S)
    extends ReducedGood f, GoodCase2 f : Prop


namespace GoodCase2

variable {R : Type u} {S : Type v} [Ring R] [Ring S] {f : R ‚Üí S} (hf : GoodCase2 f)
include hf

theorem Rtwo_ne_zero : (2 : R) ‚â† 0 :=
  Œª h ‚Ü¶ hf.map_two_ne_neg_one <| by rw [h, hf.map_zero]

/-- One-variable lift for the current case -/
theorem oneVarLift_exists [NoZeroDivisors S] (c : R) :
    ‚àÉ (R' : Type u) (_ : CommRing R')
        (œÜ : R' ‚Üí+* R) (_ : Function.Injective œÜ) (_ : c ‚àà Set.range œÜ)
      (S' : Type v) (_ : CommRing S') (_ : NoZeroDivisors S')
        (œÅ : S' ‚Üí+* S) (_ : Function.Injective œÅ)
      (f' : R' ‚Üí S') (_ : ‚àÄ a, f (œÜ a) = œÅ (f' a)), GoodCase2 f' := by
  rcases CommSubring.oneVarCommLiftDomain_exists hf.toNontrivialGood c with
    ‚ü®R', R'comm, œÜ, hœÜ, ‚ü®x, rfl‚ü©, S', S'comm, S'nzd, œÅ, hœÅ, f', h0, hf'‚ü©
  refine ‚ü®R', R'comm, œÜ, hœÜ, ‚ü®x, rfl‚ü©, S', S'comm, S'nzd, œÅ, hœÅ, f', h0, hf', ?_, ?_‚ü©
  -- `f'` is also even
  ¬∑ intro x; apply hœÅ
    rw [‚Üê h0, ‚Üê h0, œÜ.map_neg, hf.map_even]
  --` f'(2) ‚â† -1`
  ¬∑ intro h1; apply hf.map_two_ne_neg_one
    rw [‚Üê map_ofNat œÜ 2, h0, h1, œÅ.map_neg, œÅ.map_one]

theorem Eq1 : ‚àÄ x y, f (x * y - 1) = f x * f y + f (x - y) :=
  Case2.Eq1 hf.toNontrivialGood hf.map_even

/-- (2.2) -/
theorem Eq2 : ‚àÄ x, f (x * 2 - 1) = f (x - 1) * f 2 + f (x + 1) :=
  Case2.Eq2 hf.toNontrivialGood

/-- (2.3) -/
theorem Eq3 : ‚àÄ x, f (x * 2 + 1) = f (x + 1) * f 2 + f (x - 1) :=
  Case2.Eq3 hf.toNontrivialGood hf.map_even

variable [NoZeroDivisors S]

/-- (2.6) -/
theorem Eq6 (x) : f (x + 1) + f (x - 1) = (f x + 1) * (f 2 - 1) := by
  rcases oneVarLift_exists hf x with
    ‚ü®R', R'comm, œÜ, -, ‚ü®x, rfl‚ü©, S', S'comm, S'nzd, œÅ, -, f', h0, hf'‚ü©
  rw [h0, ‚Üê œÜ.map_one, ‚Üê œÜ.map_sub, ‚Üê œÜ.map_add, h0, h0, ‚Üê map_ofNat œÜ 2, h0,
    ‚Üê œÅ.map_one, ‚Üê œÅ.map_add, ‚Üê œÅ.map_add, ‚Üê œÅ.map_sub, ‚Üê œÅ.map_mul]
  exact congrArg œÅ
    (CommCase.Eq6 hf'.toNontrivialGood hf'.map_even hf'.map_two_ne_neg_one x)

/-- (2.7) -/
theorem Eq7 (h1 : f 2 ‚â† 1) (x) : (f (x + 1) + 1) * (f (x - 1) + 1) = f x ^ 2 := by
  rcases oneVarLift_exists hf x with
    ‚ü®R', R'comm, œÜ, -, ‚ü®x, rfl‚ü©, S', S'comm, S'nzd, œÅ, -, f', h0, hf'‚ü©
  have h2 : f' 2 ‚â† 1 := Œª h2 ‚Ü¶ h1 <| by rw [‚Üê map_ofNat œÜ 2, h0, h2, œÅ.map_one]
  rw [h0, ‚Üê œÜ.map_one, ‚Üê œÜ.map_sub, ‚Üê œÜ.map_add, h0, h0, ‚Üê œÅ.map_one,
    ‚Üê œÅ.map_add, ‚Üê œÅ.map_add, ‚Üê œÅ.map_mul, ‚Üê œÅ.map_pow]
  exact congrArg œÅ
    (CommCase.Eq7 hf'.toNontrivialGood hf'.map_even hf'.map_two_ne_neg_one h2 x)

theorem map_two_cases : f 2 = 1 ‚à® f 2 = 0 ‚à® f 2 = 3 := by
  have h := Eq1 hf (2 + 1) 2
  rw [add_sub_cancel_left, hf.map_one, add_zero,
    mul_two, add_sub_assoc, add_sub_cancel_right] at h
  have h0 := Eq6 hf (2 + 1 + 1)
  rw [add_sub_cancel_right, add_assoc, one_add_one_eq_two, h, ‚Üê mul_add_one (f _)] at h0
  replace h := Eq6 hf (2 + 1)
  rw [add_sub_cancel_right, add_one_mul (Œ± := S), add_sub_left_comm,
    add_comm, add_right_inj, eq_sub_iff_add_eq] at h
  rw [h, mul_assoc, eq_comm, ‚Üê sub_eq_zero, ‚Üê mul_sub, mul_eq_zero] at h0
  clear h; revert h0; refine Or.imp (Œª h ‚Ü¶ ?_) (Œª h ‚Ü¶ ?_)
  ¬∑ have h0 := Eq1 hf 2 (1 + 1)
    rw [two_mul, ‚Üê add_assoc, add_sub_cancel_right, one_add_one_eq_two, sub_self,
      hf.map_zero, h, eq_comm, add_neg_eq_zero, mul_self_eq_one_iff] at h0
    exact h0.resolve_right hf.map_two_ne_neg_one
  ¬∑ rw [mul_sub_one, sub_one_mul, sub_sub, sub_add_add_cancel, ‚Üê two_mul,
      sub_sub, ‚Üê one_add_mul 2 (f 2), ‚Üê sub_mul, mul_eq_zero, add_comm] at h
    exact h.symm.imp_right Œª h ‚Ü¶ (eq_of_sub_eq_zero h).trans two_add_one_eq_three

end GoodCase2





/-! ### Subcase 2.1: `f(2) = 1`, `char(S) ‚à§ 2` -/

theorem RGoodSubcase21.solution [Ring R] [Ring S] [NoZeroDivisors S]
    {f : R ‚Üí S} (hf : RGoodCase2 f) (h : f 2 = 1) :
    ‚àÉ œÜ : R ‚Üí+* ‚Ñ§‚ÇÑ, ‚àÄ x, f x = ‚Ñ§‚ÇÑMap (œÜ x) :=
  have h0 (x) : f (x + 2) = -f x := by
    have h0 := hf.toGoodCase2.Eq6 (x + 1)
    rw [h, sub_self, mul_zero, add_sub_cancel_right, add_assoc, one_add_one_eq_two] at h0
    exact eq_neg_iff_add_eq_zero.mpr h0
  have h1 : (4 : R) = 0 := hf.period_imp_zero Œª x ‚Ü¶ by
    have h1 : (2 : R) + 2 = 4 := by norm_num
    rw [‚Üê h1, ‚Üê add_assoc, h0, h0, neg_neg]
  have hInj : (‚Ñ§‚ÇÑ.cast : ‚Ñ§‚ÇÑ ‚Üí R).Injective :=
    ‚Ñ§‚ÇÑ.castRingHom_injective h1 hf.toGoodCase2.Rtwo_ne_zero
  have hSurj : (‚Ñ§‚ÇÑ.cast : ‚Ñ§‚ÇÑ ‚Üí R).Surjective :=
    have h2 : QuasiPeriodic f 2 := (QuasiPeriodic.iff_right hf.toNontrivialGood).mpr
      Œª x ‚Ü¶ by rw [h0, h, mul_neg_one]
    have X : (2 : R) + 1 = -1 := by
      rwa [two_add_one_eq_three, eq_neg_iff_add_eq_zero, three_add_one_eq_four]
    Œª x ‚Ü¶ (h2.reduced_main_cases hf.toReducedGood hf.toGoodCase2.Rtwo_ne_zero x).elim
      (Œª h3 ‚Ü¶ h3.elim (Œª h3 ‚Ü¶ ‚ü®‚Ñ§‚ÇÑ.‚Ñ§‚ÇÑ0, h3.symm‚ü©) (Œª h3 ‚Ü¶ ‚ü®‚Ñ§‚ÇÑ.‚Ñ§‚ÇÑ2, h3.symm‚ü©))
      (Œª h3 ‚Ü¶ h3.elim (Œª h3 ‚Ü¶ ‚ü®‚Ñ§‚ÇÑ.‚Ñ§‚ÇÑ1, h3.symm‚ü©) (Œª h3 ‚Ü¶ ‚ü®‚Ñ§‚ÇÑ.‚Ñ§‚ÇÑ3, (h3.trans X).symm‚ü©))
  have h2 : (‚Ñ§‚ÇÑ.cast : ‚Ñ§‚ÇÑ ‚Üí R).Bijective := ‚ü®hInj, hSurj‚ü©
  have h3 : ‚àÄ x, f (‚Ñ§‚ÇÑ.cast x) = ‚Ñ§‚ÇÑMap x
    | ‚Ñ§‚ÇÑ.‚Ñ§‚ÇÑ0 => by
        change f 0 = ((-1 : ‚Ñ§) : S)
        rw [hf.map_zero, Int.cast_neg, Int.cast_one]
    | ‚Ñ§‚ÇÑ.‚Ñ§‚ÇÑ1 => hf.map_one.trans Int.cast_zero.symm
    | ‚Ñ§‚ÇÑ.‚Ñ§‚ÇÑ2 => h.trans Int.cast_one.symm
    | ‚Ñ§‚ÇÑ.‚Ñ§‚ÇÑ3 => (hf.map_even _).trans (hf.map_one.trans Int.cast_zero.symm)
  let œÅ := RingEquiv.ofBijective (‚Ñ§‚ÇÑ.castRingHom h1) h2
  ‚ü®œÅ.symm, Œª x ‚Ü¶ h3 _ ‚ñ∏ congrArg f (Equiv.apply_symm_apply œÅ.toEquiv _).symm‚ü©





/-! ### Subcase 2.2: `f(2) = 0`, `char(S) ‚à§ 3` -/

namespace RGoodSubcase22

variable [Ring R] [Ring S] [NoZeroDivisors S] {f : R ‚Üí S} (hf : RGoodCase2 f) (h : f 2 = 0)
include hf h

/-- (2.2.1) -/
theorem Eq1 (x) : f (x + 1) + f (x - 1) + f x = -1 := by
  rw [hf.toGoodCase2.Eq6, h, zero_sub, mul_neg_one, neg_add_rev, neg_add_cancel_right]

theorem Rchar : (3 : R) = 0 := by
  rw [‚Üê two_add_one_eq_three, ‚Üê eq_neg_iff_add_eq_zero]
  refine hf.period_imp_eq _ _ Œª x ‚Ü¶ ?_
  have h0 := Eq1 hf h (x + 1)
  rwa [add_sub_cancel_right, add_assoc x, one_add_one_eq_two, ‚Üê Eq1 hf h x,
    ‚Üê add_rotate, add_left_inj, add_right_inj, sub_eq_add_neg] at h0

theorem Rchar' : (2 : R) = -1 := by
  rw [eq_neg_iff_add_eq_zero, two_add_one_eq_three, Rchar hf h]

/-- (2.2.2) -/
theorem Eq2 (x) : f (x + 1) * f (x - 1) = (f x + 1) * f x := by
  have h0 : f 2 ‚â† 1 := Œª h0 ‚Ü¶ hf.map_two_ne_neg_one <| by rw [h, zero_eq_neg, ‚Üê h, ‚Üê h0]
  rw [add_one_mul (f x), ‚Üê sq, ‚Üê hf.toGoodCase2.Eq7 h0, add_one_mul (f _),
    mul_add_one (f _), add_assoc, add_assoc, self_eq_add_right,
    add_right_comm, ‚Üê add_assoc, ‚Üê add_assoc, Eq1 hf h, neg_add_cancel]

theorem map_eq_neg_one_imp (h0 : f x = -1) : x = 0 := by
  have h1 : f (x + 1) = 0 ‚àß f (x - 1) = 0 := by
    have h1 : f (x + 1) + f (x - 1) = 0 := by
      rw [eq_sub_of_add_eq (Eq1 hf h x), h0, sub_self]
    obtain h2 | h2 : f (x + 1) = 0 ‚à® f (x - 1) = 0 := by
      rw [‚Üê mul_eq_zero, Eq2 hf h, h0, neg_add_cancel, zero_mul]
    ¬∑ rw [h2, zero_add] at h1; exact ‚ü®h2, h1‚ü©
    ¬∑ rw [h2, add_zero] at h1; exact ‚ü®h1, h2‚ü©
  ---- Grind the rest
  rw [sub_eq_add_neg, ‚Üê Rchar' hf h, ‚Üê one_add_one_eq_two, ‚Üê add_assoc] at h1
  replace h1 := hf.toReducedGood.period_imp_zero
    (Eq5 hf.toNontrivialGood hf.map_even h1.1 h1.2)
  rwa [Rchar' hf h, neg_one_mul, neg_add_rev, neg_add_cancel_comm, neg_eq_zero] at h1

theorem map_eq_neg_one_or_zero (h0 : (3 : S) ‚â† 0) (x) : f x = -1 ‚à® f x = 0 := by
  have Rchar' := Rchar' hf h
  ---- Get `f(x^2 + 1)`, `f(x^2 - 1)`, and `f(x^2)`
  have h1 : f (x * x + 1) = (f x + 1) * f x := by
    rw [hf.is_good, ‚Üê two_mul, Rchar', neg_one_mul, hf.map_even, add_one_mul (f x)]
  have h2 : f (x * x - 1) = f x * f x - 1 := by
    rw [Case2.Eq1 hf.toNontrivialGood hf.map_even, sub_self, hf.map_zero, sub_eq_add_neg]
  have h3 : f (x * x) = (f x + 1 + 1) * f x := by
    have h2 := hf.is_good (x + 1) (x - 1)
    rw [Eq2 hf h, add_one_mul x, mul_sub_one, sub_add_sub_cancel, sub_add_cancel,
      add_add_sub_cancel, ‚Üê two_mul, Rchar', neg_one_mul, hf.map_even] at h2
    exact h2.trans (add_one_mul _ (f x)).symm
  ---- Add all three equations and grind
  have h4 := Eq1 hf h (x * x)
  rw [h1, h2, h3, ‚Üê add_sub_assoc, ‚Üê add_mul, ‚Üê add_sub_right_comm,
    sub_eq_neg_self, ‚Üê add_mul, mul_eq_zero, add_add_add_comm] at h4
  clear h1 h2 h3; revert h4; refine Or.imp_left Œª h1 ‚Ü¶ ?_
  rw [‚Üê two_mul, ‚Üê add_one_mul 2 (f x + 1), mul_eq_zero, two_add_one_eq_three] at h1
  exact eq_neg_of_add_eq_zero_left (h1.resolve_left h0)

theorem value_bash (h0 : (3 : S) ‚â† 0) (x : R) : x = 0 ‚à® x = 1 ‚à® x = -1 := by
  have h1 (x) : x = 0 ‚à® f x = 0 :=
    (map_eq_neg_one_or_zero hf h h0 x).imp_left (map_eq_neg_one_imp hf h)
  refine (h1 x).imp_right Œª h2 ‚Ü¶ (h1 (x - 1)).imp eq_of_sub_eq_zero Œª h3 ‚Ü¶
    (h1 (x + 1)).elim eq_neg_of_add_eq_zero_left Œª h4 ‚Ü¶ ?_
  have h5 := Eq1 hf h x
  rw [h2, h3, h4, add_zero, add_zero, eq_comm, neg_eq_zero] at h5
  rw [‚Üê mul_one 3, h5, mul_zero] at h0
  exact absurd rfl h0

theorem solution (h0 : (3 : S) ‚â† 0) : ‚àÉ œÜ : R ‚Üí+* ùîΩ‚ÇÉ, ‚àÄ x, f x = ùîΩ‚ÇÉMap2 (œÜ x) :=
  have h1 : (ùîΩ‚ÇÉ.cast : ùîΩ‚ÇÉ ‚Üí R).Bijective :=
    ‚ü®ùîΩ‚ÇÉ.castRingHom_injective (Rchar hf h) Œª h ‚Ü¶ h0 <| by
      have h0 := hf.map_one; rw [h, hf.map_zero, neg_eq_zero] at h0
      rw [‚Üê mul_one 3, h0, mul_zero],
    Œª x ‚Ü¶ (value_bash hf h h0 x).elim (Œª h ‚Ü¶ ‚ü®ùîΩ‚ÇÉ.ùîΩ‚ÇÉ0, h.symm‚ü©)
      Œª h ‚Ü¶ h.elim (Œª h ‚Ü¶ ‚ü®ùîΩ‚ÇÉ.ùîΩ‚ÇÉ1, h.symm‚ü©) (Œª h ‚Ü¶ ‚ü®ùîΩ‚ÇÉ.ùîΩ‚ÇÉ2, h.symm‚ü©)‚ü©
  have h2 : ‚àÄ x, f (ùîΩ‚ÇÉ.cast x) = ùîΩ‚ÇÉMap2 x
    | ùîΩ‚ÇÉ.ùîΩ‚ÇÉ0 => by
        change f 0 = ((-1 : ‚Ñ§) : S)
        rw [hf.map_zero, Int.cast_neg, Int.cast_one]
    | ùîΩ‚ÇÉ.ùîΩ‚ÇÉ1 => (hf.map_one).trans Int.cast_zero.symm
    | ùîΩ‚ÇÉ.ùîΩ‚ÇÉ2 => (hf.map_even _).trans ((hf.map_one).trans Int.cast_zero.symm)
  let œÅ := RingEquiv.ofBijective (ùîΩ‚ÇÉ.castRingHom (Rchar hf h)) h1
  ‚ü®œÅ.symm, Œª x ‚Ü¶ h2 _ ‚ñ∏ congrArg f (Equiv.apply_symm_apply œÅ.toEquiv _).symm‚ü©

end RGoodSubcase22





/-! ### Structure for Subcase 2.3: `f(2) = 3`, `char(S) ‚à§ 2` -/

/-- Structure expressing that `g - 1` is good and `(g - 1)(2) = 3` -/
structure ShiftGood23 [NonAssocRing R] [NonAssocRing S] (g : R ‚Üí S) : Prop where
  shift_good : GoodCase2 (g - 1)
  map_two : g 2 = 4

namespace ShiftGood23

/-- One-variable lift for the current case -/
theorem oneVarLift_exists {R : Type u} {S : Type v} [Ring R] [Ring S]
    [NoZeroDivisors S] {g : R ‚Üí S} (hg : ShiftGood23 g) (c : R) :
    ‚àÉ (R' : Type u) (_ : CommRing R')
        (œÜ : R' ‚Üí+* R) (_ : Function.Injective œÜ) (_ : c ‚àà Set.range œÜ)
      (S' : Type v) (_ : CommRing S') (_ : NoZeroDivisors S')
        (œÅ : S' ‚Üí+* S) (_ : Function.Injective œÅ)
      (g' : R' ‚Üí S') (_ : ‚àÄ a, g (œÜ a) = œÅ (g' a)), ShiftGood23 g' := by
  rcases hg.shift_good.oneVarLift_exists c with
    ‚ü®R', R'comm, œÜ, hœÜ, ‚ü®x, rfl‚ü©, S', S'comm, S'nzd, œÅ, hœÅ, f', h0, hf'‚ü©
  refine ‚ü®R', R'comm, œÜ, hœÜ, ‚ü®x, rfl‚ü©, S', S'comm, S'nzd, œÅ, hœÅ, f' + 1, ?_, ?_‚ü©
  -- Homomorphism transfer
  ¬∑ intro x; rw [Pi.add_apply, Pi.one_apply, œÅ.map_add, ‚Üê h0, œÅ.map_one]
    exact (sub_add_cancel _ _).symm
  -- `ShiftGood`
  ¬∑ refine ‚ü®by rwa [add_sub_cancel_right], hœÅ ?_‚ü©
    rw [Pi.add_apply, Pi.one_apply, œÅ.map_add, œÅ.map_one, ‚Üê h0, map_ofNat,
      map_ofNat, Pi.sub_apply, Pi.one_apply, hg.map_two, sub_add_cancel]


section

variable [NonAssocRing R] [NonAssocRing S]

lemma mk_iff {g : R ‚Üí S} : ShiftGood23 g ‚Üî GoodCase2 (g - 1) ‚àß g 2 = 4 :=
  ‚ü®Œª ‚ü®h, h0‚ü© ‚Ü¶ ‚ü®h, h0‚ü©, Œª ‚ü®h, h0‚ü© ‚Ü¶ ‚ü®h, h0‚ü©‚ü©

lemma shift_mk_iff {f : R ‚Üí S} : ShiftGood23 (f + 1) ‚Üî GoodCase2 f ‚àß f 2 = 3 := by
  rw [mk_iff, add_sub_cancel_right, ‚Üê three_add_one_eq_four]
  exact and_congr_right' (add_left_inj _)

variable {g : R ‚Üí S} (hg : ShiftGood23 g)
include hg

lemma map_zero : g 0 = 0 :=
  sub_eq_neg_self.mp hg.shift_good.map_zero

lemma map_one : g 1 = 1 :=
  eq_of_sub_eq_zero hg.shift_good.map_one

lemma map_even (x) : g (-x) = g x :=
  sub_left_injective (hg.shift_good.map_even x)

lemma Schar_ne_two : (2 : S) ‚â† 0 :=
  Œª h ‚Ü¶ hg.shift_good.map_two_ne_neg_one <| by
    rw [Pi.sub_apply, Pi.one_apply, sub_eq_neg_self, hg.map_two, ‚Üê three_add_one_eq_four,
      ‚Üê two_add_one_eq_three, h, zero_add, one_add_one_eq_two, h]

lemma is_good (x y) : g (x * y + 1) = (g x - 1) * (g y - 1) + g (x + y) := by
  have h := hg.shift_good.is_good x y
  simp only [Pi.sub_apply, Pi.one_apply] at h
  rwa [‚Üê add_sub_assoc, sub_left_inj] at h

lemma alt_good (x y) : g (x * y - 1) = (g x - 1) * (g y - 1) + g (x - y) := by
  have h := hg.is_good x (-y)
  rwa [hg.map_even, mul_neg, neg_add_eq_sub, ‚Üê neg_sub, hg.map_even, ‚Üê sub_eq_add_neg] at h

end


section

variable [Ring R] [Ring S] [NoZeroDivisors S] {g : R ‚Üí S} (hg : ShiftGood23 g)
include hg

/-- (2.3.1) -/
lemma Eq1 (x) : g (x + 1) + g (x - 1) = 2 * (g x + 1) := by
  have h := hg.shift_good.Eq6 x; simp only [Pi.sub_apply, Pi.one_apply] at h
  rwa [sub_add_cancel, hg.map_two, sub_eq_of_eq_add (G := S) three_add_one_eq_four.symm,
    sub_eq_of_eq_add (G := S) two_add_one_eq_three.symm, sub_add_sub_comm, mul_two,
    sub_eq_iff_eq_add, one_add_one_eq_two, ‚Üê two_mul, ‚Üê mul_add_one] at h

omit [NoZeroDivisors S] in
/-- (2.3.2) -/
lemma Eq2 (x y) : g (x * y + 1) - g (x * y - 1) = g (x + y) - g (x - y) := by
  rw [hg.is_good, hg.alt_good, add_sub_add_left_eq_sub]

lemma Eq3_1 (x) : g (x + 1) * g (x - 1) = (g x - 1) ^ 2 := by
  have h : (g - 1) 2 ‚â† 1 := Œª h0 ‚Ü¶ by
    rw [Pi.sub_apply, hg.map_two, sub_eq_of_eq_add three_add_one_eq_four.symm,
      ‚Üê two_add_one_eq_three, add_left_eq_self] at h0
    exact hg.Schar_ne_two h0
  replace h := hg.shift_good.Eq7 h x
  simp only [Pi.sub_apply, Pi.one_apply, sub_add_cancel] at h; exact h

omit [Ring S] [NoZeroDivisors S] hg in
lemma CommCase.Eq3_2 {S} [CommRing S] [NoZeroDivisors S]
    {g : R ‚Üí S} (hg : ShiftGood23 g) (x) :
    (g (x + 1) - g (x - 1)) ^ 2 = 2 ^ 4 * g x := by
  rw [sub_sq', ‚Üê sub_eq_of_eq_add (add_sq' (g _) _),
    Eq1 hg, mul_assoc, Eq3_1 hg]
  ring

lemma Eq3_2 (x) : (g (x + 1) - g (x - 1)) ^ 2 = 2 ^ 4 * g x := by
  rcases oneVarLift_exists hg x with
    ‚ü®R', R'comm, œÜ, -, ‚ü®x, rfl‚ü©, S', S'comm, S'nzd, œÅ, -, g', h0, hg'‚ü©
  rw [‚Üê œÜ.map_one, ‚Üê œÜ.map_add, ‚Üê œÜ.map_sub, h0, h0, ‚Üê œÅ.map_sub, ‚Üê œÅ.map_pow,
    CommCase.Eq3_2 hg', h0, œÅ.map_mul, œÅ.map_pow, map_ofNat]

/-- (2.3.3) -/
lemma Eq3 (x y) : (g (x + y) - g (x - y)) ^ 2 = 2 ^ 4 * g (x * y) :=
  Eq2 hg x y ‚ñ∏ Eq3_2 hg _

/-- (2.3.4) -/
lemma Eq4 (x y) : 2 * (g (x * y) - g x * g y) =
    g (x + y) + g (x - y) - 2 * (g x + g y) := by
  have h : _ + _ = _ + _ := congrArg‚ÇÇ (¬∑ + ¬∑) (hg.is_good x y) (hg.alt_good x y)
  rw [Eq1 hg, add_add_add_comm, ‚Üê two_mul, ‚Üê sub_eq_iff_eq_add', ‚Üê mul_sub] at h
  rw [‚Üê h, ‚Üê mul_sub, sub_sub, sub_one_mul, mul_sub_one, sub_sub, sub_add,
    ‚Üê add_sub_assoc, sub_sub_cancel_left, sub_neg_eq_add, add_sub_add_right_eq_sub]

/-- (2.3.5) -/
lemma Eq5 (x) : g (x * 2) = g x * 4 := by
  have h : _ + _ = _ + _ := congrArg‚ÇÇ (¬∑ + ¬∑) (hg.shift_good.Eq3 x) (hg.shift_good.Eq2 x)
  simp only [Pi.sub_apply, Pi.one_apply] at h
  rw [‚Üê add_sub_add_comm, Eq1 hg, add_add_add_comm, ‚Üê add_mul,
    add_comm (g (x - 1) - 1), mul_sub_one, sub_add_cancel, ‚Üê add_sub_add_comm,
    Eq1 hg, one_add_one_eq_two, mul_add_one (Œ± := S), add_sub_cancel_right,
    mul_add_one (Œ± := S), add_sub_cancel_right, hg.map_two, mul_assoc,
    ‚Üê sub_eq_zero, ‚Üê mul_sub, mul_eq_zero] at h
  exact eq_of_sub_eq_zero (h.resolve_left hg.Schar_ne_two)

end

end ShiftGood23




structure RShiftGood23 [NonAssocRing R] [NonAssocRing S] (g : R ‚Üí S)
  extends RGoodCase2 (g - 1), ShiftGood23 g : Prop

namespace RShiftGood23

section

variable [NonAssocRing R] [NonAssocRing S]

lemma mk_iff {g : R ‚Üí S} : RShiftGood23 g ‚Üî RGoodCase2 (g - 1) ‚àß g 2 = 4 :=
  ‚ü®Œª ‚ü®h, h0‚ü© ‚Ü¶ ‚ü®h, (ShiftGood23.mk_iff.mp h0).2‚ü©, Œª ‚ü®h, h0‚ü© ‚Ü¶ ‚ü®h, h.toGoodCase2, h0‚ü©‚ü©

lemma shift_mk_iff {f : R ‚Üí S} : RShiftGood23 (f + 1) ‚Üî RGoodCase2 f ‚àß f 2 = 3 := by
  rw [mk_iff, add_sub_cancel_right, ‚Üê three_add_one_eq_four]
  exact and_congr_right' (add_left_inj _)

variable {g : R ‚Üí S} (hg : RShiftGood23 g)
include hg

lemma period_imp_eq‚ÇÄ (c d) (h : ‚àÄ x, g (x + c) = g (x + d)) : c = d :=
  hg.period_imp_eq c d Œª x ‚Ü¶ congrArg‚ÇÇ (¬∑ - ¬∑) (h x) rfl

lemma period_imp_zero‚ÇÄ {c} (h : ‚àÄ x, g (x + c) = g x) : c = 0 :=
  hg.period_imp_eq‚ÇÄ c 0 Œª x ‚Ü¶ by rw [h, add_zero]

end


section

variable {R : Type u} [Ring R] [Ring S] [NoZeroDivisors S] {g : R ‚Üí S} (hg : RShiftGood23 g)
include hg

/-- (2.3.6) -/
lemma Eq6 {x} (h : g x = 0) : x = 0 := by
  ---- First get `g(x + 1) = g(x - 1) = 1`
  have h0 := hg.Eq3 x 1
  rw [mul_one, h, mul_zero, sq_eq_zero_iff, sub_eq_zero] at h0
  have h1 := hg.Eq1 x
  rw [h, zero_add, h0, ‚Üê two_mul, ‚Üê sub_eq_zero, ‚Üê mul_sub,
    mul_eq_zero, or_iff_right hg.Schar_ne_two, sub_eq_zero] at h1
  rw [h1] at h0
  ---- Now prove the equality `g(y - x) + 2 g(y) = 3 g(y + x)`
  have h2 (y) : g (y - x) + 2 * g y = (2 + 1) * g (y + x) := by
    have h2 : g (x + (x + 1) * (y - 1)) = g (x + 1 - y) := by
      rw [mul_sub_one, add_sub_left_comm, sub_add_cancel_left, ‚Üê sub_eq_add_neg,
        hg.toShiftGood23.alt_good, h0, sub_self, zero_mul, zero_add]
    have h3 := hg.toShiftGood23.is_good (x + 1) (x * (y - 1))
    rw [h0, sub_self, zero_mul, zero_add, ‚Üê add_rotate, ‚Üê mul_add_one x, sub_add_cancel,
      ‚Üê mul_assoc, add_one_mul x, ‚Üê mul_add_one x, mul_assoc, hg.toShiftGood23.is_good,
      h2, hg.toShiftGood23.is_good, h, zero_sub, neg_one_mul, neg_one_mul, neg_sub,
      neg_sub, sub_add, sub_add, sub_right_inj, sub_eq_iff_eq_add] at h3
    replace h2 := hg.Eq4 (x + 1) (y - 1)
    rw [h0, one_mul, mul_sub, mul_one_add (Œ± := S), ‚Üê sub_sub, sub_left_inj,
      eq_sub_iff_add_eq, h3, mul_add, add_assoc, ‚Üê mul_add_one (Œ± := S),
      add_add_sub_cancel, ‚Üê hg.Eq1, ‚Üê sub_add, ‚Üê add_assoc, ‚Üê add_rotate,
      add_left_inj, sub_sub, add_sub_add_right_eq_sub, ‚Üê neg_sub, mul_sub,
      hg.toShiftGood23.map_even, ‚Üê add_sub_assoc, sub_eq_iff_eq_add'] at h2
    rw [h2, add_one_mul (Œ± := S), add_comm x]
  ---- Finish
  refine hg.period_imp_zero‚ÇÄ Œª y ‚Ü¶ ?_
  have h3 := h2 (-y)
  rw [‚Üê neg_add', hg.toShiftGood23.map_even, hg.toShiftGood23.map_even,
    neg_add_eq_sub, ‚Üê neg_sub, hg.toShiftGood23.map_even] at h3
  replace h2 : _ - _ = _ - _ := congrArg‚ÇÇ (¬∑ - ¬∑) h3 (h2 y)
  rw [add_sub_add_right_eq_sub, ‚Üê mul_sub, ‚Üê neg_sub, neg_eq_iff_add_eq_zero,
    ‚Üê one_add_mul (Œ± := S), mul_eq_zero, add_left_comm, one_add_one_eq_two,
    ‚Üê two_mul, mul_self_eq_zero, or_iff_right hg.Schar_ne_two, sub_eq_zero] at h2
  rwa [h2, add_one_mul (Œ± := S), add_comm, add_left_inj, ‚Üê sub_eq_zero, ‚Üê mul_sub,
    mul_eq_zero, or_iff_right hg.Schar_ne_two, sub_eq_zero, eq_comm] at h3

/-- `R` is commutative -/
lemma Rcomm : ‚àÄ x y : R, x * y = y * x := by
  ---- First get `(x^2 - y^2)(xy - yx) = 0` for all `x y : R`
  have X : (2 : S) ^ 4 ‚â† 0 := pow_ne_zero 4 hg.Schar_ne_two
  have h (x y) : g (x * y) = g (y * x) := by
    have h := hg.Eq3 x y
    rw [add_comm, ‚Üê neg_sub y, hg.toShiftGood23.map_even, hg.Eq3,
      ‚Üê sub_eq_zero, ‚Üê mul_sub, mul_eq_zero, or_iff_right X] at h
    exact (eq_of_sub_eq_zero h).symm
  replace h (x y : R) : (x * x - y * y) * (x * y - y * x) = 0 := hg.Eq6 <| by
    have h0 := hg.Eq3 (x * x - y * y) (x * y - y * x)
    rwa [sub_add_sub_comm, ‚Üê mul_add, ‚Üê mul_add, add_comm y, ‚Üê sub_mul, h,
      sub_sub_sub_comm, ‚Üê mul_sub, ‚Üê mul_sub, ‚Üê neg_sub x, mul_neg, sub_neg_eq_add,
      add_mul, sub_self, sq, zero_mul, zero_eq_mul, or_iff_right X] at h0
  ---- Now reduce repeatedly
  have h0 (x y : R) : (x + 1) * y - y * (x + 1) = x * y - y * x := by
    rw [add_one_mul x, mul_add_one y, add_sub_add_right_eq_sub]
  replace h (x y : R) : (x * 2 + 1) * (x * y - y * x) = 0 := by
    have h1 := h (x + 1) y
    rwa [h0, add_one_mul x, mul_add_one x, add_assoc, add_sub_right_comm,
      add_mul, h, zero_add, ‚Üê add_assoc, ‚Üê mul_two] at h1
  intro x y; have h1 := h (x + 1) y
  rw [h0, add_one_mul x, add_right_comm, add_mul, h, zero_add] at h1
  specialize h x y
  rwa [add_one_mul (Œ± := R), mul_assoc, h1, mul_zero, zero_add, sub_eq_zero] at h

/-- (2.3.7) -/
lemma Eq7 : ‚àÄ x y, g (x * y) = g x * g y := by
  ---- First get `g(xy^2) = g(xy) g(y)` for any `x y : R`
  have h (x y) : g (x * y * y) = g (x * y) * g y := by
    suffices g (x * y + y) + g (x * y - y) = 2 * (g (x * y) + g y) by
      rw [‚Üê sub_eq_zero, ‚Üê hg.Eq4, mul_eq_zero] at this
      exact eq_of_sub_eq_zero (this.resolve_left hg.Schar_ne_two)
    have h : _ + _ = _ + _ := congrArg‚ÇÇ (¬∑ + ¬∑) (hg.Eq4 (x + 1) y) (hg.Eq4 (x - 1) y)
    rwa [‚Üê mul_add, sub_add_sub_comm, ‚Üê add_mul, sub_add_sub_comm, add_add_add_comm,
      hg.Eq1, add_right_comm x, ‚Üê add_sub_right_comm x y, hg.Eq1, add_sub_right_comm x,
      sub_right_comm, hg.Eq1, ‚Üê mul_add, ‚Üê mul_add, ‚Üê mul_sub, add_add_add_comm,
      add_add_add_comm (g _) (g y), hg.Eq1, ‚Üê sub_eq_zero, ‚Üê mul_sub, mul_eq_zero,
      or_iff_right hg.Schar_ne_two, sub_eq_zero, sub_eq_iff_eq_add, one_add_one_eq_two,
      mul_add_one (Œ± := S), ‚Üê two_mul, add_right_comm (2 * g x), add_sub_add_right_eq_sub,
      ‚Üê mul_add, ‚Üê hg.Eq4, ‚Üê mul_add_one (Œ± := S), mul_assoc, ‚Üê mul_add, add_one_mul (g x),
      add_comm _ (g y), sub_add_add_cancel, add_one_mul x, sub_one_mul] at h
  ---- Now get either `xy = 0` or `g(xy) = g(x) g(y)`
  replace h (x y) : x * y = 0 ‚à® g (x * y) = g x * g y := by
    have h0 := h 1 (x * y)
    rw [one_mul, ‚Üê mul_assoc, hg.Rcomm _ x, ‚Üê mul_assoc, h, mul_assoc, hg.Rcomm x,
      hg.Rcomm x, h, mul_assoc, hg.Rcomm y, ‚Üê sub_eq_zero, ‚Üê mul_sub, mul_eq_zero] at h0
    exact h0.imp hg.Eq6 (Œª h0 ‚Ü¶ (eq_of_sub_eq_zero h0).symm)
  ---- Finally, bash again
  intro x y; refine (h x y).elim (Œª h0 ‚Ü¶ ?_) id
  refine (h (x + 1) y).elim (Œª h1 ‚Ü¶ ?_) (Œª h1 ‚Ü¶ ?_)
  ¬∑ rw [add_one_mul x, h0, zero_add] at h1
    rw [h1, mul_zero, hg.toShiftGood23.map_zero, mul_zero]
  refine (h (x - 1) y).elim (Œª h2 ‚Ü¶ ?_) (Œª h2 ‚Ü¶ ?_)
  ¬∑ rw [sub_one_mul x, h0, zero_sub, neg_eq_zero] at h2
    rw [h2, mul_zero, hg.toShiftGood23.map_zero, mul_zero]
  rw [add_one_mul x, h0, zero_add] at h1
  rw [sub_one_mul, h0, zero_sub, hg.toShiftGood23.map_even] at h2
  have h3 : _ + _ = _ + _ := congrArg‚ÇÇ (¬∑ + ¬∑) h1 h2
  rw [‚Üê two_mul, ‚Üê add_mul, hg.Eq1, mul_add_one (Œ± := S), add_mul, self_eq_add_left,
    mul_assoc, mul_eq_zero, or_iff_right hg.Schar_ne_two] at h3
  rw [h0, hg.toShiftGood23.map_zero, h3]

/-- (2.3.8) -/
lemma Eq8 (x y) : g (x + y) + g (x - y) = 2 ‚Ä¢ (g x + g y) := by
  rw [two_nsmul, ‚Üê two_mul, ‚Üê sub_eq_zero, ‚Üê hg.Eq4, hg.Eq7, sub_self, mul_zero]


open Extra.SquareLike


theorem solution :
    ‚àÉ (R' : Type u) (_ : CommRing R') (œÜ : R ‚Üí+* R') (Œπ : SqSubring R' ‚Üí+* S),
      ‚àÄ x, g x = Œπ (RestrictedSq (œÜ x)) := by
  let hR := CommRing.mk hg.Rcomm
  refine ‚ü®R, hR, RingHom.id R, ?_‚ü©
  have hS (x y : S) (h : 2 ‚Ä¢ x = 2 ‚Ä¢ y) : x = y := by
    rwa [two_nsmul, ‚Üê two_mul, two_nsmul, ‚Üê two_mul, ‚Üê sub_eq_zero, ‚Üê mul_sub,
      mul_eq_zero, or_iff_right hg.Schar_ne_two, sub_eq_zero] at h
  let œÜ := BilinMap hS hg.Eq8
  let œÅ := œÜ 1
  ---- Collect basic properties of `œÜ`
  have h : ‚àÄ x, œÜ x x = 2 ‚Ä¢ g x := BilinMap_eq_two_nsmul _ _
  have h0 (x y) : œÜ x y = œÅ (x * y) :=
    hS _ _ <| by rw [two_nsmul_BilinMap_eq, two_nsmul_BilinMap_eq,
      ‚Üê hg.Eq2, add_comm, ‚Üê neg_sub (x * y), hg.toShiftGood23.map_even]
  ---- Construct `Œπ` as an additive hom
  let R‚ÇÇ := AddSubgroup.closure (Set.range Œª x : R ‚Ü¶ x ^ 2)
  obtain ‚ü®Œπ, h1‚ü© : ‚àÉ Œπ : SqSubring R ‚Üí+ S, ‚àÄ a : SqSubring R, œÅ a = 2 ‚Ä¢ Œπ a :=
    suffices ‚àÉ Œπ : SqSubring R ‚Üí S, ‚àÄ a : SqSubring R, œÅ a = 2 ‚Ä¢ Œπ a by
      rcases this with ‚ü®Œπ, h1‚ü©
      have h3 (x y) : Œπ (x + y) = Œπ x + Œπ y := hS _ _ <| by
        rw [‚Üê h1, Subring.coe_add, œÅ.map_add, h1, h1, nsmul_add]
      exact ‚ü®AddMonoidHom.mk' Œπ h3, h1‚ü©
    suffices ‚àÄ r ‚àà R‚ÇÇ, ‚àÉ s, œÅ r = 2 ‚Ä¢ s
      from Classical.axiomOfChoice Œª (a : SqSubring R) ‚Ü¶ this a.1 a.2
    Œª r ‚Ü¶ AddSubgroup.closure_induction
      (Œª y ‚ü®x, h3‚ü© ‚Ü¶ ‚ü®g x, by rw [‚Üê h, h0 x, ‚Üê sq, ‚Üê h3]‚ü©)
      ‚ü®0, by rw [œÅ.map_zero, nsmul_zero]‚ü©
      (Œª x y _ _ ‚ü®s, hs‚ü© ‚ü®t, ht‚ü© ‚Ü¶ ‚ü®s + t, by rw [œÅ.map_add, hs, ht, nsmul_add]‚ü©)
      (Œª x _ ‚ü®s, hs‚ü© ‚Ü¶ ‚ü®-s, by rw [œÅ.map_neg, hs, nsmul_eq_mul, ‚Üê mul_neg, nsmul_eq_mul]‚ü©)
  ---- Reduce to multiplicativity of `Œπ`, then prove it
  suffices ‚àÄ x y, Œπ (x * y) = Œπ x * Œπ y by
    have h2 : Œπ 1 = 1 := hS _ _ <| by
      rw [‚Üê h1, Subring.coe_one, h, hg.toShiftGood23.map_one]
    refine ‚ü®‚ü®‚ü®‚ü®Œπ, h2‚ü©, this‚ü©, Œπ.map_zero, Œπ.map_add‚ü©, Œª x ‚Ü¶ hS _ _ ?_‚ü©
    change 2 ‚Ä¢ g x = 2 ‚Ä¢ Œπ (RestrictedSq x)
    rw [‚Üê h, ‚Üê h1, RestrictedSq_coe, sq, h0]
  ---- Prove that `Œπ` is multiplicative
  have X (x y : S) : (2 ‚Ä¢ x) * (2 ‚Ä¢ y) = 2 ‚Ä¢ 2 ‚Ä¢ (x * y) := by
    rw [two_nsmul, two_nsmul, add_mul, mul_add, ‚Üê two_nsmul, ‚Üê two_nsmul]
  suffices ‚àÄ a b, a ‚àà R‚ÇÇ ‚Üí b ‚àà R‚ÇÇ ‚Üí 2 ‚Ä¢ œÅ (a * b) = œÅ a * œÅ b
    from Œª x y ‚Ü¶ hS _ _ <| hS _ _ <| by
      rw [‚Üê h1, Subring.coe_mul, this _ _ x.2 y.2, h1, h1, X]
  replace h (x) : œÅ (x ^ 2) = 2 ‚Ä¢ g x := by rw [‚Üê h, sq, ‚Üê h0]
  intro a b; refine AddSubgroup.closure_induction‚ÇÇ ?_ ?_ ?_ ?_ ?_ ?_ ?_
  ¬∑ rintro _ _ ‚ü®c, rfl‚ü© ‚ü®d, rfl‚ü©; rw [‚Üê mul_pow, h, h, h, X, hg.Eq7]
  ¬∑ rintro x -; rw [zero_mul, œÅ.map_zero, zero_mul, nsmul_zero]
  ¬∑ rintro x -; rw [mul_zero, œÅ.map_zero, mul_zero, nsmul_zero]
  ¬∑ rintro x‚ÇÅ x‚ÇÇ y - - - hx‚ÇÅ hx‚ÇÇ
    rw [add_mul, œÅ.map_add, nsmul_add, hx‚ÇÅ, hx‚ÇÇ, œÅ.map_add, add_mul]
  ¬∑ rintro x y‚ÇÅ y‚ÇÇ - - - hy‚ÇÅ hy‚ÇÇ
    rw [mul_add, œÅ.map_add, nsmul_add, hy‚ÇÅ, hy‚ÇÇ, œÅ.map_add, mul_add]
  ¬∑ rintro x y - - h2; rw [neg_mul, œÅ.map_neg, œÅ.map_neg, neg_mul, ‚Üê h2]
    simp only [nsmul_eq_mul, Nat.cast_ofNat, mul_neg]
  ¬∑ rintro x y - - h2; rw [mul_neg, œÅ.map_neg, œÅ.map_neg, mul_neg, ‚Üê h2]
    simp only [nsmul_eq_mul, Nat.cast_ofNat, mul_neg]

end

end RShiftGood23





/-! ### Summary -/

section

variable {R : Type u} [Ring R] [Ring S] [NoZeroDivisors S] {f : R ‚Üí S}

theorem RGoodSubcase23.solution (hf : RGoodCase2 f) (h : f 2 = 3) :
    ‚àÉ (R' : Type u) (_ : CommRing R') (œÜ : R ‚Üí+* R') (Œπ : SqSubring R' ‚Üí+* S),
      ‚àÄ x, f x = Œπ (RestrictedSq (œÜ x) - 1) := by
  rcases (RShiftGood23.shift_mk_iff.mpr ‚ü®hf, h‚ü©).solution with ‚ü®R', _, œÜ, Œπ, h0‚ü©
  refine ‚ü®R', _, œÜ, Œπ, Œª x ‚Ü¶ ?_‚ü©
  rw [Œπ.map_sub, ‚Üê h0, Œπ.map_one]
  exact (add_sub_cancel_right _ _).symm

theorem RGoodCase2.solution (hf : RGoodCase2 f) :
    (‚àÉ (R' : Type u) (_ : CommRing R') (œÜ : R ‚Üí+* R') (Œπ : SqSubring R' ‚Üí+* S),
      ‚àÄ x, f x = Œπ (RestrictedSq (œÜ x) - 1)) ‚à®
    (‚àÉ œÜ : R ‚Üí+* ‚Ñ§‚ÇÑ, ‚àÄ x, f x = ‚Ñ§‚ÇÑMap (œÜ x)) ‚à®
    (‚àÉ œÜ : R ‚Üí+* ùîΩ‚ÇÉ, ‚àÄ x, f x = ùîΩ‚ÇÉMap2 (œÜ x)) :=
  (em (f 2 = 3)).imp (RGoodSubcase23.solution hf) Œª h ‚Ü¶
    hf.toGoodCase2.map_two_cases.imp (RGoodSubcase21.solution hf) Œª h0 ‚Ü¶
      have h0 := h0.resolve_right h
      RGoodSubcase22.solution hf h0 (h0.symm.trans_ne h).symm

/-- Refer to Case 3 if `f(2) = -1` -/
theorem CharTwo'_of_map_two (hf : ReducedGood f)
    (h : f (-1) = 0) (h0 : f 2 = -1) : (2 : R) = 0 := by
  refine hf.period_imp_zero Œª x ‚Ü¶ ?_
  rcases CommSubring.oneVarCommLiftDomain_exists hf.toNontrivialGood x with
    ‚ü®R', R'comm, œÜ, -, ‚ü®x, rfl‚ü©, S', S'comm, S'nzd, œÅ, hœÅ, f', h1, hf'‚ü©
  rw [‚Üê map_ofNat œÜ 2, ‚Üê œÜ.map_add, h1, h1, CommCase.two_periodic_of_map_two hf']
  ¬∑ refine map_even_of_map_one hf'.is_good (hœÅ ?_)
    rw [‚Üê h1, œÜ.map_neg, œÜ.map_one, h, œÅ.map_zero]
  ¬∑ apply hœÅ; rw [‚Üê h1, map_ofNat, h0, œÅ.map_neg, œÅ.map_one]

theorem solution (hf : ReducedGood f) (h : f (-1) = 0) (h0 : f 2 ‚â† -1) :
    (‚àÉ (R' : Type u) (_ : CommRing R') (œÜ : R ‚Üí+* R') (Œπ : SqSubring R' ‚Üí+* S),
      ‚àÄ x, f x = Œπ (RestrictedSq (œÜ x) - 1)) ‚à®
    (‚àÉ œÜ : R ‚Üí+* ‚Ñ§‚ÇÑ, ‚àÄ x, f x = ‚Ñ§‚ÇÑMap (œÜ x)) ‚à®
    (‚àÉ œÜ : R ‚Üí+* ùîΩ‚ÇÉ, ‚àÄ x, f x = ùîΩ‚ÇÉMap2 (œÜ x)) :=
  RGoodCase2.solution ‚ü®hf, map_even_of_map_one hf.is_good h, h0‚ü©
