/-
Copyright (c) 2025 Gian Cordana Sanjaya. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Gian Cordana Sanjaya
-/

import Mathlib.Analysis.Convex.SpecificFunctions.Pow
import Mathlib.Analysis.Convex.Jensen

/-!
# IMO 2021 A4 (P2)

Prove that for any real numbers $x_1, x_2, â€¦, x_n$,
$$ \sum_{i = 1}^n \sum_{j = 1}^n \sqrt{|x_i - x_j|}
  â‰¤ \sum_{i = 1}^n \sum_{j = 1}^n \sqrt{|x_i + x_j|}. $$

### Solution

We follow Solution 1 of the
  [official solution](https://www.imo-official.org/problems/IMO2021SL.pdf).
More generally, we prove that for any increasing and concave function $g : â„_{â‰¥ 0} â†’ â„$,
$$ \sum_{i = 1}^n \sum_{j = 1}^n g(|x_i - x_j|)
  â‰¤ \sum_{i = 1}^n \sum_{j = 1}^n g(|x_i + x_j|). $$
See `IMOSL.IMO2021A4.targetSum_nonneg_of_monotone_concave'`.

Instead of taking $T$ large enough, we split into three cases:
1. $x_i â‰¥ 0$ for all $i$;
2. $x_i â‰¤ 0$ for all $i$;
3. some of the $x_i$'s are positive while some others are negative.
-/

namespace IMOSL
namespace IMO2021A4

/-! ### Subset relations of intervals -/

/-- If `c â‰¤ a`, then `[a, b] âŠ† [c, âˆ)`. -/
theorem Icc_subset_Ici [Preorder Î±] {a b c : Î±} (h : c â‰¤ a) :
    Set.Icc a b âŠ† Set.Ici c :=
  Î» _ hd â†¦ Set.mem_Ici.mpr (h.trans (Set.mem_Icc.mp hd).1)

/-- If `b â‰¤ c`, then `[a, b] âŠ† (-âˆ, c]`. -/
theorem Icc_subset_Iic [Preorder Î±] {a b c : Î±} (h : b â‰¤ c) :
    Set.Icc a b âŠ† Set.Iic c :=
  Î» _ hd â†¦ Set.mem_Iic.mpr ((Set.mem_Icc.mp hd).2.trans h)





/-! ### Some concavity properties -/

section

open Set

variable [Semiring ğ•œ] [PartialOrder ğ•œ]
  [AddCommGroup E] [LinearOrder E] [IsOrderedAddMonoid E] [Module ğ•œ E]
  [AddCommMonoid Î²] [PartialOrder Î²] [SMul ğ•œ Î²] (g : E â†’ Î²) (hg : ConcaveOn ğ•œ (Ici 0) g)
include g hg

/-- If `g` is concave on non-negative inputs, then `t â†¦ g(|t|)` is concave on `[0, âˆ)`. -/
theorem ConcaveOn_abs_zero_Ici : ConcaveOn ğ•œ (Ici 0) (Î» t â†¦ g |t|) :=
  hg.congr Î» _ hx â†¦ congrArg g (abs_of_nonneg (mem_Ici.mp hx)).symm

/-- If `g` is concave on non-negative inputs, then `t â†¦ g(|t|)` is concave on `(-âˆ, 0]`. -/
theorem ConcaveOn_abs_zero_Iic : ConcaveOn ğ•œ (Iic 0) (Î» t â†¦ g |t|) := by
  have h : (-LinearMap.id (R := ğ•œ) (M := E)) â»Â¹' Ici 0 = Iic 0 := by
    ext x; change -x âˆˆ Ici 0 â†” x âˆˆ Iic 0
    rw [mem_Ici, mem_Iic, neg_nonneg]
  replace h : ConcaveOn ğ•œ (Iic 0) (Î» t â†¦ g |-t|) :=
    h â–¸ (ConcaveOn_abs_zero_Ici g hg).comp_linearMap (-LinearMap.id)
  simpa only [abs_neg] using h

/-- If `g` is concave on non-negative inputs, then for any `x âˆˆ E`,
  the function `t â†¦ g(|x - t|)` is concave on `(-âˆ, x]`. -/
theorem ConcaveOn_abs_sub_Ici (x) : ConcaveOn ğ•œ (Iic x) (Î» t â†¦ g |x - t|) := by
  have h : (-x + Â·) â»Â¹' Iic 0 = Iic x :=
    ext Î» x â†¦ by rw [preimage_const_add_Iic, sub_neg_eq_add, zero_add, mem_Iic]
  conv => right; ext t; rw [abs_sub_comm, sub_eq_add_neg]
  exact h â–¸ (ConcaveOn_abs_zero_Iic g hg).translate_left (-x)

/-- If `g` is concave on non-negative inputs, then for any `x âˆˆ E`,
  the function `t â†¦ g(|x - t|)` is concave on `[x, âˆ)`. -/
theorem ConcaveOn_abs_sub_Iic (x) : ConcaveOn ğ•œ (Ici x) (Î» t â†¦ g |x - t|) := by
  have h : (-x + Â·) â»Â¹' Ici 0 = Ici x :=
    ext Î» x â†¦ by rw [preimage_const_add_Ici, sub_neg_eq_add, zero_add, mem_Ici]
  conv => right; ext t; rw [abs_sub_comm, sub_eq_add_neg]
  exact h â–¸ (ConcaveOn_abs_zero_Ici g hg).translate_left (-x)

end


section

variable [Semiring ğ•œ] [PartialOrder ğ•œ] [AddCommMonoid E]
  [AddCommMonoid Î²] [PartialOrder Î²] [IsOrderedAddMonoid Î²] [SMul ğ•œ E] [Module ğ•œ Î²]
  [DecidableEq Î¹] (S : Finset Î¹) {s : Set E} (hs : Convex ğ•œ s)
  (g : Î¹ â†’ E â†’ Î²) (hg : âˆ€ i âˆˆ S, ConcaveOn ğ•œ s (g i))
include hs hg

/-- The sum of concave functions on a subset is concave on the same subset.
  This should eventually get into `mathlib`. -/
theorem ConcaveOn_sum : ConcaveOn ğ•œ s (âˆ‘ i âˆˆ S, g i) := by
  induction S using Finset.induction_on with
  | empty => exact concaveOn_const 0 hs
  | insert i S hiS h =>
      rw [Finset.forall_mem_insert] at hg
      rw [Finset.sum_insert hiS]
      exact hg.1.add (h hg.2)

/-- The sum of concave functions on a subset is concave on the same subset.
  This should eventually get into `mathlib`. -/
theorem ConcaveOn_of_sum_eq {f : E â†’ Î²} (hf : âˆ€ x, f x = âˆ‘ i âˆˆ S, g i x) :
    ConcaveOn ğ•œ s f := by
  obtain rfl : f = âˆ‘ i âˆˆ S, g i := funext Î» x â†¦ (hf x).trans (Finset.sum_apply _ _ _).symm
  exact ConcaveOn_sum S hs g hg

end


/-- If a function is concave on a closed interval, then the function attains its minimum
  on the interval at one of the endpoints. This should eventually get into `mathlib`. -/
theorem ConcaveOn_min_endpoints_le [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
    [AddCommGroup Î²] [LinearOrder Î²] [IsOrderedAddMonoid Î²] [Module ğ•œ Î²]
    [IsStrictOrderedModule ğ•œ Î²] {f : ğ•œ â†’ Î²} (hf : ConcaveOn ğ•œ (Set.Icc x y) f)
    (hxz : x â‰¤ z) (hzy : z â‰¤ y) : min (f x) (f y) â‰¤ f z := by
  have h : x â‰¤ y := hxz.trans hzy
  exact hf.min_le_of_mem_Icc (Set.left_mem_Icc.mpr h)
    (Set.right_mem_Icc.mpr h) (Set.mem_Icc.mpr âŸ¨hxz, hzyâŸ©)





/-! ### Start of the problem -/

open Finset

section

variable [AddCommGroup ğ•œ] [LinearOrder ğ•œ] [AddCommGroup Î²] (g : ğ•œ â†’ Î²)

/-- The "target sum": `âˆ‘_{i, j âˆˆ S} (g(|x_i + x_j|) - g(|x_i - x_j|))`. -/
def targetSum (S : Finset Î¹) (x : Î¹ â†’ ğ•œ) : Î² :=
  âˆ‘ p âˆˆ S Ã—Ë¢ S, (g |x p.1 + x p.2| - g |x p.1 - x p.2|)

/-- If `x_i = 0`, then the value of the target sum with
  index set `S âˆª {i}` and index set `S` are equal. -/
theorem targetSum_insert_of_eq_zero
    [DecidableEq Î¹] (S : Finset Î¹) (x : Î¹ â†’ ğ•œ) (hi : x i = 0) :
    targetSum g (insert i S) x = targetSum g S x := by
  obtain h | h : i âˆˆ S âˆ¨ Â¬i âˆˆ S := dec_em _
  ---- Case 1: `i âˆˆ S`.
  Â· rw [insert_eq_of_mem h]
  ---- Case 2: `i âˆ‰ S`.
  Â· calc âˆ‘ p âˆˆ insert i S Ã—Ë¢ insert i S, (g |x p.1 + x p.2| - g |x p.1 - x p.2|)
      _ = âˆ‘ j âˆˆ insert i S, (g |x i + x j| - g |x i - x j|)
          + âˆ‘ jâ‚ âˆˆ S, âˆ‘ jâ‚‚ âˆˆ insert i S, (g |x jâ‚ + x jâ‚‚| - g |x jâ‚ - x jâ‚‚|) := by
        rw [sum_product, sum_insert h]
      _ = âˆ‘ jâ‚ âˆˆ S, âˆ‘ jâ‚‚ âˆˆ insert i S, (g |x jâ‚ + x jâ‚‚| - g |x jâ‚ - x jâ‚‚|) := by
        refine add_eq_right.mpr (sum_eq_zero Î» j _ â†¦ ?_)
        rw [hi, zero_add, zero_sub, abs_neg, sub_self]
      _ = âˆ‘ jâ‚ âˆˆ S, âˆ‘ jâ‚‚ âˆˆ S, (g |x jâ‚ + x jâ‚‚| - g |x jâ‚ - x jâ‚‚|) := by
        refine sum_congr rfl Î» jâ‚ _ â†¦ ?_
        rw [sum_insert h, add_eq_right, hi, add_zero, sub_zero, sub_self]
      _ = âˆ‘ p âˆˆ S Ã—Ë¢ S, (g |x p.1 + x p.2| - g |x p.1 - x p.2|) := by
        rw [sum_product]

/-- If `iâ‚ â‰  iâ‚‚ âˆ‰ S` and `x_{iâ‚} + x_{iâ‚‚} = 0`, then the value of the target sum with
  index set `S âˆª {i, j}` and index set `S` are equal. -/
theorem targetSum_insert_of_add_eq_zero [DecidableEq Î¹] (S : Finset Î¹)
    (hi : Disjoint {iâ‚, iâ‚‚} S) (hi0 : iâ‚ â‰  iâ‚‚) (x : Î¹ â†’ ğ•œ) (hi1 : x iâ‚ + x iâ‚‚ = 0) :
    targetSum g ({iâ‚, iâ‚‚} âˆª S) x = targetSum g S x :=
  have hi1 : x iâ‚‚ = -x iâ‚ := eq_neg_of_add_eq_zero_right hi1
  calc âˆ‘ p âˆˆ ({iâ‚, iâ‚‚} âˆª S) Ã—Ë¢ ({iâ‚, iâ‚‚} âˆª S), (g |x p.1 + x p.2| - g |x p.1 - x p.2|)
  _ = âˆ‘ jâ‚ âˆˆ {iâ‚, iâ‚‚}, âˆ‘ jâ‚‚ âˆˆ {iâ‚, iâ‚‚} âˆª S, (g |x jâ‚ + x jâ‚‚| - g |x jâ‚ - x jâ‚‚|)
      + âˆ‘ jâ‚ âˆˆ S, âˆ‘ jâ‚‚ âˆˆ {iâ‚, iâ‚‚} âˆª S, (g |x jâ‚ + x jâ‚‚| - g |x jâ‚ - x jâ‚‚|) := by
    rw [sum_product, sum_union hi]
  _ = âˆ‘ jâ‚ âˆˆ S, âˆ‘ jâ‚‚ âˆˆ {iâ‚, iâ‚‚} âˆª S, (g |x jâ‚ + x jâ‚‚| - g |x jâ‚ - x jâ‚‚|) := by
    rw [add_eq_right, sum_pair hi0, â† sum_add_distrib]
    refine sum_eq_zero Î» j _ â†¦ ?_
    rw [hi1, â† neg_add', abs_neg, sub_add_sub_cancel',
      neg_add_eq_sub, abs_sub_comm, sub_self]
  _ = âˆ‘ jâ‚ âˆˆ S, âˆ‘ jâ‚‚ âˆˆ S, (g |x jâ‚ + x jâ‚‚| - g |x jâ‚ - x jâ‚‚|) := by
    refine sum_congr rfl Î» jâ‚ _ â†¦ ?_
    rw [sum_union hi, add_eq_right, sum_pair hi0, hi1, sub_neg_eq_add,
      sub_add_sub_cancel', â† sub_eq_add_neg, abs_sub_comm, sub_self]
  _ = âˆ‘ p âˆˆ S Ã—Ë¢ S, (g |x p.1 + x p.2| - g |x p.1 - x p.2|) := by
    rw [sum_product]

end


section

variable [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [AddCommGroup Î²]
  [LinearOrder Î²] [IsOrderedAddMonoid Î²] [Module ğ•œ Î²] [IsStrictOrderedModule ğ•œ Î²]
  (g : ğ•œ â†’ Î²) (hg : âˆ€ y â‰¥ 0, âˆ€ x â‰¥ y, g x â‰¥ g y) (hg0 : ConcaveOn ğ•œ (Set.Ici 0) g)
include hg hg0

/-- If `S` is non-empty, then there exists a pair `(iâ‚, iâ‚‚) âˆˆ S^2` such that the target sum
  with `x_i - (x_{i_1} + x_{i_2})/2` is less than or equal to the target sum with `x_i`. -/
theorem exists_targetSum_shift_le_targetSum
    [DecidableEq Î¹] {S : Finset Î¹} (hS : S.Nonempty) (x : Î¹ â†’ ğ•œ) :
    âˆƒ p âˆˆ S Ã—Ë¢ S, targetSum g S (Î» i â†¦ x i - (x p.1 + x p.2) / 2) â‰¤ targetSum g S x := by
  ---- If `x_i â‰¥ 0` for all `i âˆˆ S`, then take `p = (i, i)` where `x_i` is minimal.
  by_cases hS_pos : âˆ€ i âˆˆ S, x i â‰¥ 0
  Â· obtain âŸ¨i, hiS, hiâŸ© : âˆƒ i âˆˆ S, âˆ€ j âˆˆ S, x i â‰¤ x j := S.exists_min_image _ hS
    refine âŸ¨(i, i), mk_mem_product hiS hiS, sum_le_sum Î» p hp â†¦ ?_âŸ©
    replace hi (j) (hj : j âˆˆ S) : 0 â‰¤ x j - x i := sub_nonneg.mpr (hi j hj)
    replace hp : p.1 âˆˆ S âˆ§ p.2 âˆˆ S := mem_product.mp hp
    rw [add_self_div_two, sub_sub_sub_cancel_right, sub_le_sub_iff_right]
    refine hg _ (abs_nonneg _) _ ?_
    calc |x p.1 + x p.2|
      _ = x p.1 + x p.2 := abs_of_nonneg (add_nonneg (hS_pos _ hp.1) (hS_pos _ hp.2))
      _ â‰¥ (x p.1 - x i) + (x p.2 - x i) := by
        have hi0 : 0 â‰¤ x i := hS_pos i hiS
        exact add_le_add (sub_le_self _ hi0) (sub_le_self _ hi0)
      _ = |(x p.1 - x i) + (x p.2 - x i)| :=
        (abs_of_nonneg (add_nonneg (hi _ hp.1) (hi _ hp.2))).symm
  ---- If `x_i â‰¤ 0` for all `i âˆˆ S`, then take `p = (i, i)` where `x_i` is maximal.
  by_cases hS_neg : âˆ€ i âˆˆ S, x i â‰¤ 0
  Â· obtain âŸ¨i, hiS, hiâŸ© : âˆƒ i âˆˆ S, âˆ€ j âˆˆ S, x j â‰¤ x i := S.exists_max_image _ hS
    refine âŸ¨(i, i), mk_mem_product hiS hiS, sum_le_sum Î» p hp â†¦ ?_âŸ©
    replace hi (j) (hj : j âˆˆ S) : x j - x i â‰¤ 0 := sub_nonpos.mpr (hi j hj)
    replace hp : p.1 âˆˆ S âˆ§ p.2 âˆˆ S := mem_product.mp hp
    rw [add_self_div_two, sub_sub_sub_cancel_right, sub_le_sub_iff_right]
    refine hg _ (abs_nonneg _) _ ?_
    calc |x p.1 + x p.2|
      _ = -(x p.1 + x p.2) := abs_of_nonpos (add_nonpos (hS_neg _ hp.1) (hS_neg _ hp.2))
      _ â‰¥ -((x p.1 - x i) + (x p.2 - x i)) := by
        have hi0 : x i â‰¤ 0 := hS_neg i hiS
        refine neg_le_neg (add_le_add ?_ ?_)
        all_goals exact (le_sub_self_iff _).mpr hi0
      _ = |(x p.1 - x i) + (x p.2 - x i)| :=
        (abs_of_nonpos (add_nonpos (hi _ hp.1) (hi _ hp.2))).symm
  ---- In the remaining case, we know that `x_i + x_j` takes both signs over `i, j âˆˆ S`.
  simp_rw [not_forall, not_le] at hS_pos hS_neg
  replace hS_pos : {p âˆˆ S Ã—Ë¢ S | x p.1 + x p.2 â‰¤ 0}.Nonempty := by
    rcases hS_pos with âŸ¨i, hi, hi0âŸ©
    exact âŸ¨(i, i), mem_filter.mpr âŸ¨mk_mem_product hi hi, (add_neg hi0 hi0).leâŸ©âŸ©
  replace hS_neg : {p âˆˆ S Ã—Ë¢ S | x p.1 + x p.2 â‰¥ 0}.Nonempty := by
    rcases hS_neg with âŸ¨i, hi, hi0âŸ©
    exact âŸ¨(i, i), mem_filter.mpr âŸ¨mk_mem_product hi hi, (add_pos hi0 hi0).leâŸ©âŸ©
  ---- First pick `pâ‚š = (iâ‚š, jâ‚š)` with `x_{iâ‚š + x_{jâ‚š}` minimally non-negative.
  obtain âŸ¨pâ‚š, hpâ‚š, hpâ‚š0, hpâ‚š1âŸ© :
      âˆƒ p âˆˆ S Ã—Ë¢ S, x p.1 + x p.2 â‰¥ 0 âˆ§
        âˆ€ q âˆˆ S Ã—Ë¢ S, x q.1 + x q.2 â‰¥ 0 â†’ x p.1 + x p.2 â‰¤ x q.1 + x q.2 := by
    obtain âŸ¨p, hp, hp0âŸ© := exists_min_image _ (Î» p : Î¹ Ã— Î¹ â†¦ x p.1 + x p.2) hS_neg
    simp_rw [mem_filter, and_imp] at hp hp0
    exact âŸ¨p, hp.1, hp.2, hp0âŸ©
  clear hS_neg
  ---- Now pick `pâ‚™ = (iâ‚™, jâ‚™)` with `x_{iâ‚™} + x_{jâ‚™}` maximally non-positive.
  obtain âŸ¨pâ‚™, hpâ‚™, hpâ‚™0, hpâ‚™1âŸ© :
      âˆƒ p âˆˆ S Ã—Ë¢ S, x p.1 + x p.2 â‰¤ 0 âˆ§
        âˆ€ q âˆˆ S Ã—Ë¢ S, x q.1 + x q.2 â‰¤ 0 â†’ x q.1 + x q.2 â‰¤ x p.1 + x p.2 := by
    obtain âŸ¨p, hp, hp0âŸ© := exists_max_image _ (Î» p : Î¹ Ã— Î¹ â†¦ x p.1 + x p.2) hS_pos
    simp_rw [mem_filter, and_imp] at hp hp0
    exact âŸ¨p, hp.1, hp.2, hp0âŸ©
  clear hS_pos
  /- Claim: the target sum with respect to `t â†¦ x_i - t/2` is concave on
    the interval `I = [x_{iâ‚™} + x_{jâ‚™}, x_{iâ‚š} + x_{jâ‚š}]`. -/
  let I : Set ğ•œ := Set.Icc (x pâ‚™.1 + x pâ‚™.2) (x pâ‚š.1 + x pâ‚š.2)
  have hI : Convex ğ•œ I := convex_Icc _ _
  have h : ConcaveOn ğ•œ I (Î» t â†¦ targetSum g S (Î» i â†¦ x i - t / 2)) := by
    refine ConcaveOn_of_sum_eq (S Ã—Ë¢ S) hI
      (Î» p t â†¦ g |x p.1 + x p.2 - t| + -g |x p.1 - x p.2|) ?_ ?_
    -- Check that each term `g(|x_i + x_j - t|)` is concave on `I`.
    Â· intro p hp; apply ConcaveOn.add_const
      obtain h | h : x p.1 + x p.2 â‰¤ 0 âˆ¨ x p.1 + x p.2 â‰¥ 0 := le_total _ _
      exacts [(ConcaveOn_abs_sub_Iic g hg0 _).subset (Icc_subset_Ici (hpâ‚™1 p hp h)) hI,
        (ConcaveOn_abs_sub_Ici g hg0 _).subset (Icc_subset_Iic (hpâ‚š1 p hp h)) hI]
    -- Check that the sum of the functions match.
    Â· intro t; refine sum_congr rfl Î» p _ â†¦ ?_
      rw [sub_sub_sub_cancel_right, sub_add_sub_comm, add_halves, sub_eq_add_neg]
  ---- Now `0` belongs to that interval, so we can apply minimality on the endpoint.
  replace h :
      min (targetSum g S (Î» i â†¦ x i - (x pâ‚™.1 + x pâ‚™.2) / 2))
        (targetSum g S (Î» i â†¦ x i - (x pâ‚š.1 + x pâ‚š.2) / 2))
        â‰¤ targetSum g S (Î» i â†¦ x i - 0 / 2) :=
    ConcaveOn_min_endpoints_le (ğ•œ := ğ•œ) (Î² := Î²) h hpâ‚™0 hpâ‚š0
  ---- Picking one of the endpoints, we are done.
  simp_rw [zero_div, sub_zero, inf_le_iff] at h
  rcases h with h | h
  exacts [âŸ¨pâ‚™, hpâ‚™, hâŸ©, âŸ¨pâ‚š, hpâ‚š, hâŸ©]

/-- If `g` is monotone and concave on `[0, âˆ)`, then the target sum is nonnegative. -/
theorem targetSum_nonneg_of_monotone_concave [DecidableEq Î¹] (S : Finset Î¹) (x : Î¹ â†’ ğ•œ) :
    targetSum g S x â‰¥ 0 := by
  ---- Proceed by strong induction on `S`.
  induction S using Finset.strongInduction generalizing x with | H S S_ih => ?_
  ---- If `S = âˆ…`, we are done.
  obtain rfl | hS : S = âˆ… âˆ¨ S.Nonempty := S.eq_empty_or_nonempty
  Â· exact le_refl 0
  /- If `S â‰  âˆ…`, by `exists_targetSum_shift_le_targetSum`, we can find `(iâ‚, iâ‚‚) âˆˆ S^2`
    such that the target sum with respect to `x_i - (x_{i_1} + x_{i_2})/2` is less than
    those with respect to `x_i`. -/
  obtain âŸ¨âŸ¨iâ‚, iâ‚‚âŸ©, hi, hâŸ© :
      âˆƒ p âˆˆ S Ã—Ë¢ S, targetSum g S (Î» i â†¦ x i - (x p.1 + x p.2) / 2) â‰¤ targetSum g S x :=
    exists_targetSum_shift_le_targetSum _ hg hg0 hS _
  replace hi : iâ‚ âˆˆ S âˆ§ iâ‚‚ âˆˆ S := mem_product.mp hi
  replace hi : {iâ‚, iâ‚‚} âŠ† S := by
    intro j hj
    rw [mem_insert, mem_singleton] at hj
    rcases hj with rfl | rfl
    exacts [hi.1, hi.2]
  ---- Now prove the inequality by applying induction hypothesis on `S \ {iâ‚, iâ‚‚}`.
  calc 0
    _ â‰¤ targetSum g (S \ {iâ‚, iâ‚‚}) (x Â· - (x iâ‚ + x iâ‚‚) / 2) :=
      S_ih _ (sdiff_ssubset hi âŸ¨iâ‚, mem_insert_self _ _âŸ©) _
    _ = targetSum g ({iâ‚, iâ‚‚} âˆª (S \ {iâ‚, iâ‚‚})) (x Â· - (x iâ‚ + x iâ‚‚) / 2) := by
      obtain rfl | h1 : iâ‚ = iâ‚‚ âˆ¨ iâ‚ â‰  iâ‚‚ := dec_em _
      -- If `iâ‚ = iâ‚‚`, use `targetSum_insert_of_eq_zero`.
      Â· rw [pair_eq_singleton]
        refine (targetSum_insert_of_eq_zero _ _ _ ?_).symm
        rw [add_self_div_two, sub_self]
      -- If `iâ‚ â‰  iâ‚‚`, use `targetSum_insert_of_add_eq_zero`.
      Â· refine (targetSum_insert_of_add_eq_zero _ _ disjoint_sdiff h1 _ ?_).symm
        rw [sub_add_sub_comm, add_halves, sub_self]
    _ = targetSum g S (x Â· - (x iâ‚ + x iâ‚‚) / 2) := by
      rw [union_sdiff_self_eq_union, union_eq_right.mpr hi]
    _ â‰¤ targetSum g S x := h

/-- If `g` is monotone and concave on `[0, âˆ)`,
  then `âˆ‘_{i, j âˆˆ S} g(|x_i - x_j|) â‰¤ âˆ‘_{i, j âˆˆ S} g(|x_i + x_j|)`. -/
theorem targetSum_nonneg_of_monotone_concave' [DecidableEq Î¹] (S : Finset Î¹) (x : Î¹ â†’ ğ•œ) :
    âˆ‘ p âˆˆ S Ã—Ë¢ S, g |x p.1 - x p.2| â‰¤ âˆ‘ p âˆˆ S Ã—Ë¢ S, g |x p.1 + x p.2| := by
  rw [â† sub_nonneg, â† sum_sub_distrib]
  exact targetSum_nonneg_of_monotone_concave g hg hg0 S x

end


/-- Final solution -/
theorem final_solution [DecidableEq Î¹] (S : Finset Î¹) (x : Î¹ â†’ â„) :
    âˆ‘ p âˆˆ S Ã—Ë¢ S, âˆš|x p.1 - x p.2| â‰¤ âˆ‘ p âˆˆ S Ã—Ë¢ S, âˆš|x p.1 + x p.2| :=
  targetSum_nonneg_of_monotone_concave' _ (Î» _ _ _ â†¦ Real.sqrt_le_sqrt)
    Real.strictConcaveOn_sqrt.concaveOn S x
