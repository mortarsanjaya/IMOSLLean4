/-
Copyright (c) 2025 Gian Cordana Sanjaya. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Gian Cordana Sanjaya
-/

import Mathlib.FieldTheory.Finite.Basic

/-!
# IMO 2024 N6

Determine all positive integers $n$ such that for any polynomial $P ‚àà ‚Ñ§[X]$,
  there exists a polynomial $Q ‚àà ‚Ñ§[X]$ of degree $2$ such that for any integer $k$,
$$ n ‚à§ Q(k)(P(k) + Q(k)). $$

### Answer

$n > 2$.

### Solution

We follow and generalize Solution 1 of the
  [official solution](https://www.imo-official.org/problems/IMO2024SL.pdf).
Given a commutative ring $R$, we say that a function $f : R ‚Üí R$ is *good* if
  there exists $a, b, c ‚àà R$ such that for any $r ‚àà R$,
$$ (ar^2 + br + c)(f(r) - (ar^2 + br + c)) ‚â† 0. $$

The original problem is equivalent to finding all values of $n > 0$
  for which every polynomial over $‚Ñ§/n‚Ñ§$ is good.
The official solution implies that this holds for $n > 2$, and "more generally",
  every function over $ùîΩ‚Çö$ is good if $p$ is an odd prime.
(This isn't really more general, as every function from $ùîΩ‚Çö$ to itself is a polynomial.)
We show even more:
* every function from $‚Ñ§/4‚Ñ§$ to itself is good (see below);
* every function from a finite field of characteristic $‚â† 2$ to itself is good
  (follows from the official solution).

### The case $n = 4$

Consider an arbitrary function $f : ‚Ñ§/4‚Ñ§ ‚Üí ‚Ñ§/4‚Ñ§$.
We show that there exists $a, c ‚àà ‚Ñ§/4‚Ñ§$ such that $Q(r)(f(r) - Q(r)) ‚â† 0$
  for all $r ‚àà ‚Ñ§/4‚Ñ§$, where $Q(r) = a(1 - r^2) + cr^2$.
If $\{f(0), f(2)\} ‚â† \{1, 3\}$, pick any $a \in \{1, 3\} \setminus \{f(0), f(2)\}$.
If $\{f(0), f(2)\} = \{1, 3\}$, pick $a = 2$.
We do similar procedure to pick $c$ but with $\{f(1), f(3)\}$ replacing $\{f(0), f(2)\}$.
-/

namespace IMOSL
namespace IMO2024N6

open Polynomial

/-! ### Definitions and general properties -/

/-- A function `f : R ‚Üí R` is called `good` if there exists `a, b, c ‚àà R`
  such that `(ar^2 + br + c)(f(r) - (ar^2 + br + c)) ‚â† 0` for any `r ‚àà R`. -/
def good [Ring R] (f : R ‚Üí R) :=
  ‚àÉ a b c : R, ‚àÄ r : R, (a * r ^ 2 + b * r + c) * (f r - (a * r ^ 2 + b * r + c)) ‚â† 0

/-- A surjective ring homomorphism pulls good polynomials back to good polynomials. -/
theorem good_of_good_polynomial_map [Ring R] [Ring S]
    (œÜ : R ‚Üí+* S) (hœÜ : (‚áëœÜ).Surjective) (P : R[X]) (hP : good (P.map œÜ).eval) :
    good P.eval := by
  rcases hP with ‚ü®a', b', c', h'‚ü©
  obtain ‚ü®a, rfl‚ü© : ‚àÉ a, œÜ a = a' := hœÜ a'
  obtain ‚ü®b, rfl‚ü© : ‚àÉ b, œÜ b = b' := hœÜ b'
  obtain ‚ü®c, rfl‚ü© : ‚àÉ c, œÜ c = c' := hœÜ c'
  refine ‚ü®a, b, c, Œª r hr ‚Ü¶ h' (œÜ r) ?_‚ü©
  dsimp only; rw [P.eval_map œÜ, P.eval‚ÇÇ_at_apply, ‚Üê œÜ.map_pow, ‚Üê œÜ.map_mul,
    ‚Üê œÜ.map_mul, ‚Üê œÜ.map_add, ‚Üê œÜ.map_add, ‚Üê œÜ.map_sub, ‚Üê œÜ.map_mul, hr, œÜ.map_zero]

/-- If `R` is a domain, then `f : R ‚Üí R` is good if and only if there exists `a, b, c ‚àà R`
  such that `ar^2 + br + c ‚â† 0` and `ar^2 + br + c ‚â† f(r)` for any `r ‚àà R`. -/
theorem good_iff_of_domain [Ring R] [NoZeroDivisors R] {f : R ‚Üí R} :
    good f ‚Üî ‚àÉ a b c : R, ‚àÄ r : R,
      a * r ^ 2 + b * r + c ‚â† 0 ‚àß a * r ^ 2 + b * r + c ‚â† f r := by
  conv => left; unfold good; right; ext; right; ext; right; ext; ext
          rw [mul_ne_zero_iff, sub_ne_zero, ne_comm (a := f _)]

/-- If `f : R ‚Üí R` does not attain some unit, then `f` is good. -/
theorem good_of_map_ne_unit [Ring R]
    (f : R ‚Üí R) (u : RÀ£) (hf : ‚àÄ r, f r ‚â† u) : good f := by
  refine ‚ü®0, 0, u, Œª r ‚Ü¶ ?_‚ü©
  rw [zero_mul, zero_mul, zero_add, zero_add, ne_eq, Units.mul_right_eq_zero, sub_eq_zero]
  exact hf r


section

variable [CommRing R] {f : R ‚Üí R}

/-- If `f : R ‚Üí R` is good, then for any `v ‚àà R`, the function `x ‚Ü¶ f(x + v)` is good. -/
theorem good.shift (hf : good f) (v : R) : good (Œª x ‚Ü¶ f (x + v)) := by
  rcases hf with ‚ü®a, b, c, h‚ü©
  refine ‚ü®a, a * (2 * v) + b, a * v ^ 2 + b * v + c, Œª r ‚Ü¶ ?_‚ü©
  rw [add_mul _ _ r, ‚Üê add_assoc (a * r ^ 2), mul_assoc, ‚Üê mul_add, ‚Üê add_assoc,
    add_add_add_comm, ‚Üê mul_add, ‚Üê mul_add, mul_right_comm, ‚Üê add_sq]
  exact h (r + v)

/-- For any `f : R ‚Üí R` and `v ‚àà R`, the function `x ‚Ü¶ f(x + v)` is good iff `f` is good. -/
theorem good_shift_iff {v : R} : good (Œª x ‚Ü¶ f (x + v)) ‚Üî good f :=
  ‚ü®Œª hf ‚Ü¶ by simpa only [neg_add_cancel_right] using hf.shift (-v), Œª hf ‚Ü¶ hf.shift v‚ü©

/-- If `f : R ‚Üí R` is good, then for any `u ‚àà R`, the function `x ‚Ü¶ f(ux)` is good. -/
theorem good.scale (hf : good f) (u : R) : good (Œª x ‚Ü¶ f (u * x)) := by
  rcases hf with ‚ü®a, b, c, h‚ü©
  refine ‚ü®a * u ^ 2, b * u, c, Œª r ‚Ü¶ ?_‚ü©
  rw [mul_assoc, ‚Üê mul_pow, mul_assoc]
  exact h (u * r)

/-- For any `f : R ‚Üí R` and `u ‚àà RÀ£`, the function `x ‚Ü¶ f(ux)` is good iff `f` is good. -/
theorem good_scale_iff {u : RÀ£} : good (Œª x ‚Ü¶ f (u * x)) ‚Üî good f :=
  ‚ü®Œª hf ‚Ü¶ by simpa only [Units.mul_inv_cancel_left] using hf.scale ‚Üëu‚Åª¬π, Œª hf ‚Ü¶ hf.scale u‚ü©

/-- For any `f : R ‚Üí R`, `u ‚àà RÀ£`, and `v ‚àà R`,
  the function `x ‚Ü¶ f(ux + v)` is good iff `f` is good. -/
theorem good_shift_scale_iff {u : RÀ£} {v : R} : good (Œª x ‚Ü¶ f (u * x + v)) ‚Üî good f :=
  (good_scale_iff (f := Œª x ‚Ü¶ f (x + v)) (u := u)).trans good_shift_iff

end





/-! ### The finite field case -/

/-- Let `F` be a division ring. For any `a, b, c, d ‚àà F` such that `a ‚â† b` and `c ‚â† d`,
  there exists `u ‚àà FÀ£` and `v ‚àà F` such that `uc + v = a` and `ud + v = b`. -/
theorem linear_transform_solver [DivisionRing F] {a b c d : F} (h : a ‚â† b) (h0 : c ‚â† d) :
    ‚àÉ u : FÀ£, ‚àÉ v : F, u * c + v = a ‚àß u * d + v = b := by
  ---- Solving directly yields `u = (b - a)/(d - c)`; and `v = a - uc`.
  replace h : b - a ‚â† 0 := sub_ne_zero_of_ne h.symm
  replace h0 : d - c ‚â† 0 := sub_ne_zero_of_ne h0.symm
  obtain ‚ü®u, hu‚ü© : ‚àÉ u : FÀ£, ‚Üëu = (b - a) / (d - c) :=
    ‚ü®Units.mk0 ((b - a) / (d - c)) (div_ne_zero h h0), rfl‚ü©
  refine ‚ü®u, a - u * c, add_sub_cancel _ _, ?_‚ü©
  rw [add_sub_left_comm, ‚Üê mul_sub, hu, div_mul_cancel‚ÇÄ _ h0, add_sub_cancel]

open Finset in
/-- Let `f : S ‚Üí S` be a function on a finite set `S`. Suppose that there exists
  `a, b, c ‚àà S` pairwise distinct and `d ‚â† c` such that `f(a) = f(b)` and `f(c) = f(d)`.
  Then for any `x ‚àà S`, there exists `y ‚àà S` with `y ‚â† x` such that `y ‚àâ f(S)`. -/
theorem exists_ne_map_ne_of_four [Fintype S] [DecidableEq S] {f : S ‚Üí S}
    {a b c d : S} (hab : a ‚â† b) (hac : a ‚â† c) (hbc : b ‚â† c) (hcd : c ‚â† d)
    (hab0 : f a = f b) (hcd0 : f c = f d) (x) :
    ‚àÉ y ‚â† x, ‚àÄ z, f z ‚â† y := by
  ---- WLOG assume that `a ‚â† d`.
  wlog had : a ‚â† d generalizing a b
  ¬∑ exact this hab.symm hbc hac hab0.symm Œª h ‚Ü¶ had (hab.trans_eq h)
  ---- First note that `{c, a}·∂ú` is smaller than `{x}·∂ú`.
  have h : ({c, a}·∂ú : Finset S).card < ({x}·∂ú : Finset S).card := by
    rw [card_compl {x}, card_singleton, ‚Üê card_singleton a, ‚Üê card_compl, compl_insert]
    refine card_erase_lt_of_mem ?_
    rwa [mem_compl, mem_singleton, eq_comm]
  ---- Then there exists `y ‚â† x` such that `f(z) ‚â† y` for `z ‚â† a, c`.
  obtain ‚ü®y, hy, hy0‚ü© : ‚àÉ y ‚â† x, ‚àÄ z ‚â† c, z ‚â† a ‚Üí f z ‚â† y := by
    -- Thus `f` does not map `{c, a}·∂ú` into a superset of `{x}·∂ú`, and so we're done.
    replace h : ¬¨Set.SurjOn f ({c, a}·∂ú : Finset S) ({x}·∂ú : Finset S) :=
      Œª h0 ‚Ü¶ h.not_ge (card_le_card_of_surjOn f h0)
    rw [Set.SurjOn, Set.subset_def, not_forall] at h
    rcases h with ‚ü®y, hy‚ü©
    rw [_root_.not_imp, mem_coe, mem_compl, mem_singleton, Set.mem_image, not_exists] at hy
    refine ‚ü®y, hy.1, Œª z ‚Ü¶ ?_‚ü©
    replace hy : ¬¨(z ‚àà (({c, a}·∂ú : Finset S) : Set S) ‚àß f z = y) := hy.2 z
    rw [not_and, mem_coe, mem_compl, mem_insert, not_or, mem_singleton] at hy
    exact Œª h h0 ‚Ü¶ hy ‚ü®h, h0‚ü©
  ---- Now we claim that this `y` indeed works, i.e., `f(a), f(c) ‚â† y`.
  refine ‚ü®y, hy, Œª z ‚Ü¶ ?_‚ü©
  clear h hy
  ---- First consider the case `z = a`.
  obtain rfl | hza : z = a ‚à® z ‚â† a := dec_em _
  ¬∑ exact hab0.trans_ne (hy0 b hbc hab.symm)
  ---- Second, consider the case `z = c`.
  obtain rfl | hzc : z = c ‚à® z ‚â† c := dec_em _
  ¬∑ exact hcd0.trans_ne (hy0 d hcd.symm had.symm)
  ---- Finally, resolve the case `z ‚â† a, c`.
  ¬∑ exact hy0 z hzc hza

/-- Let `F` be a finite field of characteristic `‚â† 2`.
  Then every function from `F` to itself is good. -/
theorem good_of_char_ne_two [Field F] [Fintype F] [DecidableEq F] (hF : ringChar F ‚â† 2) :
    ‚àÄ f : F ‚Üí F, good f := by
  have X : (1 : F) ‚â† 0 := one_ne_zero
  ---- Reduce to the case where `0 ‚àâ f(F)`.
  intro f; wlog h : ‚àÄ x, f x ‚â† 0 generalizing f
  ¬∑ -- Consider `g : F ‚Üí F` defined by `g(x) = 1` if `x = 0` and `g(x) = f(x)` otherwise.
    obtain ‚ü®a, b, c, h‚ü© : ‚àÉ a b c : F, ‚àÄ r : F, a * r ^ 2 + b * r + c ‚â† 0 ‚àß
        a * r ^ 2 + b * r + c ‚â† if f r = 0 then 1 else f r := by
      refine good_iff_of_domain.mp (this (Œª x ‚Ü¶ if f x = 0 then 1 else f x) ?_)
      intro x; dsimp only
      split_ifs with h
      exacts [X, h]
    -- Now that we've shown that `g` is good, use it to show that `f` is good.
    refine good_iff_of_domain.mpr ‚ü®a, b, c, Œª r ‚Ü¶ ?_‚ü©
    let s : F := a * r ^ 2 + b * r + c
    obtain ‚ü®h0, h1‚ü© : s ‚â† 0 ‚àß s ‚â† if f r = 0 then 1 else f r := h r
    refine ‚ü®h0, Œª (h2 : s = f r) ‚Ü¶ h1 ?_‚ü©
    rw [h2, if_neg (h2.symm.trans_ne h0)]
  ---- Now reduce to the case where `f(-1) = f(1)`.
  wlog h0 : f 1 = f (-1) generalizing f
  ¬∑ -- Note that `f` is not injective. Find `c ‚â† d ‚àà F` such that `f(c) = f(d)`.
    obtain ‚ü®c, d, h1, h2‚ü© : ‚àÉ c d, f c = f d ‚àß c ‚â† d := by
      have h1 : ¬¨f.Surjective := Œª h1 ‚Ü¶ (h1 0).elim h
      simpa [Function.Injective] using Finite.surjective_of_injective.mt h1
    -- Now find `u ‚àà RÀ£` and `v ‚àà R` such that `uc + v = 1` and `ud + v = -1`.
    obtain ‚ü®u, v, h3, h4‚ü© : ‚àÉ u : FÀ£, ‚àÉ v : F, u * c + v = 1 ‚àß u * d + v = -1 :=
      linear_transform_solver (Œª h3 ‚Ü¶ h0 (congrArg f h3)) h2
    -- Then `x ‚Ü¶ f(u‚Åª¬π(x - v))` being good implies that `f` is good.
    refine (good_shift_scale_iff (u := u‚Åª¬π) (v := u‚Åª¬π * -v)).mp (this _ (Œª x ‚Ü¶ h _) ?_)
    rw [‚Üê mul_add, ‚Üê mul_add, ‚Üê h4, add_neg_cancel_right, Units.inv_mul_cancel_left,
      ‚Üê h3, add_neg_cancel_right, Units.inv_mul_cancel_left, h1]
  ---- If `f` does not attain a non-zero value, then we are done.
  by_cases h1 : ‚àÉ u : FÀ£, ‚àÄ x, f x ‚â† u
  ¬∑ rcases h1 with ‚ü®u, hu‚ü©
    exact good_of_map_ne_unit f u hu
  ---- Now suppose that `f` attains every non-zero value.
  replace h1 : ‚àÄ u ‚â† 0, ‚àÉ x, f x = u := by
    simp only [ne_eq, not_exists, not_forall, not_not] at h1
    exact Œª u hu ‚Ü¶ h1 (Units.mk0 u hu)
  ---- Let `r` be a non-square in `F`.
  obtain ‚ü®r', hr‚ü© : ‚àÉ r' : F, ¬¨IsSquare r' := FiniteField.exists_nonsquare hF
  ---- Choose `y` such that `f(y) = (1 - r') f(0)`, and note that `y ‚â† 0`.
  obtain ‚ü®y, hy‚ü© : ‚àÉ y, f y = (1 - r') * f 0 :=
    h1 _ (mul_ne_zero (sub_ne_zero.mpr Œª hr0 ‚Ü¶ hr (hr0 ‚ñ∏ IsSquare.one)) (h 0))
  have hr' : r' ‚â† 0 := Œª hr' ‚Ü¶ hr (hr' ‚ñ∏ IsSquare.zero)
  have hy0 : y ‚â† 0 := by
    rintro rfl; rw [eq_comm, mul_eq_right‚ÇÄ (h 0), sub_eq_self] at hy
    exact hr' hy
  ---- Let `r = y^2/r'`, and note that `r` is not a square either.
  let r : F := y ^ 2 / r'
  replace hr : ¬¨IsSquare r := by
    refine Œª hr0 ‚Ü¶ hr ?_
    simpa only [r, div_div_cancel‚ÇÄ (pow_ne_zero 2 hy0)] using (IsSquare.sq y).div hr0
  replace hr (x) : x ^ 2 - r ‚â† 0 :=
    Œª hr0 ‚Ü¶ hr ‚ü®x, by rw [‚Üê sq, eq_comm, ‚Üê sub_eq_zero, hr0]‚ü©
  ---- The function `œÜ(x) = f(x)/(x^2 - r)` does not attain a non-zero value.
  obtain ‚ü®a, ha, ha0‚ü© : ‚àÉ a ‚â† 0, ‚àÄ x, f x / (x ^ 2 - r) ‚â† a := by
    refine exists_ne_map_ne_of_four (a := -1) (b := 1) (c := 0) (d := y)
      (Ring.neg_one_ne_one_of_char_ne_two hF) (neg_ne_zero.mpr X) X hy0.symm ?_ ?_ _
    -- Verify that `œÜ(-1) = œÜ(1)`.
    ¬∑ rw [h0, neg_sq]
    -- Verify that `œÜ(0) = œÜ(y)`.
    ¬∑ rw [hy, div_eq_div_iff (hr _) (hr _), zero_pow Nat.two_pos.ne.symm, zero_sub,
        mul_right_comm, mul_neg, one_sub_mul, neg_sub, mul_div_cancel‚ÇÄ _ hr', mul_comm]
  ---- Then take `(a, b, c) = (a, 0, -ar)` and see that it works.
  refine good_iff_of_domain.mpr ‚ü®a, 0, a * (-r), Œª x ‚Ü¶ ?_‚ü©
  rw [zero_mul, add_zero, ‚Üê mul_add, ‚Üê sub_eq_add_neg]
  exact ‚ü®mul_ne_zero ha (hr x), Œª h2 ‚Ü¶ ha0 x (eq_div_of_mul_eq (hr x) h2).symm‚ü©





/-! ### The `ZMod` case -/

/-- Any function over `ZMod 1` is not good. -/
theorem ZMod1_not_good (f : ZMod 1 ‚Üí ZMod 1) : ¬¨good f := by
  rintro ‚ü®_, _, c, h‚ü©
  replace h : (0 + c) * (f 0 - (0 + c)) ‚â† 0 := h 0
  rw [Subsingleton.eq_zero c, Subsingleton.eq_zero (f 0)] at h
  exact h rfl

/-- The constant one function is not good over `ZMod 2`. -/
theorem const_one_ZMod2_not_good : ¬¨good (Œª _ : ZMod 2 ‚Ü¶ 1) := by
  rintro ‚ü®_, _, c, h‚ü©
  refine h 0 (?_ : (0 + c) * (1 - (0 + c)) = 0)
  match c with | 0 => rfl | 1 => rfl

/-- For any `a, b ‚àà ZMod 4`, there exists `c ‚àà ZMod 4` such that `c(a - c), c(b - c) ‚â† 0`. -/
theorem ZMod4_exists_mul_sub_ne_zero‚ÇÇ :
    ‚àÄ a b : ZMod 4, ‚àÉ c, c * (a - c) ‚â† 0 ‚àß c * (b - c) ‚â† 0 := by
  decide

/-- Every function `f : ZMod 4 ‚Üí ZMod 4` is good. -/
theorem ZMod4_good (f : ZMod 4 ‚Üí ZMod 4) : good f := by
  ---- Pick some `u ‚àà ZMod 4` such that `c(f(0) - c) ‚â† 0` and `c(f(2) - c) ‚â† 0`.
  obtain ‚ü®u, hc0, hc2‚ü© : ‚àÉ u, u * (f 0 - u) ‚â† 0 ‚àß u * (f 2 - u) ‚â† 0 :=
    ZMod4_exists_mul_sub_ne_zero‚ÇÇ (f 0) (f 2)
  ---- Similarly, pick some `v ‚àà ZMod 4` such that `d(f(1) - d) ‚â† 0` and `d(f(3) - d) ‚â† 0`.
  obtain ‚ü®v, hd1, hd3‚ü© : ‚àÉ v, v * (f 1 - v) ‚â† 0 ‚àß v * (f 3 - v) ‚â† 0 :=
    ZMod4_exists_mul_sub_ne_zero‚ÇÇ (f 1) (f 3)
  ---- Then `ax^2 + bx + c = u(1 - x^2) + vx^2 = (v - u)x^2 + u` works.
  refine ‚ü®v - u, 0, u, Œª r ‚Ü¶ ?_‚ü©
  have h (y) : ((v - u) * 0 + u) * (y - ((v - u) * 0 + u)) = u * (y - u) := by
    rw [mul_zero, zero_add]
  have h0 (y) : ((v - u) * 1 + u) * (y - ((v - u) * 1 + u)) = v * (y - v) := by
    rw [mul_one, sub_add_cancel]
  rw [zero_mul, add_zero]
  match r with
  | 0 => exact (h (f 0)).trans_ne hc0
  | 1 => exact (h0 (f 1)).trans_ne hd1
  | 2 => exact (h (f 2)).trans_ne hc2
  | 3 => exact (h0 (f 3)).trans_ne hd3

/-- If `p` is an odd prime, then every function `f : ZMod p ‚Üí ZMod p` is good. -/
theorem ZMod_good_of_prime_ne_two (p) [Fact (Nat.Prime p)] (hp : p ‚â† 2) :
    ‚àÄ f : ZMod p ‚Üí ZMod p, good f :=
  good_of_char_ne_two ((ZMod.ringChar_zmod_n p).trans_ne hp)

/-- If `n > 2`, then every polynomial over `ZMod n` is good. -/
theorem ZMod_good_of_two_lt (hn : 2 < n) (P : (ZMod n)[X]) : good P.eval := by
  ---- There are two cases: (1). `n` is a power of `2`; (2). `n` has an odd prime factor.
  obtain ‚ü®k, rfl‚ü© | ‚ü®p, hp, hpn, hp0‚ü© :
      (‚àÉ k, n = 2 ^ k) ‚à® (‚àÉ p, Nat.Prime p ‚àß p ‚à£ n ‚àß Odd p) :=
    Nat.eq_two_pow_or_exists_odd_prime_and_dvd n
  ---- Case 1: `n` is a power of two.
  ¬∑ replace hn : 2 ‚â§ k := (Nat.pow_lt_pow_iff_right Nat.one_lt_two).mp hn
    replace hn : 4 ‚à£ 2 ^ k := Nat.pow_dvd_pow 2 hn
    exact good_of_good_polynomial_map (ZMod.castHom hn (ZMod 4))
      (ZMod.castHom_surjective _) P (ZMod4_good _)
  ---- Case 2: `n` is divisible by an odd prime.
  ¬∑ haveI : Fact (Nat.Prime p) := ‚ü®hp‚ü©
    replace hp0 : p ‚â† 2 := Œª h ‚Ü¶ Nat.not_even_iff_odd.mpr hp0 (h ‚ñ∏ even_two)
    exact good_of_good_polynomial_map (ZMod.castHom hpn (ZMod p))
      (ZMod.castHom_surjective _) P (ZMod_good_of_prime_ne_two p hp0 _)

/-- If `n > 0`, then every polynomial over `ZMod n` is good iff `n > 2`. -/
theorem ZMod_forall_polynomial_good_iff_two_lt (hn : n > 0) :
    (‚àÄ P : (ZMod n)[X], good P.eval) ‚Üî 2 < n := by
  refine ‚ü®Œª hn0 ‚Ü¶ Nat.lt_of_not_le Œª hn1 ‚Ü¶ ?_, ZMod_good_of_two_lt‚ü©
  rw [Nat.le_succ_iff, Nat.le_one_iff_eq_zero_or_eq_one] at hn1
  rcases (hn1 : (n = 0 ‚à® n = 1) ‚à® n = 2) with (rfl | rfl) | rfl
  exacts [hn.ne rfl, ZMod1_not_good X.eval (hn0 _),
    const_one_ZMod2_not_good (by simpa only [eval_one] using hn0 1)]





/-! ### The `‚Ñ§` version -/

/-- A function `f : ‚Ñ§ ‚Üí ‚Ñ§` is called `n`-good if there exists `a, b, c ‚àà ‚Ñ§` with `a ‚â† 0`
  such that `n ‚à§ (ak^2 + bk + c)(P(k) + (ak^2 + bk + c))` for all integer `k`. -/
def natGood (n : ‚Ñï) (f : ‚Ñ§ ‚Üí ‚Ñ§) :=
  ‚àÉ a ‚â† 0, ‚àÉ b c, ‚àÄ k, ¬¨(n : ‚Ñ§) ‚à£ (a * k ^ 2 + b * k + c) * (f k + (a * k ^ 2 + b * k + c))

/-- The version of `natGood` over `ZMod n`, also with `f(k) - (ak^2 + bk + c)`
  instead of `f(k) + (ak^2 + bk + c)`. -/
theorem natGood_iff :
    natGood n f ‚Üî ‚àÉ a ‚â† 0, ‚àÉ b c,
      ‚àÄ k, ¬¨(n : ‚Ñ§) ‚à£ (a * k ^ 2 + b * k + c) * (f k - (a * k ^ 2 + b * k + c)) := by
  refine ‚ü®?_, ?_‚ü©
  ---- The `‚Üí` direction.
  ¬∑ rintro ‚ü®a, ha, b, c, h‚ü©
    refine ‚ü®-a, Int.neg_ne_zero.mpr ha, -b, -c, Œª k ‚Ü¶ ?_‚ü©
    rw [neg_mul, neg_mul, ‚Üê neg_add, ‚Üê neg_add, neg_mul, dvd_neg, sub_neg_eq_add]
    exact h k
  ---- The `‚Üê` direction; basically the same proof.
  ¬∑ rintro ‚ü®a, ha, b, c, h‚ü©
    refine ‚ü®-a, Int.neg_ne_zero.mpr ha, -b, -c, Œª k ‚Ü¶ ?_‚ü©
    rw [neg_mul, neg_mul, ‚Üê neg_add, ‚Üê neg_add, neg_mul, dvd_neg, ‚Üê sub_eq_add_neg]
    exact h k

/-- If `n > 0`, the condition `a ‚â† 0` on `natGood f n` is superficial. -/
theorem natGood_iff_of_pos (hn : n > 0) :
    natGood n f ‚Üî ‚àÉ a b c, ‚àÄ k,
      ¬¨(n : ‚Ñ§) ‚à£ (a * k ^ 2 + b * k + c) * (f k - (a * k ^ 2 + b * k + c)) := by
  ---- Only the `‚Üê` direction is non-trivial.
  refine natGood_iff.trans ‚ü®Œª h ‚Ü¶ h.elim Œª a ha ‚Ü¶ ‚ü®a, ha.2‚ü©, ?_‚ü©
  rintro ‚ü®a, ha‚ü©
  ---- If `a ‚â† 0`, we are done.
  obtain ha0 | rfl : a ‚â† 0 ‚à® a = 0 := ne_or_eq a 0
  ¬∑ exact ‚ü®a, ha0, ha‚ü©
  ---- If `a = 0`, then replace it with `a = n`.
  rcases ha with ‚ü®b, c, h‚ü©
  simp only [zero_mul, zero_add] at h
  refine ‚ü®n, Int.natCast_ne_zero.mpr hn.ne.symm, b, c, Œª k ‚Ü¶ ?_‚ü©
  rw [Int.add_assoc, Int.add_mul, Int.mul_assoc, Int.dvd_self_mul_add, ‚Üê Int.sub_sub,
    Int.sub_eq_add_neg (a := f k), add_sub_right_comm, Int.mul_add, ‚Üê Int.mul_neg,
    Int.mul_left_comm, Int.add_comm, Int.dvd_self_mul_add]
  exact h k

/-- An integer polynomial `P` is `n`-good if and only if `œÜ(P)` is `good`,
  where `œÜ` is the canonical map from `‚Ñ§` to `‚Ñ§/n‚Ñ§`. -/
theorem natGood_polynomial_iff_good (hn : n > 0) (P : ‚Ñ§[X]) :
    natGood n P.eval ‚Üî good (P.map (Int.castRingHom (ZMod n))).eval := by
  set œÜ : ‚Ñ§ ‚Üí+* ZMod n := Int.castRingHom (ZMod n)
  have hœÜ : (œÜ : ‚Ñ§ ‚Üí ZMod n).Surjective := ZMod.ringHom_surjective œÜ
  rw [natGood_iff_of_pos hn, good, hœÜ.exists‚ÇÉ]
  refine exists_congr Œª a ‚Ü¶ exists_congr Œª b ‚Ü¶ exists_congr Œª c ‚Ü¶ ?_
  rw [hœÜ.forall]; refine forall_congr' Œª k ‚Ü¶ ?_
  rw [‚Üê ZMod.intCast_zmod_eq_zero_iff_dvd, Int.cast_mul,
    Int.cast_sub, eq_intCast œÜ k, eval_intCast_map, Int.cast_eq]
  simp_rw [Int.cast_add, Int.cast_mul, Int.cast_pow, eq_intCast]

/-- Final solution -/
theorem final_solution (hn : n > 0) : (‚àÄ P : ‚Ñ§[X], natGood n P.eval) ‚Üî n > 2 := by
  simp_rw [‚Üê ZMod_forall_polynomial_good_iff_two_lt hn, natGood_polynomial_iff_good hn]
  exact Iff.symm (map_surjective _ (ZMod.ringHom_surjective _)).forall
