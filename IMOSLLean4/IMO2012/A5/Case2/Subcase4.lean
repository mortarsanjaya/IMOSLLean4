/-
Copyright (c) 2023 Gian Cordana Sanjaya. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Gian Cordana Sanjaya
-/

import IMOSLLean4.IMO2012.A5.Case2.Basic
import IMOSLLean4.IMO2012.A5.A5Quotient

/-!
# IMO 2012 A5, Subcase 2.4: $f(-1) = 0$ and $f(2) = 1$

This file solves Subcase 2.4.
-/

namespace IMOSL
namespace IMO2012A5

/-! #### Some lemmas in characteristic two (to avoid `CharP` imports) -/

namespace Char2

variable [CommRing R] (h : (2 : R) = 0)

theorem add_self (x : R) : x + x = 0 :=
  (two_mul x).symm.trans (mul_eq_zero_of_left h x)

theorem add_add_cancel (x y : R) : x + y + y = x :=
  by rw [add_assoc, add_self h, add_zero]

theorem sub_eq_add (x y : R) : x - y = x + y :=
  sub_eq_of_eq_add (add_add_cancel h x y).symm

theorem add_sq (x y : R) : (x + y) ^ 2 = x ^ 2 + y ^ 2 :=
  by rw [add_sq', h, zero_mul, zero_mul, add_zero]

theorem add_one_sq (x : R) : (x + 1) ^ 2 = x ^ 2 + 1 :=
  by rw [add_sq h, one_pow]

theorem add_eq_iff_eq_add {x y z : R} : x + y = z â†” x = z + y :=
  sub_eq_add h x y â–¸ sub_eq_iff_eq_add

theorem add_eq_iff_eq_add' {x y z : R} : x + y = z â†” x = y + z :=
  sub_eq_add h x y â–¸ sub_eq_iff_eq_add'

theorem add_eq_zero_iff_eq {x y : R} : x + y = 0 â†” x = y :=
  by rw [add_eq_iff_eq_add h, zero_add]

end Char2





/-! ## The solution -/

variable [CommRing R] [CommRing S] [IsDomain S] {f : R â†’ S} (h : good f)

/-- `2 âˆˆ I` -/
theorem case2_4_lem1 (h0 : f (-1) = 0) (h1 : f 2 = -1) :
    2 âˆˆ periodIdeal h := by
  have h2 x : f (x + 1) = f (x - 1) âˆ¨ f x + f (x - 1) = -1 := by
    have h2 := case2_special_identity h h0 x
    rwa [case2_map_add_two_eq h h0, h1, neg_one_mul, neg_sub, sub_add,
      mul_neg_one, â† add_sub_assoc, neg_add_self, eq_sub_iff_add_eq, mul_sub,
      â† sub_add, mul_comm _ (f x), â† mul_sub, â† add_mul, â† add_one_mul (Î± := S),
      mul_eq_zero, add_eq_zero_iff_eq_neg, sub_eq_zero, or_comm] at h2
  intro x
  rw [â† one_add_one_eq_two, â† add_rotate]
  rcases h2 (x + 1) with h3 | h3
  rwa [add_sub_cancel] at h3
  specialize h2 (-(x + 1))
  have h4 := case2_map_even h h0
  rw [â† neg_add', h4, h4, neg_add_eq_sub, sub_add_cancel'', h4] at h2
  refine h2.elim Eq.symm Î» h2 â†¦ ?_
  rwa [add_sub_cancel, â† h2, add_right_inj, eq_comm] at h3

section Rchar2

variable (h0 : (2 : R) = 0) (h1 : f 0 = -1)

theorem case2_4_lem2 : f (-1) = 0 := by
  rw [â† one_add_one_eq_two, add_eq_zero_iff_eq_neg] at h0
  rw [â† h0, good_map_one h]

/-- (10.1) -/
theorem case2_4_lem3 (x : R) : f (x * (x + 1) + 1) = f x * f (x + 1) :=
  Char2.sub_eq_add h0 (x * (x + 1)) 1 â–¸
    case2_map_self_mul_add_one_sub_one h (case2_4_lem2 h h0) x

/-- (10.2) -/
theorem case2_4_lem4 (x : R) : f (x ^ 2 + 1) = f x ^ 2 - 1 :=
  Char2.sub_eq_add h0 (x ^ 2) 1 â–¸ case2_map_sq_sub_one h (case2_4_lem2 h h0) h1 x

/-- (10.3) -/
theorem case2_4_lem5 (x : R) : f (x ^ 2) = f (x + 1) ^ 2 - 1 := by
  rw [â† case2_4_lem4 h h0 h1, Char2.add_one_sq h0, Char2.add_add_cancel h0]

/-- Lemma 4 (TODO: Optimize the proof, if possible) -/
theorem case2_4_lem6 (x : R) :
    f x ^ 2 + f (x + 1) ^ 2 = 1 âˆ¨ f x + f (x + 1) = 1 := by
  have h3 := case2_4_lem3 h h0
  have h4 := case2_4_lem5 h h0 h1
  ---- (10.L1.1)
  have h5 x :
      (f (x + 1) ^ 2 - 1) * (f (x + 1) - 1) + f x * f (x + 1)
        = f x * f (x * (x + 1)) := by
    rw [â† h3, â† h4, â† h, eq_sub_iff_add_eq, add_right_comm, â† eq_sub_iff_add_eq,
      â† mul_assoc, mul_add_one x, â† sq, add_left_comm, Char2.add_self h0,
      add_zero, â† mul_add_one (f (x ^ 2)), sub_add_cancel, add_assoc, h]
  ---- Now use (10.L1.1) for more reduction
  have h6 : (f x) * _ = _ := congr_argâ‚‚ _ rfl (h5 (x + 1))
  rw [Char2.add_add_cancel h0, mul_left_comm, mul_comm (x + 1), â† h5] at h6
  replace h6 :
      (f x ^ 2 + f (x + 1) ^ 2 - 1) * (f x + f (x + 1) - 1)
        * (f x - f (x + 1)) = 0 :=
    sub_eq_zero_of_eq h6 â–¸ by ring
  rw [mul_eq_zero, mul_eq_zero, sub_eq_zero, sub_eq_zero, sub_eq_zero] at h6
  rcases h6 with h6 | h6; exact h6; right
  ---- Assuming `f(x) = f(x + 1)`, prove that `f(x) + f(x + 1) = 1`
  specialize h3 (x ^ 2)
  have h2 := case2_4_lem4 h h0 h1
  rw [h4, h2, â† Char2.add_one_sq h0, â† mul_pow, h2] at h3
  replace h3 : _ * _ = _ * _ := congr_arg ((f x) ^ 2 * Â·) h3
  rw [mul_sub_one, â† mul_pow, â† h5, â† h6, â† sq, mul_sub_one,
    â† add_sub_right_comm, add_sub_assoc, sub_sub_cancel, â† sq, add_sq,
    mul_comm, one_pow, add_assoc, mul_pow, sub_eq_iff_eq_add, add_right_inj,
    mul_one, â† eq_sub_iff_add_eq,â† mul_assoc, mul_left_eq_selfâ‚€] at h3
  rcases h3 with h3 | h3
  Â· rwa [â† h6, â† two_mul]
  Â· specialize h5 (x ^ 2)
    rw [h2, h4, â† h6, h3, sq, zero_mul, zero_mul,
      zero_sub, neg_mul_neg, mul_one, add_zero] at h5
    exact absurd h5 one_ne_zero

/-- Solution for the current sub-subcase (`hom_sub_one: x â†¦ Ï†(x) - 1`).
  (TODO: Optimize the proof, if possible) -/
theorem case2_4_Schar2_quot_IsAnswer (h3 : (2 : S) = 0) : IsAnswer f :=
  IsAnswer_of_add_one_additive h <| by
    ---- (10.L2.1)
    have h4 x : f (x + 1) = f x + 1 := by
      rw [â† Char2.add_eq_iff_eq_add' h3, add_comm]
      refine (case2_4_lem6 h h0 h1 x).elim (Î» h4 â†¦ ?_) id
      rwa [â† Char2.add_sq h3, â† Char2.add_eq_zero_iff_eq h3,
        â† Char2.add_one_sq h3, sq_eq_zero_iff, Char2.add_eq_zero_iff_eq h3] at h4
    ---- (10.L2.2)
    replace h x y : f (x * y) = f x * f y + f (x + y) + 1 :=
      by rw [â† h, sub_add_cancel, h4, Char2.add_add_cancel h3]
    ---- Back to the main equality
    intros x y
    let a := f x
    let b := f y
    let c := f (x + y)
    have h6 := h (x * y) ((y + 1) * (x + 1))
    rw [mul_mul_mul_comm, h, add_left_inj] at h6
    have h7 : x * y + (y + 1) * (x + 1) = x * (y + 1) + y * (x + 1) + 1 := by ring
    rw [h7, h4, â† add_assoc, â† sub_eq_iff_eq_add', add_sub_add_right_eq_sub] at h6
    replace h7 : f (x * y) = a * b + c + 1 := h x y
    have h8 : f (x * (y + 1)) = a * (b + 1) + (c + 1) + 1 := by rw [h, â† add_assoc, h4, h4]
    have h9 : f (y * (x + 1)) = b * (a + 1) + (c + 1) + 1 := by
      rw [h, add_left_comm, â† add_assoc, h4, h4]
    have h10 : f ((y + 1) * (x + 1)) = (b + 1) * (a + 1) + c + 1 := by
      rw [h, add_add_add_comm, one_add_one_eq_two, h0, add_zero, add_comm y x, h4, h4]
    replace h6 := (congr_argâ‚‚ _ (congr_argâ‚‚ _ h8 h9) (congr_argâ‚‚ _ h7 h10)).symm.trans h6
    rw [â† sub_eq_iff_eq_add', â† h6, eq_comm, â† sub_eq_zero]
    refine' Eq.trans _ (mul_eq_zero_of_left h3 <| (a + 1) * (b + 1))
    ring

variable (h2 : âˆ€ c âˆˆ periodIdeal h, c = 0) (h3 : (2 : S) â‰  0)

/-- Lemma 5 -/
theorem case2_4_lem7 (x : R) :
    f x * f (x + 1) = 0 âˆ¨ f x + f (x + 1) = 1 âˆ§ f x * f (x + 1) = -1 := by
  have h4 x : f ((x ^ 2) ^ 2) = f x ^ 2 * (f x ^ 2 - 2) := by
    rw [case2_4_lem5 h h0 h1, case2_4_lem4 h h0 h1, â† one_pow 2,
      sq_sub_sq, one_pow, sub_add_cancel, sub_sub, one_add_one_eq_two]
  have h5 := case2_4_lem3 h h0
  have h6 := Char2.add_one_sq h0
  have h7 := h4 (x * (x + 1) + 1)
  rw [h5, h6, h6, mul_pow, h6, mul_pow, h6, h5, h4, â† h6, â† h6, h4,
    mul_mul_mul_comm, â† mul_pow, mul_eq_mul_left_iff, or_comm] at h7
  refine h7.imp sq_eq_zero_iff.mp Î» h8 â†¦ ?_
  rw [sub_mul, mul_sub, â† mul_pow, sub_sub, sub_right_inj, mul_comm, â† mul_add,
    mul_right_eq_selfâ‚€, or_iff_left h3, â† add_sub_assoc, sub_eq_iff_eq_add] at h8
  replace h7 := case2_4_lem6 h h0 h1 x
  rw [h8, add_right_eq_self, or_iff_right h3] at h7
  refine âŸ¨h7, ?_âŸ©
  apply congr_arg (Â· ^ 2) at h7
  rw [add_sq', h8, one_pow, add_assoc, add_right_eq_self, mul_assoc,
    â† mul_one_add (2 : S), mul_eq_zero, or_iff_right h3] at h7
  exact eq_neg_of_add_eq_zero_right h7

/-- Lemma 6 (TODO: Refactor the proof, if possible) -/
theorem case2_4_lem8 : âˆ€ c, f c = -1 â†’ c = 0 := by
  ---- (10.L3.1)
  have A1 x (h6 : f x = -1) : f (x + 1) = 0 := by
    rcases case2_4_lem7 h h0 h1 h3 x with h7 | âŸ¨h7, h8âŸ©
    Â· rwa [h6, neg_one_mul, neg_eq_zero] at h7
    Â· rw [h6, neg_one_mul, neg_inj] at h8
      rw [h6, h8, neg_add_self] at h7
      rw [h8, h7]
  ---- (10.L3.2)
  have A2 c (h6 : f c = -1) x : f (c ^ 2 + c * x + 1) = -f (c * x + 1) := by
    rw [sq, â† mul_add, eq_add_of_sub_eq (h _ _), â† add_assoc,
      Char2.add_self h0, zero_add, eq_add_of_sub_eq (h _ _), h6,
      neg_one_mul, neg_one_mul, neg_add, neg_neg, add_comm]
  ---- Reduce to `c^4 = 0`
  intro c h4
  have X := case2_4_lem5 h h0 h1
  have h5 := X c
  rw [A1 c h4, sq (0 : S), zero_mul, zero_sub] at h5
  have h6 (d : R) (h7 : f d = -1) (h8 : d ^ 2 = 0) : d = 0 := h2 d <| by
    rw [mem_periodIdeal_iff, h1]
    refine âŸ¨Î» x â†¦ ?_, h7âŸ©
    have h9 := A2 d h7 x
    rwa [h8, zero_add, eq_neg_iff_add_eq_zero,
      â† two_mul, mul_eq_zero, or_iff_right h3] at h9
  refine h6 c h4 (h6 _ h5 ?_)
  ---- Prove that `c^6 + c^4 = 0`
  replace h6 : ((c ^ 2) ^ 2 + c ^ 2) * c ^ 2 = 0 := h2 _ <| by
    rw [mem_periodIdeal_iff, h1]
    have h7 := Char2.add_sq h0
    specialize A1 c h4
    have h8 : f (c ^ 2 + c + 1) = 0 := by
      rw [sq c, â† mul_add_one c, case2_4_lem3 h h0, A1, mul_zero]
    constructor
    Â· replace h6 x : f ((c ^ 2) ^ 2 + c ^ 2 + c ^ 2 * x + 1) = f (c ^ 2 * x + 1) :=
        by rw [add_assoc ((c ^ 2) ^ 2), â† mul_one_add (c ^ 2) x, A2 _ h5,
          mul_one_add (c ^ 2) x, sq, mul_assoc, â† sq, A2 _ h4, neg_neg]
      intro x
      rw [mul_assoc, mul_left_comm, â† h6, mul_left_comm,
        â† mul_one_add (Î± := R), eq_add_of_sub_eq (h _ _),
        add_comm (1 : R), â† add_assoc, h6, â† add_one_mul (Î± := S)]
      apply mul_eq_zero_of_left
      rw [â† h7, X, sub_add_cancel, h8, sq, zero_mul]
    Â· rwa [â† h7, â† mul_pow, X, sub_eq_neg_self, sq_eq_zero_iff,
        sq, â† add_one_mul c, mul_assoc, eq_add_of_sub_eq (h _ _),
        A1, zero_mul, zero_add, â† sq, â† add_rotate]
  ---- Final step
  apply h2
  rw [mem_periodIdeal_iff, h1, and_comm]
  have h7 := A1 (c ^ 2) h5
  constructor
  Â· rw [X, h7, sq, zero_mul, zero_sub]
  Â· intro x
    rw [sq, â† add_one_mul (Î± := R), mul_assoc, â† sq] at h6
    have h8 := h (c ^ 2 + 1) ((c ^ 2) ^ 2 * x + 1)
    rw [h7, zero_mul, sub_eq_zero, add_add_add_comm, Char2.add_self h0,
      add_zero, mul_add_one (Î± := R), â† mul_assoc, h6, zero_mul,
      zero_add, Char2.add_add_cancel h0, h5] at h8
    replace h6 : (c ^ 2) ^ 2 = c * (c * c ^ 2) := by rw [â† mul_assoc, â† sq, â† sq]
    rw [h6, mul_assoc, â† neg_eq_zero, â† A2 c h4, â† mul_assoc, â† h6]
    exact A1 _ h8.symm

/-- Lemma 7 -/
theorem case2_4_lem9 (x : R) :
    (x ^ 2 = 0 âˆ¨ x ^ 2 = 1) âˆ¨ f x + f (x + 1) = 1 âˆ§ x * (x + 1) + 1 = 0 := by
  have h4 := case2_4_lem8 h h0 h1 h2 h3
  refine (case2_4_lem7 h h0 h1 h3 x).imp ?_ (Î» h5 â†¦ ?_)
  Â· rw [mul_eq_zero, or_comm]
    refine Or.imp (Î» h5 â†¦ h4 _ <| ?_) (Î» h5 â†¦ ?_)
    Â· rw [case2_4_lem5 h h0 h1, h5, sq, zero_mul, zero_sub]
    Â· rw [â† Char2.add_eq_zero_iff_eq h0]
      apply h4; rw [case2_4_lem4 h h0 h1, h5, sq, zero_mul, zero_sub]
  Â· exact âŸ¨h5.1, h4 _ <| (case2_4_lem3 h h0 x).trans h5.2âŸ©

theorem case2_4_lem10 (h4 : âˆ€ x : R, x ^ 2 = 0 â†’ x = 0) (x : R) :
    (x = 0 âˆ¨ x = 1) âˆ¨ f x + f (x + 1) = 1 âˆ§ x * (x + 1) + 1 = 0 :=
  (case2_4_lem9 h h0 h1 h2 h3 x).imp_left <| Or.imp (h4 x) Î» h5 â†¦ by
    rw [â† Char2.add_eq_zero_iff_eq h0] at h5 âŠ¢
    exact h4 _ ((Char2.add_one_sq h0 x).trans h5)



/-- The main lemma for the `ğ”½â‚‚[X]/âŸ¨X^2âŸ©` subcase -/
theorem case2_4_ğ”½â‚‚Îµ_main_lemma {c : R} (h4 : c â‰  0) (h5 : c * c = 0) :
    âˆ€ x, (x = 0 âˆ¨ x = c) âˆ¨ x = 1 âˆ¨ x = c + 1 := by
  refine cases_of_nonperiod_quasi_period h h2 h1 (Î» x â†¦ ?_) h4
  have h6 := case2_4_lem5 h h0 h1 (c * x)
  rwa [mul_pow, sq, h5, zero_mul, h1, eq_comm,
    sub_eq_neg_self, sq_eq_zero_iff] at h6

/-- Solution for the current sub-subcase (`ğ”½â‚‚ÎµMap`) -/
theorem case2_4_ğ”½â‚‚Îµ_quot_IsAnswer {c : R} (h4 : c â‰  0) (h5 : c * c = 0) :
    IsAnswer f :=
  -- `ğ”½â‚‚[X]/âŸ¨X^2âŸ© â†’ R/I` induced by `X â†¦ c` is bijective
  have X :Function.Bijective (ğ”½â‚‚Îµ.castHom h0 h5) :=
    âŸ¨ğ”½â‚‚Îµ.castHom_injective _ _ h4,
    Î» x â†¦ by rcases case2_4_ğ”½â‚‚Îµ_main_lemma h h0 h1 h2 h4 h5 x
               with (h5 | h5) | (h5 | h5)
             exacts [âŸ¨ğ”½â‚‚Îµ.O, h5.symmâŸ©, âŸ¨ğ”½â‚‚Îµ.X, h5.symmâŸ©,
                     âŸ¨ğ”½â‚‚Îµ.I, h5.symmâŸ©, âŸ¨ğ”½â‚‚Îµ.Y, h5.symmâŸ©]âŸ©
  -- Factor `f` out as `ğ”½â‚‚ÎµMap âˆ˜ Ï€`
  let Ï€ := (RingEquiv.ofBijective _ X).symm
  suffices f = ğ”½â‚‚ÎµMap S âˆ˜ Ï€
    from this.symm â–¸ IsAnswer.ğ”½â‚‚Îµ_map_comp Ï€.toRingHom Ï€.surjective
  have h6 := good_map_one h
  (MulEquiv.eq_comp_symm _ _ _).mpr <| funext Î» x â†¦
    match x with
      | ğ”½â‚‚Îµ.O => h1
      | ğ”½â‚‚Îµ.I => h6
      | ğ”½â‚‚Îµ.X => by
          have h7 := case2_4_lem4 h h0 h1 c
          rw [sq, h5, zero_add, h6, eq_comm, sub_eq_zero, sq_eq_one_iff] at h7
          exact h7.resolve_right <| mt (case2_4_lem8 h h0 h1 h2 h3 c) h4
      | ğ”½â‚‚Îµ.Y => by
          have h7 := case2_4_lem5 h h0 h1 c
          rwa [sq, h5, h1, eq_comm, sub_eq_neg_self, sq_eq_zero_iff] at h7



/-- The main lemma for the `ğ”½â‚„` subcase -/
theorem case2_4_ğ”½â‚„_main_lemma
    (h4 : âˆ€ x : R, x ^ 2 = 0 â†’ x = 0) (h5 : c * (c + 1) = 1) (x : R) :
    (x = 0 âˆ¨ x = 1) âˆ¨ x = c âˆ¨ x = c + 1 := by
  have h7 := case2_4_lem10 h h0 h1 h2 h3 h4
  refine (h7 x).imp_right Î» h8 â†¦ (h7 (x + c)).elim ?_ ?_
  Â· exact Or.imp (Char2.add_eq_zero_iff_eq h0).mp (Char2.add_eq_iff_eq_add' h0).mp
  Â· rintro âŸ¨-, h9âŸ©
    rw [mul_add_one (x + c), â† sq, Char2.add_sq h0, add_add_add_comm, sq,
      sq, â† mul_add_one c, h5, â† mul_add_one x, h8.2, zero_add] at h9
    exact absurd h9 (one_ne_zero_of_map_zero h h1)

/-- Solution for the current sub-subcase (`ğ”½â‚„Map`) -/
theorem case2_4_ğ”½â‚„_quot_IsAnswer (h4 : âˆ€ x : R, x ^ 2 = 0 â†’ x = 0)
    (h5 : f c + f (c + 1) = 1) (h6 : c * (c + 1) = 1) : IsAnswer f :=
  -- `ğ”½â‚„ â†’ R/I` is bijective
  have X :Function.Bijective (ğ”½â‚„.castHom h0 h6) :=
    âŸ¨ğ”½â‚„.castHom_injective _ _ (one_ne_zero_of_map_zero h h1),
    Î» x â†¦ by rcases case2_4_ğ”½â‚„_main_lemma h h0 h1 h2 h3 h4 h6 x
               with (h7 | h7) | (h7 | h7)
             exacts [âŸ¨ğ”½â‚„.O, h7.symmâŸ©, âŸ¨ğ”½â‚„.I, h7.symmâŸ©,
                     âŸ¨ğ”½â‚„.X, h7.symmâŸ©, âŸ¨ğ”½â‚„.Y, h7.symmâŸ©]âŸ©
  -- Factor `f` out as `ğ”½â‚„Map âˆ˜ Ï€`
  let Ï€ := (RingEquiv.ofBijective _ X).symm
  have h7 := eq_sub_of_add_eq' h5
  suffices f = ğ”½â‚„Map S (f c) âˆ˜ Ï€
    from this.symm â–¸ IsAnswer.ğ”½â‚„_map_comp Ï€.toRingHom Ï€.surjective (f c) <|
      by rwa [â† h7, â† case2_4_lem3 h h0, h6, Char2.add_self h0]
  (MulEquiv.eq_comp_symm _ _ _).mpr <| funext Î» x â†¦
    match x with
      | ğ”½â‚„.O => h1
      | ğ”½â‚„.I => good_map_one h
      | ğ”½â‚„.X => rfl
      | ğ”½â‚„.Y => h7



/-- The main lemma for the `ğ”½â‚‚` subcase -/
theorem case2_4_ğ”½â‚‚_main_lemma (h4 : âˆ€ x : R, x ^ 2 = 0 â†’ x = 0)
    (h5 : Â¬âˆƒ c, f c + f (c + 1) = 1 âˆ§ c * (c + 1) = 1) (x : R) : x = 0 âˆ¨ x = 1 :=
  (case2_4_lem10 h h0 h1 h2 h3 h4 x).resolve_right Î» h6 â†¦
    h5 âŸ¨x, h6.1, (Char2.add_eq_zero_iff_eq h0).mp h6.2âŸ©

/-- Solution for the current sub-subcase (`ğ”½â‚‚Map`) -/
theorem case2_4_ğ”½â‚‚_quot_IsAnswer (h4 : âˆ€ x : R, x ^ 2 = 0 â†’ x = 0)
    (h5 : Â¬âˆƒ c, f c + f (c + 1) = 1 âˆ§ c * (c + 1) = 1) : IsAnswer f :=
  -- `ğ”½â‚‚ â†’ R/I` is bijective
  have X :Function.Bijective (ğ”½â‚‚.castHom h0) :=
    âŸ¨ğ”½â‚‚.castHom_injective _ (one_ne_zero_of_map_zero h h1),
    Î» x â†¦ by rcases case2_4_ğ”½â‚‚_main_lemma h h0 h1 h2 h3 h4 h5 x with h5 | h5
             exacts [âŸ¨ğ”½â‚‚.O, h5.symmâŸ©, âŸ¨ğ”½â‚‚.I, h5.symmâŸ©]âŸ©
  -- Factor `f` out as `ğ”½â‚‚Map âˆ˜ Ï€`
  let Ï€ := (RingEquiv.ofBijective _ X).symm
  suffices f = ğ”½â‚‚Map S âˆ˜ Ï€
    from this.symm â–¸ IsAnswer.ğ”½â‚‚_map_comp Ï€.toRingHom Ï€.surjective
  (MulEquiv.eq_comp_symm _ _ _).mpr <| funext Î» x â†¦
    match x with
      | ğ”½â‚‚.O => h1
      | ğ”½â‚‚.I => good_map_one h

end Rchar2



/-- Solution for the current subcase (4 classes) -/
theorem case2_4_quot_IsAnswer
    (h0 : f (-1) = 0) (h1 : f 2 = -1) (h2 : âˆ€ c âˆˆ periodIdeal h, c = 0) :
    IsAnswer f := by
  have h3 := h2 _ (case2_4_lem1 h h0 h1)
  have h4 : f 0 = -1 := h3 â–¸ h1
  by_cases h5 : (2 : S) = 0
  Â· exact case2_4_Schar2_quot_IsAnswer h h3 h4 h5
  by_cases h6 : âˆƒ x : R, Â¬(x ^ 2 = 0 â†’ x = 0)
  Â· rcases h6 with âŸ¨c, h6âŸ©; rw [not_imp] at h6
    exact case2_4_ğ”½â‚‚Îµ_quot_IsAnswer h h3 h4 h2 h5 h6.2 ((sq c).symm.trans h6.1)
  rw [â† not_forall, not_not] at h6
  rcases em (âˆƒ c, f c + f (c + 1) = 1 âˆ§ c * (c + 1) = 1) with âŸ¨c, h7, h8âŸ© | h7
  Â· exact case2_4_ğ”½â‚„_quot_IsAnswer h h3 h4 h2 h5 h6 h7 h8
  Â· exact case2_4_ğ”½â‚‚_quot_IsAnswer h h3 h4 h2 h5 h6 h7
