/-
Copyright (c) 2023 Gian Cordana Sanjaya. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Gian Cordana Sanjaya
-/

import IMOSLLean4.IMO2012.A5.A5Defs
import Mathlib.Algebra.GroupWithZero.Basic
import Mathlib.Algebra.Ring.Regular

/-!
# IMO 2012 A5 (Basic Results)

This file collects some basic results.
-/

namespace IMOSL
namespace IMO2012A5

variable [Ring R] [Ring Râ‚€] [Ring S]

/-- Given `f : R â†’ S` and `Ï† : Râ‚€ â†’+* R` surjective,
  `f` is good if `f âˆ˜ Ï†` is good. -/
theorem good_of_comp_hom_good_surjective {Ï† : Râ‚€ â†’+* R}
    (h : Function.Surjective Ï†) {f : R â†’ S} (h0 : good (f âˆ˜ Ï†.toFun)) :
    good f := Î» x y â†¦ by
  rcases h x with âŸ¨a, rflâŸ©
  rcases h y with âŸ¨b, rflâŸ©
  rw [â† Ï†.map_add, â† Ï†.map_mul, â† Ï†.map_one, â† Ï†.map_add]
  exact h0 a b

/-- Given `f : R â†’ S` and `Ï† : Râ‚€ â†’+* R` surjective,
  `f âˆ˜ Ï†` is an answer if `f` is an answer. -/
theorem IsAnswer_comp_hom {Ï† : Râ‚€ â†’+* R} (h : Ï†.toFun.Surjective)
    {f : R â†’ S} (h0 : IsAnswer f) : IsAnswer (f âˆ˜ Ï†.toFun) :=
  h0.recOn IsAnswer.of_zero
    (Î» Ï â†¦ IsAnswer.hom_sub_one (Ï.comp Ï†))
    (Î» Ï â†¦ IsAnswer.hom_sq_sub_one (Ï.comp Ï†))
    (Î» Ï h1 â†¦ IsAnswer.ğ”½â‚‚_map_comp (Ï.comp Ï†) (h1.comp h))
    (Î» Ï h1 â†¦ IsAnswer.ğ”½â‚ƒ_map1_comp (Ï.comp Ï†) (h1.comp h))
    (Î» Ï h1 â†¦ IsAnswer.ğ”½â‚ƒ_map2_comp (Ï.comp Ï†) (h1.comp h))
    (Î» Ï h1 â†¦ IsAnswer.â„¤â‚„_map_comp (Ï.comp Ï†) (h1.comp h))
    (Î» Ï h1 â†¦ IsAnswer.ğ”½â‚‚Îµ_map_comp (Ï.comp Ï†) (h1.comp h))
    (Î» Ï h1 â†¦ IsAnswer.ğ”½â‚„_map_comp (Ï.comp Ï†) (h1.comp h))



variable [IsDomain S] {f : R â†’ S} (h : good f)

theorem good_map_one : f 1 = 0 := by
  rw [â† mul_self_eq_zero, â† h, mul_one, sub_self]

theorem neg_map_zero_mul (x : R) : -f 0 * f x = f x := by
  rw [neg_mul, neg_eq_iff_eq_neg, â† h, zero_mul,
    zero_add, good_map_one h, zero_sub, zero_add]

/-- (1.1) -/
theorem eq_zero_of_map_zero_ne_neg_one (h0 : f 0 â‰  -1) : f = 0 :=
  funext Î» x â†¦ (mul_left_eq_selfâ‚€.mp <| neg_map_zero_mul h x).resolve_left
    Î» h1 â†¦ h0 <| neg_eq_iff_eq_neg.mp h1

theorem one_ne_zero_of_map_zero (h0 : f 0 = -1) : (1 : R) â‰  0 :=
  mt (congr_arg f) <| by rw [good_map_one h, h0, zero_eq_neg]; exact one_ne_zero

/-- (1.2) -/
theorem map_neg_sub_map1 (x : R) : f (1 - x) - f (x - 1) = f x * f (-1) := by
  rw [â† h, mul_neg_one, neg_add_eq_sub, â† sub_eq_add_neg]

/-- (1.3) -/
theorem map_neg_sub_map2 (x : R) : f (-x) - f x = f (x + 1) * f (-1) := by
  rw [â† map_neg_sub_map1 h, sub_add_cancel'', add_sub_cancel]

/-- Auxiliary lemma for two sub-cases -/
theorem eq_hom_sub_one_of (h0 : âˆ€ x y, f (x + y) = f x + f y + 1) :
    âˆƒ Ï† : R â†’+* S, f = (Ï†.toFun Â· - 1) :=
  let g := (f Â· + 1)
  have h1 : f 1 = 0 := good_map_one h
  have h2 : g 1 = 1 := add_left_eq_self.mpr h1
  have h3 : âˆ€ x y, g (x + y) = g x + g y := Î» x y â†¦ by
    rw [add_add_add_comm, â† add_assoc, â† h0]
  have h4 : âˆ€ x y, g (x * y) = g x * g y := Î» x y â†¦ by
    rw [add_one_mul (f x), mul_add_one (f x), add_assoc, â† add_assoc (f x),
      â† h0, â† h, sub_add_cancel, h0, add_assoc, h1, zero_add]
  âŸ¨RingHom.mk' âŸ¨âŸ¨g, h2âŸ©, h4âŸ© h3, funext Î» x â†¦ (add_sub_cancel (f x) 1).symmâŸ©

/-- Corollary of the previous result -/
theorem IsAnswer_of_add_one_additive (h0 : âˆ€ x y, f (x + y) = f x + f y + 1) :
    IsAnswer f := by
  rcases eq_hom_sub_one_of h h0 with âŸ¨Ï†, rflâŸ©
  exact IsAnswer.hom_sub_one Ï†
