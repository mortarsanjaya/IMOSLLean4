/-
Copyright (c) 2024 Gian Cordana Sanjaya. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Gian Cordana Sanjaya
-/

import IMOSLLean4.Extra.CharTwo.Finset

/-!
# Explicit construction of `ğ”½â‚‚[X]`

In this file, we explicitly construct the ring `ğ”½â‚‚[X]`.
We prove that it is a ring, and we construct ring homomorphisms from `ğ”½â‚‚[X]`.
The explicit construction is done so that proof of equality between
  two expressions involving explicit elements can be done with just `rfl`.

### Implementation details

We implement `ğ”½â‚‚[X]` as a wrapper around `Finset â„•`.
Addition is done by symmetric difference.
Multiplication by `X` is done by adding `1` to every element.
-/

namespace IMOSL
namespace IMO2012A5

open Extra

@[ext] structure ğ”½â‚‚X where
  toFinset : Finset â„•


namespace ğ”½â‚‚X

/-- Get an element of `ğ”½â‚‚[X]` from a `Finset â„•`.
  This might be preferred over manually typing the polynomial for larger ones. -/
def ofFinset (S : Finset â„•) : ğ”½â‚‚X := âŸ¨SâŸ©

instance : Coe (Finset â„•) ğ”½â‚‚X := âŸ¨ofFinsetâŸ©

/-- `X^n`, preferred over `X ^ n`. -/
def Xpow (n : â„•) : ğ”½â‚‚X := âŸ¨{n}âŸ©

instance : Zero ğ”½â‚‚X := âŸ¨âŸ¨âˆ…âŸ©âŸ©
instance : One ğ”½â‚‚X := âŸ¨âŸ¨{0}âŸ©âŸ©
instance : Neg ğ”½â‚‚X := âŸ¨idâŸ©

def X : ğ”½â‚‚X := âŸ¨{1}âŸ©

instance : DecidableEq ğ”½â‚‚X := Î» P Q â†¦ match decEq P.toFinset Q.toFinset with
  | isTrue h => isTrue (ğ”½â‚‚X.ext h)
  | isFalse h => isFalse Î» h0 â†¦ h (congrArg toFinset h0)





/-! ### Addition -/

protected def add : ğ”½â‚‚X â†’ ğ”½â‚‚X â†’ ğ”½â‚‚X
  | âŸ¨SâŸ©, âŸ¨TâŸ© => âŸ¨symmDiff S TâŸ©

instance : Add ğ”½â‚‚X := âŸ¨ğ”½â‚‚X.addâŸ©

protected lemma add_toFinset (P Q : ğ”½â‚‚X) :
    (P + Q).toFinset = symmDiff P.toFinset Q.toFinset := rfl

protected lemma zero_add (P : ğ”½â‚‚X) : 0 + P = P :=
  ğ”½â‚‚X.ext (bot_symmDiff _)

protected lemma add_zero (P : ğ”½â‚‚X) : P + 0 = P :=
  ğ”½â‚‚X.ext (symmDiff_bot _)

protected lemma add_comm (P Q : ğ”½â‚‚X) : P + Q = Q + P :=
  ğ”½â‚‚X.ext (symmDiff_comm _ _)

protected lemma add_assoc (P Q R : ğ”½â‚‚X) : P + Q + R = P + (Q + R) :=
  ğ”½â‚‚X.ext (symmDiff_assoc _ _ _)

protected lemma add_self_eq_zero (P : ğ”½â‚‚X) : P + P = 0 :=
  ğ”½â‚‚X.ext (symmDiff_self _)

instance : AddCommGroup ğ”½â‚‚X where
  zero_add := ğ”½â‚‚X.zero_add
  add_zero := ğ”½â‚‚X.add_zero
  add_comm := ğ”½â‚‚X.add_comm
  add_assoc := ğ”½â‚‚X.add_assoc
  add_left_neg := ğ”½â‚‚X.add_self_eq_zero
  nsmul := nsmulRec
  zsmul := zsmulRec

instance : CharTwo ğ”½â‚‚X := âŸ¨ğ”½â‚‚X.add_self_eq_zeroâŸ©





/-! ### Induction principles -/

protected theorem poly_induction {p : ğ”½â‚‚X â†’ Prop} (zero : p 0)
    (add_Xpow : âˆ€ (n : â„•) {P : ğ”½â‚‚X}, p P â†’ p (Xpow n + P)) (P : ğ”½â‚‚X) : p P :=
  P.toFinset.induction (p := Î» S â†¦ p (ofFinset S)) zero Î» n _ h h0 â†¦
    symmDiff_singleton_eq_insert h â–¸ add_Xpow n h0

protected theorem Xpow_add_induction {p : ğ”½â‚‚X â†’ Prop}
    (of_Xpow : âˆ€ n : â„•, p (Xpow n)) (add : âˆ€ {P Q}, p P â†’ p Q â†’ p (P + Q)) :
    âˆ€ P, p P :=
  ğ”½â‚‚X.poly_induction (let h := of_Xpow 0; add h h) Î» n _ â†¦ add (of_Xpow n)





/-! ### Multiplication by powers of `X` -/

/-- Given `n` and `P(X)`, compute `X^n P(X)` -/
def XpowMul (n : â„•) (P : ğ”½â‚‚X) : ğ”½â‚‚X :=
  âŸ¨P.toFinset.image Î» k â†¦ k + nâŸ©

lemma XpowMul_nat_zero (P : ğ”½â‚‚X) : P.XpowMul 0 = P :=
  ğ”½â‚‚X.ext Finset.image_id

lemma XpowMul_ğ”½â‚‚X_zero (n : â„•) : XpowMul n 0 = 0 :=
  ğ”½â‚‚X.ext (Finset.image_empty _)

lemma XpowMul_ğ”½â‚‚X_one (n : â„•) : XpowMul n 1 = Xpow n :=
  ğ”½â‚‚X.ext ((Finset.image_singleton _ _).trans (congrArg _ n.zero_add))

lemma XpowMul_Xpow (m n : â„•) : (Xpow m).XpowMul n = Xpow (m + n) := rfl

lemma XpowMul_nat_add (m n : â„•) (P : ğ”½â‚‚X) :
    P.XpowMul (m + n) = (P.XpowMul m).XpowMul n := by
  unfold XpowMul; rw [ğ”½â‚‚X.ext_iff, eq_comm, Finset.image_image, comp_add_right]

lemma XpowMul_ğ”½â‚‚X_add (n : â„•) (P Q : ğ”½â‚‚X) :
    (P + Q).XpowMul n = P.XpowMul n + Q.XpowMul n :=
  ğ”½â‚‚X.ext (Finset.image_symmDiff _ _ (add_left_injective n))

lemma XpowMul_sum (n : â„•) [DecidableEq Î¹] (f : Î¹ â†’ ğ”½â‚‚X) (S : Finset Î¹) :
    (S.sum f).XpowMul n = S.sum Î» i â†¦ (f i).XpowMul n :=
  S.induction rfl Î» i S h h0 â†¦ by
    rw [Finset.sum_insert h, Finset.sum_insert h, XpowMul_ğ”½â‚‚X_add, h0]

lemma sum_Xpow_eq_ofFinset : âˆ€ S : Finset â„•, S.sum Xpow = ofFinset S :=
  Finset.induction rfl Î» i S h h0 â†¦ by
    rw [Finset.sum_insert h, h0]
    exact ğ”½â‚‚X.ext (symmDiff_singleton_eq_insert h)

lemma toFinset_sum_Xpow_eq_self (P : ğ”½â‚‚X) : P.toFinset.sum Xpow = P :=
  ğ”½â‚‚X.sum_Xpow_eq_ofFinset P.toFinset





/-! ### Multiplication -/

protected def mul (P Q : ğ”½â‚‚X) : ğ”½â‚‚X :=
  P.toFinset.sum Q.XpowMul

instance : Mul ğ”½â‚‚X := âŸ¨ğ”½â‚‚X.mulâŸ©

protected lemma mul_def (P Q : ğ”½â‚‚X) : P * Q = P.toFinset.sum Q.XpowMul := rfl

protected lemma zero_mul (P : ğ”½â‚‚X) : 0 * P = 0 :=
  Finset.sum_empty (f := P.XpowMul)

protected lemma mul_zero (P : ğ”½â‚‚X) : P * 0 = 0 :=
  Finset.sum_eq_zero Î» n _ â†¦ XpowMul_ğ”½â‚‚X_zero n

protected lemma one_mul (P : ğ”½â‚‚X) : 1 * P = P :=
  (Finset.sum_singleton _ _).trans (ğ”½â‚‚X.XpowMul_nat_zero P)

protected lemma mul_one (P : ğ”½â‚‚X) : P * 1 = P :=
  (Finset.sum_congr rfl Î» n _ â†¦ XpowMul_ğ”½â‚‚X_one n).trans
    P.toFinset_sum_Xpow_eq_self

protected lemma add_mul (P Q R : ğ”½â‚‚X) : (P + Q) * R = P * R + Q * R :=
  CharTwo.symmDiff_sum_eq _ _ _

protected lemma mul_add (P Q R : ğ”½â‚‚X) : P * (Q + R) = P * Q + P * R :=
  (Finset.sum_congr rfl Î» n _ â†¦ XpowMul_ğ”½â‚‚X_add n Q R).trans
    Finset.sum_add_distrib

lemma XpowMul_eq_Xpow_mul (n : â„•) (P : ğ”½â‚‚X) : P.XpowMul n = Xpow n * P :=
  (Finset.sum_singleton P.XpowMul n).symm

lemma Xpow_add (k m : â„•) : Xpow (k + m) = Xpow k * Xpow m := by
  rw [â† XpowMul_eq_Xpow_mul, XpowMul_Xpow, add_comm]

lemma XpowMul_eq_mul_Xpow (n : â„•) : âˆ€ P : ğ”½â‚‚X, P.XpowMul n = P * Xpow n :=
  ğ”½â‚‚X.Xpow_add_induction
    (Î» k â†¦ by rw [XpowMul_Xpow, Xpow_add])
    (Î» h h0 â†¦ by rw [XpowMul_ğ”½â‚‚X_add, ğ”½â‚‚X.add_mul, h, h0])

protected lemma mul_comm (P : ğ”½â‚‚X) : âˆ€ Q, P * Q = Q * P :=
  ğ”½â‚‚X.Xpow_add_induction
    (Î» n â†¦ by rw [â† XpowMul_eq_Xpow_mul, XpowMul_eq_mul_Xpow])
    (Î» h h0 â†¦ by rw [ğ”½â‚‚X.add_mul, ğ”½â‚‚X.mul_add, h, h0])

lemma mul_XpowMul_left (n : â„•) (P : ğ”½â‚‚X) : âˆ€ Q, P.XpowMul n * Q = (P * Q).XpowMul n :=
  ğ”½â‚‚X.Xpow_add_induction
    (Î» k â†¦ by rw [â† XpowMul_eq_mul_Xpow, â† XpowMul_eq_mul_Xpow,
      â† XpowMul_nat_add, â† XpowMul_nat_add, Nat.add_comm])
    (Î» h h0 â†¦ by rw [ğ”½â‚‚X.mul_add, ğ”½â‚‚X.mul_add, h, h0, XpowMul_ğ”½â‚‚X_add])

lemma mul_XpowMul_right (n : â„•) (P Q : ğ”½â‚‚X) : P * Q.XpowMul n = (P * Q).XpowMul n := by
  rw [P.mul_comm, mul_XpowMul_left, Q.mul_comm]

protected lemma mul_assoc (P Q : ğ”½â‚‚X) : âˆ€ R, P * Q * R = P * (Q * R) :=
  ğ”½â‚‚X.Xpow_add_induction
    (Î» n â†¦ by rw [â† XpowMul_eq_mul_Xpow, â† XpowMul_eq_mul_Xpow, mul_XpowMul_right])
    (Î» h h0 â†¦ by rw [ğ”½â‚‚X.mul_add, ğ”½â‚‚X.mul_add, ğ”½â‚‚X.mul_add, h, h0])





/-! ### Square -/

def square (P : ğ”½â‚‚X) : ğ”½â‚‚X := âŸ¨P.toFinset.image Î» n â†¦ 2 * nâŸ©

theorem square_zero : square 0 = 0 := rfl

theorem square_one : square 1 = 1 := rfl

theorem square_add (P Q : ğ”½â‚‚X) : square (P + Q) = square P + square Q :=
  ğ”½â‚‚X.ext (Finset.image_symmDiff _ _ Î» _ _ â†¦ (Nat.mul_right_inj (Nat.succ_ne_zero 1)).mp)

theorem square_add_one (P : ğ”½â‚‚X) : square (P + 1) = square P + 1 :=
  P.square_add 1

theorem square_Xpow (n : â„•) : (Xpow n).square = Xpow (2 * n) := rfl

theorem square_eq_mul_self : âˆ€ P : ğ”½â‚‚X, square P = P * P :=
  ğ”½â‚‚X.poly_induction rfl Î» n P h â†¦ by
    rw [square_add, square_Xpow, ğ”½â‚‚X.add_mul, ğ”½â‚‚X.mul_add, â† Xpow_add, Nat.two_mul,
      ğ”½â‚‚X.mul_add, â† h, â† P.mul_comm, CharTwo.add_add_add_cancel_middle]

theorem square_XpowMul (n : â„•) (P : ğ”½â‚‚X) :
    (P.XpowMul n).square = P.square.XpowMul (2 * n) := by
  unfold square XpowMul; rw [Finset.image_image, Finset.image_image]
  exact ğ”½â‚‚X.ext (congrArg P.toFinset.image <| funext Î» n â†¦ Nat.mul_add 2 _ _)

theorem square_mul (P : ğ”½â‚‚X) : âˆ€ Q, square (P * Q) = square P * square Q :=
  ğ”½â‚‚X.Xpow_add_induction
    (Î» n â†¦ by rw [â† XpowMul_eq_mul_Xpow, square_XpowMul,
      square_Xpow, XpowMul_eq_mul_Xpow])
    (Î» h h0 â†¦ by rw [P.mul_add, square_add, h, h0, square_add, ğ”½â‚‚X.mul_add])





/-! ### Modified, more efficient power -/

protected def natPow (P : ğ”½â‚‚X) (n : â„•) : ğ”½â‚‚X :=
  if n = 0 then 1 else
    let Q := ğ”½â‚‚X.natPow (square P) (n / 2)
    if n % 2 = 0 then Q else Q * P
termination_by n
decreasing_by apply Nat.bitwise_rec_lemma; assumption

protected lemma natPow_zero (P : ğ”½â‚‚X) : P.natPow 0 = 1 := by
  rw [ğ”½â‚‚X.natPow, if_pos rfl]

protected lemma natPow_of_ne_zero (P : ğ”½â‚‚X) (h : n â‰  0) :
    P.natPow n = if n % 2 = 0 then ğ”½â‚‚X.natPow (square P) (n / 2)
      else ğ”½â‚‚X.natPow (square P) (n / 2) * P := by
  rw [ğ”½â‚‚X.natPow, if_neg h]

protected lemma natPow_two_mul (P : ğ”½â‚‚X) (n : â„•) :
    P.natPow (2 * n) = (square P).natPow n := by
  rcases Decidable.eq_or_ne n 0 with rfl | h
  Â· rw [Nat.mul_zero, ğ”½â‚‚X.natPow_zero, ğ”½â‚‚X.natPow_zero]
  Â· have h0 : 0 < 2 := Nat.two_pos
    rw [P.natPow_of_ne_zero (Nat.mul_ne_zero h0.ne.symm h),
      if_pos (Nat.mul_mod_right _ _), Nat.mul_div_right _ h0]

protected lemma natPow_two_mul_add_one (P : ğ”½â‚‚X) (n : â„•) :
    P.natPow (2 * n + 1) = (square P).natPow n * P := by
  rw [P.natPow_of_ne_zero (2 * n).add_one_ne_zero, Nat.mul_add_mod,
    Nat.mul_add_div Nat.two_pos, if_neg Nat.one_ne_zero,
    Nat.div_eq_of_lt Nat.one_lt_two, Nat.add_zero]

protected lemma natPow_one (P : ğ”½â‚‚X) : P.natPow 1 = P := by
  rw [ğ”½â‚‚X.natPow, if_neg Nat.one_ne_zero, ğ”½â‚‚X.natPow_zero,
    if_neg Nat.one_ne_zero, ğ”½â‚‚X.one_mul]

protected lemma natPow_two (P : ğ”½â‚‚X) : P.natPow 2 = square P := by
  rw [ğ”½â‚‚X.natPow, if_neg (Nat.succ_ne_zero 1), ğ”½â‚‚X.natPow_one, if_pos rfl]

protected lemma natPow_succ (P : ğ”½â‚‚X) (n : â„•) : P.natPow n.succ = P.natPow n * P := by
  rw [â† n.div_add_mod 2]; rcases n.mod_two_eq_zero_or_one with h0 | h0
  Â· rw [h0, Nat.add_zero, ğ”½â‚‚X.natPow_two_mul_add_one, ğ”½â‚‚X.natPow_two_mul]
  Â· rw [h0, Nat.succ_eq_add_one, â† Nat.mul_succ 2, ğ”½â‚‚X.natPow_two_mul,
      ğ”½â‚‚X.natPow_two_mul_add_one, ğ”½â‚‚X.mul_assoc, â† P.square_eq_mul_self]
    exact (square P).natPow_succ (n / 2)
termination_by n
decreasing_by apply Nat.bitwise_rec_lemma
              rintro rfl; exact absurd h0.symm Nat.one_ne_zero
